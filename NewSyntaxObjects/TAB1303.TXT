OBJECT Table 1303 Mini Vendor Template
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160126.330T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    OnInsert=VAR
               FieldRefArray@1001 : ARRAY [20] OF FieldRef;
               RecRef@1000 : RecordRef;
             BEGIN
               TESTFIELD("Template Name");
               RecRef.GETTABLE(Rec);
               CreateFieldRefArray(FieldRefArray,RecRef);

               InsertConfigurationTemplateHeaderAndLines;
             END;

    OnModify=VAR
               FieldRefArray@1000 : ARRAY [20] OF FieldRef;
               RecRef@1001 : RecordRef;
             BEGIN
               TESTFIELD(Code);
               TESTFIELD("Template Name");
               RecRef.GETTABLE(Rec);
               CreateFieldRefArray(FieldRefArray,RecRef);
               ConfigTemplateManagement.UpdateConfigTemplateAndLines(Code,"Template Name",DATABASE::Vendor,FieldRefArray);
             END;

    OnDelete=VAR
               ConfigTemplateHeader@1000 : Record "Config. Template Header";
             BEGIN
               if ConfigTemplateHeader.GET(Code) then begin
                 ConfigTemplateManagement.DeleteRelatedTemplates(Code,DATABASE::"Default Dimension");
                 ConfigTemplateHeader.DELETE(true);
               end;
             END;

    CaptionML=ENU=Mini Vendor Template;
  }
  FIELDS
  {
    { 1   ;   ;Key                 ;Integer       ;AutoIncrement=true;
                                                   CaptionML=ENU=Key }
    { 2   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 3   ;   ;Template Name       ;Text50        ;CaptionML=ENU=Template Name;
                                                   NotBlank=true }
    { 7   ;   ;City                ;Text30        ;TableRelation=IF ("Country/Region Code"=CONST('')) "Post Code".City
                                                                 ELSE IF ("Country/Region Code"=FILTER(<>'')) "Post Code".City WHERE ("Country/Region Code"=FIELD("Country/Region Code"));
                                                   OnValidate=VAR
                                                                PostCodeRec@1000 : Record "Post Code";
                                                              BEGIN
                                                                if City <> '' then begin
                                                                  PostCodeRec.SETFILTER("Search City",City);

                                                                  case PostCodeRec.COUNT of
                                                                    0:
                                                                      exit;
                                                                    1:
                                                                      PostCodeRec.FINDFIRST;
                                                                    else
                                                                      if PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK then
                                                                        exit;
                                                                  end;

                                                                  "Post Code" := PostCodeRec.Code;
                                                                  "Country/Region Code" := PostCodeRec."Country/Region Code";
                                                                end;
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=City }
    { 21  ;   ;Vendor Posting Group;Code20        ;TableRelation="Vendor Posting Group";
                                                   CaptionML=ENU=Vendor Posting Group }
    { 22  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code }
    { 24  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=ENU=Language Code }
    { 27  ;   ;Payment Terms Code  ;Code10        ;TableRelation="Payment Terms";
                                                   CaptionML=ENU=Payment Terms Code }
    { 28  ;   ;Fin. Charge Terms Code;Code10      ;TableRelation="Finance Charge Terms";
                                                   CaptionML=ENU=Fin. Charge Terms Code }
    { 33  ;   ;Invoice Disc. Code  ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=ENU=Invoice Disc. Code }
    { 35  ;   ;Country/Region Code ;Code10        ;TableRelation="Country/Region";
                                                   CaptionML=ENU=Country/Region Code }
    { 47  ;   ;Payment Method Code ;Code10        ;TableRelation="Payment Method";
                                                   CaptionML=ENU=Payment Method Code }
    { 80  ;   ;Application Method  ;Option        ;CaptionML=ENU=Application Method;
                                                   OptionCaptionML=ENU=Manual,Apply to Oldest;
                                                   OptionString=Manual,"Apply to Oldest" }
    { 82  ;   ;Prices Including VAT;Boolean       ;CaptionML=ENU=Prices Including Tax }
    { 88  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 91  ;   ;Post Code           ;Code20        ;TableRelation=IF ("Country/Region Code"=CONST('')) "Post Code"
                                                                 ELSE IF ("Country/Region Code"=FILTER(<>'')) "Post Code" WHERE ("Country/Region Code"=FIELD("Country/Region Code"));
                                                   OnValidate=VAR
                                                                PostCodeRec@1000 : Record "Post Code";
                                                              BEGIN
                                                                if "Post Code" <> '' then begin
                                                                  PostCodeRec.SETFILTER(Code,"Post Code");

                                                                  case PostCodeRec.COUNT of
                                                                    0:
                                                                      exit;
                                                                    1:
                                                                      PostCodeRec.FINDFIRST;
                                                                    else
                                                                      if PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK then
                                                                        exit;
                                                                  end;

                                                                  City := PostCodeRec.City;
                                                                  "Country/Region Code" := PostCodeRec."Country/Region Code";
                                                                end;
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=ZIP Code }
    { 92  ;   ;County              ;Text30        ;CaptionML=ENU=State }
    { 109 ;   ;Tax Liable          ;Boolean       ;CaptionML=ENU=Tax Liable }
    { 110 ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=Tax Bus. Posting Group }
    { 116 ;   ;Block Payment Tolerance;Boolean    ;CaptionML=ENU=Block Payment Tolerance }
    { 7602;   ;Validate EU Vat Reg. No.;Boolean   ;CaptionML=ENU=Validate EU Tax Reg. No. }
    { 10018;  ;Check Date Format   ;Option        ;CaptionML=ENU=Check Date Format;
                                                   OptionCaptionML=ENU=" ,MM DD YYYY,DD MM YYYY,YYYY MM DD";
                                                   OptionString=" ","MM DD YYYY","DD MM YYYY","YYYY MM DD" }
    { 10019;  ;Check Date Separator;Option        ;CaptionML=ENU=Check Date Separator;
                                                   OptionCaptionML=ENU=" ,-,.,/";
                                                   OptionString=" ","-",".","/" }
  }
  KEYS
  {
    {    ;Key                                     ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConfigTemplateManagement@1000 : Codeunit "Config. Template Management";

    [External]
    PROCEDURE CreateFieldRefArray@12(VAR FieldRefArray@1000 : ARRAY [20] OF FieldRef;RecRef@1003 : RecordRef);
    VAR
      I@1002 : Integer;
    BEGIN
      I := 1;

      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO(City)));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Vendor Posting Group")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Currency Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Language Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Payment Terms Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Fin. Charge Terms Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Invoice Disc. Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Country/Region Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Payment Method Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Application Method")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Prices Including VAT")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Gen. Bus. Posting Group")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Post Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO(County)));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Tax Liable")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("VAT Bus. Posting Group")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Block Payment Tolerance")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Check Date Format")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Check Date Separator")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Validate EU Vat Reg. No.")));
      OnAfterCreateFieldRefArray(FieldRefArray,RecRef);
    END;

    LOCAL PROCEDURE AddToArray@4(VAR FieldRefArray@1000 : ARRAY [21] OF FieldRef;VAR I@1001 : Integer;CurrFieldRef@1002 : FieldRef);
    BEGIN
      FieldRefArray[I] := CurrFieldRef;
      I += 1;
    END;

    [External]
    PROCEDURE InitializeTempRecordFromConfigTemplate@1(VAR TempMiniVendorTemplate@1000 : TEMPORARY Record "Mini Vendor Template";ConfigTemplateHeader@1001 : Record "Config. Template Header");
    VAR
      RecRef@1003 : RecordRef;
    BEGIN
      TempMiniVendorTemplate.INIT;
      TempMiniVendorTemplate.Code := ConfigTemplateHeader.Code;
      TempMiniVendorTemplate."Template Name" := ConfigTemplateHeader.Description;
      TempMiniVendorTemplate.INSERT;

      RecRef.GETTABLE(TempMiniVendorTemplate);

      ConfigTemplateManagement.ApplyTemplateLinesWithoutValidation(ConfigTemplateHeader,RecRef);

      RecRef.SETTABLE(TempMiniVendorTemplate);
    END;

    [External]
    PROCEDURE CreateConfigTemplateFromExistingVendor@5(Vendor@1000 : Record Vendor;VAR TempMiniVendorTemplate@1001 : TEMPORARY Record "Mini Vendor Template");
    VAR
      DimensionsTemplate@1002 : Record "Dimensions Template";
      ConfigTemplateHeader@1006 : Record "Config. Template Header";
      RecRef@1005 : RecordRef;
      FieldRefArray@1004 : ARRAY [20] OF FieldRef;
      NewTemplateCode@1003 : Code[10];
    BEGIN
      RecRef.GETTABLE(Vendor);
      CreateFieldRefArray(FieldRefArray,RecRef);

      ConfigTemplateManagement.CreateConfigTemplateAndLines(NewTemplateCode,'',DATABASE::Vendor,FieldRefArray);
      ConfigTemplateHeader.GET(NewTemplateCode);
      DimensionsTemplate.CreateTemplatesFromExistingMasterRecord(Vendor."No.",NewTemplateCode,DATABASE::Vendor);
      InitializeTempRecordFromConfigTemplate(TempMiniVendorTemplate,ConfigTemplateHeader);
    END;

    [External]
    PROCEDURE SaveAsTemplate@7(Vendor@1000 : Record Vendor);
    VAR
      TempMiniVendorTemplate@1001 : TEMPORARY Record "Mini Vendor Template";
      VendorTemplateCard@1002 : Page "Vendor Template Card";
    BEGIN
      VendorTemplateCard.CreateFromVend(Vendor);
      VendorTemplateCard.SETRECORD(TempMiniVendorTemplate);
      VendorTemplateCard.LOOKUPMODE := true;
      if VendorTemplateCard.RUNMODAL = ACTION::LookupOK then;
    END;

    LOCAL PROCEDURE InsertConfigurationTemplateHeaderAndLines@2();
    VAR
      FieldRefArray@1001 : ARRAY [20] OF FieldRef;
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      CreateFieldRefArray(FieldRefArray,RecRef);
      ConfigTemplateManagement.CreateConfigTemplateAndLines(Code,"Template Name",DATABASE::Vendor,FieldRefArray);
    END;

    [External]
    PROCEDURE NewVendorFromTemplate@3(VAR Vendor@1002 : Record Vendor) : Boolean;
    VAR
      ConfigTemplateHeader@1004 : Record "Config. Template Header";
      ConfigTemplates@1001 : Page "Config Templates";
    BEGIN
      ConfigTemplateHeader.SETRANGE("Table ID",DATABASE::Vendor);
      ConfigTemplateHeader.SETRANGE(Enabled,true);

      if ConfigTemplateHeader.COUNT = 1 then begin
        ConfigTemplateHeader.FINDFIRST;
        InsertVendorFromTemplate(ConfigTemplateHeader,Vendor);
        exit(true);
      end;

      if (ConfigTemplateHeader.COUNT > 1) and GUIALLOWED then begin
        ConfigTemplates.SETTABLEVIEW(ConfigTemplateHeader);
        ConfigTemplates.LOOKUPMODE(true);
        ConfigTemplates.SetNewMode;
        if ConfigTemplates.RUNMODAL = ACTION::LookupOK then begin
          ConfigTemplates.GETRECORD(ConfigTemplateHeader);
          InsertVendorFromTemplate(ConfigTemplateHeader,Vendor);
          exit(true);
        end;
      end;

      exit(false);
    END;

    [External]
    PROCEDURE UpdateVendorFromTemplate@8(VAR Vendor@1000 : Record Vendor);
    VAR
      ConfigTemplateHeader@1001 : Record "Config. Template Header";
      DimensionsTemplate@1004 : Record "Dimensions Template";
      ConfigTemplates@1003 : Page "Config Templates";
      VendorRecRef@1002 : RecordRef;
    BEGIN
      if GUIALLOWED then begin
        ConfigTemplateHeader.SETRANGE("Table ID",DATABASE::Vendor);
        ConfigTemplateHeader.SETRANGE(Enabled,true);
        ConfigTemplates.SETTABLEVIEW(ConfigTemplateHeader);
        ConfigTemplates.LOOKUPMODE(true);
        if ConfigTemplates.RUNMODAL = ACTION::LookupOK then begin
          ConfigTemplates.GETRECORD(ConfigTemplateHeader);
          VendorRecRef.GETTABLE(Vendor);
          ConfigTemplateManagement.UpdateRecord(ConfigTemplateHeader,VendorRecRef);
          DimensionsTemplate.InsertDimensionsFromTemplates(ConfigTemplateHeader,Vendor."No.",DATABASE::Vendor);
          VendorRecRef.SETTABLE(Vendor);
          Vendor.FIND;
        end;
      end;
    END;

    [External]
    PROCEDURE InsertVendorFromTemplate@16(ConfigTemplateHeader@1006 : Record "Config. Template Header";VAR Vendor@1003 : Record Vendor);
    VAR
      DimensionsTemplate@1001 : Record "Dimensions Template";
      ConfigTemplateMgt@1002 : Codeunit "Config. Template Management";
      RecRef@1000 : RecordRef;
    BEGIN
      Vendor.SetInsertFromTemplate(true);
      InitVendorNo(Vendor,ConfigTemplateHeader);
      Vendor.INSERT(true);
      RecRef.GETTABLE(Vendor);
      ConfigTemplateMgt.UpdateRecord(ConfigTemplateHeader,RecRef);
      RecRef.SETTABLE(Vendor);

      DimensionsTemplate.InsertDimensionsFromTemplates(ConfigTemplateHeader,Vendor."No.",DATABASE::Vendor);
      Vendor.FIND;
    END;

    [External]
    PROCEDURE UpdateVendorsFromTemplate@6(VAR Vendor@1000 : Record Vendor);
    VAR
      ConfigTemplateHeader@1001 : Record "Config. Template Header";
      DimensionsTemplate@1004 : Record "Dimensions Template";
      ConfigTemplates@1003 : Page "Config Templates";
      VendorRecRef@1002 : RecordRef;
    BEGIN
      if GUIALLOWED then begin
        ConfigTemplateHeader.SETRANGE("Table ID",DATABASE::Vendor);
        ConfigTemplateHeader.SETRANGE(Enabled,true);
        ConfigTemplates.SETTABLEVIEW(ConfigTemplateHeader);
        ConfigTemplates.LOOKUPMODE(true);
        if ConfigTemplates.RUNMODAL = ACTION::LookupOK then begin
          ConfigTemplates.GETRECORD(ConfigTemplateHeader);
          VendorRecRef.GETTABLE(Vendor);
          if VendorRecRef.FINDSET then
            repeat
              ConfigTemplateManagement.UpdateRecord(ConfigTemplateHeader,VendorRecRef);
              DimensionsTemplate.InsertDimensionsFromTemplates(ConfigTemplateHeader,Vendor."No.",DATABASE::Vendor);
            until VendorRecRef.NEXT = 0;
          VendorRecRef.SETTABLE(Vendor);
        end;
      end;
    END;

    LOCAL PROCEDURE InitVendorNo@9(VAR Vendor@1000 : Record Vendor;ConfigTemplateHeader@1004 : Record "Config. Template Header");
    VAR
      NoSeriesMgt@1002 : Codeunit NoSeriesManagement;
    BEGIN
      if ConfigTemplateHeader."Instance No. Series" = '' then
        exit;
      NoSeriesMgt.InitSeries(ConfigTemplateHeader."Instance No. Series",'',0D,Vendor."No.",Vendor."No. Series");
    END;

    [Integration]
    PROCEDURE OnAfterCreateFieldRefArray@10(VAR FieldRefArray@1000 : ARRAY [23] OF FieldRef;RecRef@1001 : RecordRef);
    BEGIN
    END;

    BEGIN
    END.
  }
}

