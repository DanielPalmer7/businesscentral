OBJECT Page 9623 Finish Up Design
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=161707.677T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Finish Up Design;
    PageType=NavigatePage;
    RefreshOnActivate=true;
    OnInit=VAR
             Designer@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=12.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.Designer.NavDesignerALFunctions";
           BEGIN
             SaveVisible := true;
             DownloadCode := false;
             AppName := Designer.GetDesignerExtensionName();
             Publisher := Designer.GetDesignerExtensionPublisher();
             DownloadCodeEnabled := Designer.GetDesignerExtensionShowMyCode();
             if AppName = '' then
               NameAndPublisherEnabled := true
             else
               NameAndPublisherEnabled := false;
           END;

    ActionList=ACTIONS
    {
      { 6       ;    ;ActionContainer;
                      Name=ActionContainer6;
                      ActionContainerType=ActionItems }
      { 7       ;1   ;Action    ;
                      Name=Save;
                      CaptionML=ENU=Save;
                      ApplicationArea=#Basic,#Suite;
                      Visible=SaveVisible;
                      InFooterBar=true;
                      Image=Approve;
                      OnAction=VAR
                                 TempBlob@1004 : TEMPORARY Record TempBlob;
                                 FileManagement@1003 : Codeunit "File Management";
                                 NvOutStream@1002 : OutStream;
                                 Designer@1001 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=12.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.Designer.NavDesignerALFunctions";
                                 FileName@1000 : Text;
                                 CleanFileName@1005 : Text;
                               BEGIN
                                 if STRLEN(AppName) = 0 then
                                   ERROR(BlankNameErr);

                                 if STRLEN(Publisher) = 0 then
                                   ERROR(BlankPublisherErr);

                                 if not Designer.ExtensionNameAndPublisherIsValid(AppName,Publisher) then
                                   ERROR(DuplicateNameAndPublisherErr);

                                 SaveVisible := false;

                                 Designer.SaveDesignerExtension(AppName,Publisher);

                                 if DownloadCode and DownloadCodeEnabled then begin
                                   TempBlob.Blob.CREATEOUTSTREAM(NvOutStream);
                                   Designer.GenerateDesignerPackageZipStream(NvOutStream,Publisher,AppName);
                                   FileName := STRSUBSTNO(ExtensionFileNameTxt,AppName,Publisher);
                                   CleanFileName := Designer.SanitizeDesignerFileName(FileName,'_');
                                   FileManagement.BLOBExport(TempBlob,CleanFileName,true);
                                 end;

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Control1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Control2;
                Visible=SaveVisible;
                GroupType=Group }

    { 4   ;2   ;Field     ;
                Name=AppName;
                CaptionML=ENU=Extension Name;
                ApplicationArea=#Basic,#Suite;
                NotBlank=true;
                SourceExpr=AppName;
                Enabled=NameAndPublisherEnabled;
                Editable=NameAndPublisherEnabled }

    { 5   ;2   ;Field     ;
                Name=Publisher;
                CaptionML=ENU=Publisher;
                ApplicationArea=#Basic,#Suite;
                NotBlank=true;
                SourceExpr=Publisher;
                Enabled=NameAndPublisherEnabled;
                Editable=NameAndPublisherEnabled }

    { 8   ;2   ;Field     ;
                Name=DownloadCode;
                CaptionML=ENU=Download Code;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DownloadCode;
                Enabled=DownloadCodeEnabled }

  }
  CODE
  {
    VAR
      SaveVisible@1000 : Boolean;
      ExtensionFileNameTxt@1005 : TextConst '@@@="{Locked}; %1=Name, %2=Publisher";ENU=%1_%2_1.0.0.0.zip';
      AppName@1002 : Text[250];
      Publisher@1003 : Text[250];
      DownloadCode@1004 : Boolean;
      BlankNameErr@1006 : TextConst '@@@=Specifies that field cannot be blank.;ENU=Name cannot be blank.';
      BlankPublisherErr@1007 : TextConst '@@@=Specifies that field cannot be blank.;ENU=Publisher cannot be blank.';
      NameAndPublisherEnabled@1001 : Boolean;
      DownloadCodeEnabled@1008 : Boolean;
      DuplicateNameAndPublisherErr@1009 : TextConst '@@@=An extension with the same name and publisher already exists.;ENU=The specified name and publisher are already used in another extension. Please specify another name or publisher.';

    BEGIN
    END.
  }
}

