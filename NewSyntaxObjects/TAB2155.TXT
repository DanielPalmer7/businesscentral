OBJECT Table 2155 O365 Payment Instructions
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160131.353T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    OnDelete=VAR
               DocumentDescription@1000 : Text;
             BEGIN
               if Default then
                 ERROR(CannotDeleteDefaultErr);

               DocumentDescription := FindDraftsUsingInstructions;
               if DocumentDescription <> '' then
                 ERROR(PaymentIsUsedErr,FindDraftsUsingInstructions);

               if GUIALLOWED then
                 if not CONFIRM(DoYouWantToDeleteQst) then
                   ERROR('');

               DeleteTranslationsForRecord;
             END;

    CaptionML=ENU=O365 Payment Instructions;
  }
  FIELDS
  {
    { 1   ;   ;Id                  ;Integer       ;AutoIncrement=true;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Id }
    { 5   ;   ;Name                ;Text20        ;CaptionML=ENU=Name }
    { 6   ;   ;Payment Instructions;Text250       ;CaptionML=ENU=Payment Instruction }
    { 7   ;   ;Payment Instructions Blob;BLOB     ;CaptionML=ENU=Payment Instructions Blob }
    { 8   ;   ;Default             ;Boolean       ;OnValidate=VAR
                                                                O365PaymentInstructions@1000 : Record "O365 Payment Instructions";
                                                              BEGIN
                                                                if Default then begin
                                                                  O365PaymentInstructions.SETFILTER(Id,'<>%1',Id);
                                                                  O365PaymentInstructions.MODIFYALL(Default,false,false);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Default }
  }
  KEYS
  {
    {    ;Id                                      ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DocumentDescriptionTxt@1000 : TextConst '@@@="%1=Document description (e.g. Invoice, Estimate,...); %2=Document Number";ENU=%1 %2';
      PaymentIsUsedErr@1001 : TextConst '@@@=%1: Document type and number;ENU=You cannot delete the Payment Instructions because at least one invoice (%1) is using them.';
      CannotDeleteDefaultErr@1002 : TextConst 'ENU=You cannot delete the default Payment Instructions.';
      LanguageManagement@1003 : Codeunit LanguageManagement;
      DoYouWantToDeleteQst@1004 : TextConst 'ENU=Are you sure you want to delete the payment instructions?';

    PROCEDURE GetPaymentInstructions@14() : Text;
    VAR
      TempBlob@1000 : Record TempBlob;
      CR@1001 : Text[1];
    BEGIN
      CALCFIELDS("Payment Instructions Blob");
      if not "Payment Instructions Blob".HASVALUE then
        exit("Payment Instructions");
      CR[1] := 10;
      TempBlob.Blob := "Payment Instructions Blob";
      exit(TempBlob.ReadAsText(CR,TEXTENCODING::Windows));
    END;

    PROCEDURE SetPaymentInstructions@15(NewInstructions@1001 : Text);
    VAR
      TempBlob@1000 : Record TempBlob;
    BEGIN
      CLEAR("Payment Instructions Blob");
      "Payment Instructions" := COPYSTR(NewInstructions,1,MAXSTRLEN("Payment Instructions"));
      if STRLEN(NewInstructions) <= MAXSTRLEN("Payment Instructions") then
        exit; // No need to store anything in the blob
      if NewInstructions = '' then
        exit;
      TempBlob.WriteAsText(NewInstructions,TEXTENCODING::Windows);
      "Payment Instructions Blob" := TempBlob.Blob;
      MODIFY;
    END;

    PROCEDURE FindDraftsUsingInstructions@1() DocumentDescription : Text;
    VAR
      SalesHeader@1000 : Record "Sales Header";
    BEGIN
      DocumentDescription := '';
      SalesHeader.SETRANGE("Payment Instructions Id",Id);

      if SalesHeader.FINDFIRST then
        DocumentDescription := STRSUBSTNO(DocumentDescriptionTxt,SalesHeader.GetDocTypeTxt,SalesHeader."No.");
    END;

    PROCEDURE GetNameInCurrentLanguage@2() : Text[20];
    VAR
      O365PaymentInstrTransl@1000 : Record "O365 Payment Instr. Transl.";
      LanguageCode@1001 : Code[10];
    BEGIN
      LanguageCode := LanguageManagement.GetLanguageCodeFromLanguageID(GLOBALLANGUAGE);

      if not O365PaymentInstrTransl.GET(Id,LanguageCode) then
        exit(Name);

      exit(O365PaymentInstrTransl."Transl. Name");
    END;

    PROCEDURE GetPaymentInstructionsInCurrentLanguage@3() : Text;
    VAR
      O365PaymentInstrTransl@1000 : Record "O365 Payment Instr. Transl.";
      LanguageCode@1001 : Code[10];
    BEGIN
      LanguageCode := LanguageManagement.GetLanguageCodeFromLanguageID(GLOBALLANGUAGE);

      if not O365PaymentInstrTransl.GET(Id,LanguageCode) then
        exit(GetPaymentInstructions);

      exit(O365PaymentInstrTransl.GetTransPaymentInstructions);
    END;

    PROCEDURE DeleteTranslationsForRecord@4();
    VAR
      O365PaymentInstrTransl@1000 : Record "O365 Payment Instr. Transl.";
    BEGIN
      O365PaymentInstrTransl.SETRANGE(Id,Id);
      O365PaymentInstrTransl.DELETEALL(true);
    END;

    PROCEDURE CopyInstructionsInCurrentLanguageToBlob@5(VAR TempBlob@1000 : Record TempBlob);
    BEGIN
      TempBlob.WriteAsText(GetPaymentInstructionsInCurrentLanguage,TEXTENCODING::Windows);
    END;

    BEGIN
    END.
  }
}

