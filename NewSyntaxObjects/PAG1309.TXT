OBJECT Page 1309 O365 Getting Started
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=161342.843T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Trial Experience;
    SourceTable="O365 Getting Started";
    PageType=NavigatePage;
    OnInit=VAR
             AllProfile@1001 : Record "All Profile";
             ConfPersonalizationMgt@1000 : Codeunit "Conf./Personalization Mgt.";
           BEGIN
             SETRANGE("User ID",USERID);
             CurrProfileID := ConfPersonalizationMgt.GetCurrentProfileIDNoError;

             if CurrProfileID <> '' then
               AllProfile.SETRANGE("Profile ID",CurrProfileID);

             if not AllProfile.FINDFIRST then
               exit;

             CurrProfileID := AllProfile."Profile ID";
             ChangedProfileID := AllProfile."Profile ID";
           END;

    OnOpenPage=BEGIN
                 if not AlreadyShown then
                   MarkAsShown;

                 CurrentPage := true;
               END;

    OnClosePage=BEGIN
                  "Tour in Progress" := false;
                  "Tour Completed" := true;
                  if not MODIFY then;
                END;

    OnQueryClosePage=VAR
                       ConfPersonalizationMgt@1000 : Codeunit "Conf./Personalization Mgt.";
                     BEGIN
                       if (ConfPersonalizationMgt.GetCurrentProfileIDNoError <> ChangedProfileID) and RoleCenterOverview.GetAcceptAction then
                         if not CONFIRM(RoleNotSavedQst) then
                           ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 6       ;    ;ActionContainer;
                      Name=ActionContainer6;
                      ActionContainerType=NewDocumentItems }
      { 7       ;1   ;Action    ;
                      Name=Get Started;
                      CaptionML=ENU=Get Started;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      InFooterBar=true;
                      OnAction=VAR
                                 UserPersonalization@1002 : Record "User Personalization";
                                 ConfPersonalizationMgt@1001 : Codeunit "Conf./Personalization Mgt.";
                                 SessionSet@1000 : SessionSettings;
                               BEGIN
                                 if ConfPersonalizationMgt.GetCurrentProfileIDNoError = ChangedProfileID then
                                   CurrPage.CLOSE;

                                 if ConfPersonalizationMgt.GetCurrentProfileIDNoError <> ChangedProfileID then begin
                                   ConfPersonalizationMgt.SetCurrentProfileID(ChangedProfileID);
                                   UserPersonalization.GET(USERSECURITYID);

                                   with SessionSet do begin
                                     INIT;
                                     PROFILEID := ChangedProfileID;
                                     PROFILEAPPID := UserPersonalization."App ID";
                                     PROFILESYSTEMSCOPE := UserPersonalization.Scope = UserPersonalization.Scope::System;
                                     LANGUAGEID := UserPersonalization."Language ID";
                                     LOCALEID := UserPersonalization."Locale ID";
                                     TIMEZONE := UserPersonalization."Time Zone";
                                     REQUESTSESSIONUPDATE(true);
                                   end;
                                 end;

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Control1;
                ContainerType=ContentArea }

    { 3   ;1   ;Group     ;
                Name=Control3;
                Visible=CurrentPage;
                GroupType=Group }

    { 4   ;2   ;Group     ;
                Name=Control4;
                GroupType=Group }

    { 2   ;3   ;Field     ;
                Name=WelcomeWizard;
                ApplicationArea=#Basic,#Suite;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.WelcomeWizard;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      RoleCenterOverview@1004 : Page "Role Center Overview";
      CurrProfileID@1003 : Code[30];
      ChangedProfileID@1001 : Code[30];
      RoleNotSavedQst@1005 : TextConst 'ENU=Your choice of role center is not saved. Are you sure you want to close?';
      TitleTxt@1006 : TextConst '@@@=%1 is the branding PRODUCTNAME.MARKETING string constant;ENU=Welcome to %1';
      SubTitleTxt@1007 : TextConst 'ENU=Let''s get started';
      ExplanationTxt@1008 : TextConst '@@@=%1 - This is the COMPANYNAME. ex. Cronus US Inc.;ENU=Start with basic business processes, or jump right in to advanced operations. Use our %1 demo company and data, or create a new company and import your own data.';
      IntroTxt@1009 : TextConst 'ENU=Introduction';
      IntroDescTxt@1010 : TextConst 'ENU=Get to know Business Central';
      GetStartedTxt@1011 : TextConst 'ENU=Get Started';
      GetStartedDescTxt@1012 : TextConst 'ENU=See the important first steps';
      FindHelpTxt@1013 : TextConst 'ENU=Get Assistance';
      FindHelpDescTxt@1014 : TextConst 'ENU=Know where to go for information';
      RoleCentersTxt@1015 : TextConst 'ENU=Role Centers';
      RoleCentersDescTxt@1016 : TextConst 'ENU=Explore different business roles';
      CurrentPage@1000 : Boolean;

    [External]
    PROCEDURE GetNextPageID@1014(Increment@1000 : Integer;CurrentPageID@1002 : Integer) NextPageID : Integer;
    BEGIN
      NextPageID := CurrentPageID + Increment;
    END;

    LOCAL PROCEDURE GetProfileDescription@1() : Text[250];
    VAR
      AllProfile@1000 : Record "All Profile";
    BEGIN
      AllProfile.SETRANGE(Scope,AllProfile.Scope::System);
      AllProfile.SETFILTER("Profile ID",ChangedProfileID);
      if AllProfile.FINDFIRST then
        exit(AllProfile.Description);
    END;

    EVENT WelcomeWizard@-2::ControlAddInReady@3();
    VAR
      ExplanationStr@1000 : Text;
      WelcomeToTitle@1001 : Text;
    BEGIN
      ExplanationStr := STRSUBSTNO(ExplanationTxt,COMPANYNAME);
      WelcomeToTitle := STRSUBSTNO(TitleTxt,PRODUCTNAME.MARKETING);
      CurrPage.WelcomeWizard.Initialize(WelcomeToTitle,SubTitleTxt,ExplanationStr,IntroTxt,IntroDescTxt,
        GetStartedTxt,GetStartedDescTxt,FindHelpTxt,FindHelpDescTxt,RoleCentersTxt,RoleCentersDescTxt,GetProfileDescription);
    END;

    EVENT WelcomeWizard@-2::ErrorOccurred@4(error@1001 : Text;description@1000 : Text);
    BEGIN
    END;

    EVENT WelcomeWizard@-2::Refresh@5();
    BEGIN
    END;

    EVENT WelcomeWizard@-2::ThumbnailClicked@6(selection@1000 : Integer);
    VAR
      VideoLink@1001 : Page "Video link";
    BEGIN
      case selection of
        1:
          begin
            VideoLink.SetURL('https://go.microsoft.com/fwlink/?linkid=867632');
            VideoLink.RUNMODAL;
          end;
        2:
          begin
            VideoLink.SetURL('https://go.microsoft.com/fwlink/?linkid=867634');
            VideoLink.RUNMODAL;
          end;
        3:
          begin
            VideoLink.SetURL('https://go.microsoft.com/fwlink/?linkid=867635');
            VideoLink.RUNMODAL;
          end;
        4:
          begin
            CLEAR(RoleCenterOverview);
            RoleCenterOverview.DelaySessionUpdateRequest;
            if RoleCenterOverview.RUNMODAL = ACTION::OK then begin
              ChangedProfileID := RoleCenterOverview.GetSelectedProfileID;
              CurrPage.WelcomeWizard.UpdateProfileId(GetProfileDescription);
            end;
          end;
      end;
    END;

    BEGIN
    END.
  }
}

