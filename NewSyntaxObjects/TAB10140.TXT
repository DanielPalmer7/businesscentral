OBJECT Table 10140 Deposit Header
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160237.257T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    DataCaptionFields="No.";
    OnInsert=BEGIN
               "Journal Template Name" := GETRANGEMAX("Journal Template Name");
               "Journal Batch Name" := GETRANGEMAX("Journal Batch Name");
               GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
               GenJnlManagement.LookupName("Journal Batch Name",GenJnlLine);
               FILTERGROUP(2);
               SETRANGE("Journal Batch Name","Journal Batch Name");
               FILTERGROUP(0);
               DepositHeader2.COPY(Rec);
               DepositHeader2.SETRANGE("Journal Template Name","Journal Template Name");
               DepositHeader2.SETRANGE("Journal Batch Name","Journal Batch Name");
               if DepositHeader2.FINDFIRST then
                 ERROR(Text002,TABLECAPTION,GenJnlBatch.TABLECAPTION);
               GLSetup.GET;
               if "No." = '' then begin
                 GLSetup.TESTFIELD("Deposit Nos.");
                 NoSeriesMgt.InitSeries(GLSetup."Deposit Nos.",xRec."No. Series","Posting Date","No.","No. Series");
               end;
               if "Posting Date" = 0D then
                 VALIDATE("Posting Date",WORKDATE);
               "Posting Description" := STRSUBSTNO(Text000,FIELDNAME("No."),"No.");

               GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
               if (GenJnlBatch."Bal. Account Type" = GenJnlBatch."Bal. Account Type"::"Bank Account") and
                  (GenJnlBatch."Bal. Account No." <> '')
               then
                 VALIDATE("Bank Account No.",GenJnlBatch."Bal. Account No.");
               "Reason Code" := GenJnlBatch."Reason Code";
             END;

    OnDelete=BEGIN
               GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
               GenJnlLine.DELETEALL(true);
             END;

    OnRename=BEGIN
               ERROR(Text003,TABLECAPTION);
             END;

    CaptionML=ENU=Deposit Header;
    LookupPageID="Deposit List";
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                if "No." <> xRec."No." then begin
                                                                  GLSetup.GET;
                                                                  NoSeriesMgt.TestManual(GLSetup."Deposit Nos.");
                                                                  "No. Series" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 2   ;   ;Bank Account No.    ;Code20        ;TableRelation="Bank Account";
                                                   OnValidate=BEGIN
                                                                BankAccount.GET("Bank Account No.");
                                                                VALIDATE("Currency Code",BankAccount."Currency Code");
                                                                "Bank Acc. Posting Group" := BankAccount."Bank Acc. Posting Group";
                                                                "Language Code" := BankAccount."Language Code";

                                                                CreateDim(DATABASE::"Bank Account","Bank Account No.");
                                                              END;

                                                   CaptionML=ENU=Bank Account No. }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                UpdateCurrencyFactor;
                                                                if "Currency Code" <> xRec."Currency Code" then begin
                                                                  GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
                                                                  GenJnlLine.MODIFYALL("Currency Code","Currency Code",true);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Currency Code;
                                                   Editable=false }
    { 4   ;   ;Currency Factor     ;Decimal       ;CaptionML=ENU=Currency Factor;
                                                   DecimalPlaces=0:15;
                                                   MinValue=0;
                                                   Editable=false }
    { 5   ;   ;Posting Date        ;Date          ;OnValidate=BEGIN
                                                                UpdateCurrencyFactor;
                                                                if "Document Date" = 0D then
                                                                  "Document Date" := "Posting Date";
                                                                GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
                                                                GenJnlLine.MODIFYALL("Posting Date","Posting Date",true);
                                                              END;

                                                   CaptionML=ENU=Posting Date }
    { 6   ;   ;Total Deposit Amount;Decimal       ;CaptionML=ENU=Total Deposit Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 7   ;   ;Document Date       ;Date          ;OnValidate=BEGIN
                                                                if "Posting Date" = 0D then
                                                                  VALIDATE("Posting Date","Document Date");
                                                              END;

                                                   CaptionML=ENU=Document Date }
    { 8   ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 9   ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 10  ;   ;Bank Acc. Posting Group;Code20     ;TableRelation="Bank Account Posting Group";
                                                   CaptionML=ENU=Bank Acc. Posting Group }
    { 11  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=ENU=Language Code }
    { 12  ;   ;No. Printed         ;Integer       ;CaptionML=ENU=No. Printed;
                                                   Editable=false }
    { 13  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=ENU=Reason Code }
    { 14  ;   ;Correction          ;Boolean       ;CaptionML=ENU=Correction }
    { 15  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=false }
    { 16  ;   ;Posting Description ;Text50        ;CaptionML=ENU=Posting Description }
    { 17  ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=ENU=Journal Template Name;
                                                   Editable=false }
    { 18  ;   ;Journal Batch Name  ;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE ("Journal Template Name"=FIELD("Journal Template Name"));
                                                   CaptionML=ENU=Journal Batch Name;
                                                   Editable=false }
    { 21  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Bank Comment Line" WHERE ("Table Name"=CONST(Deposit),
                                                                                                "Bank Account No."=FIELD("Bank Account No."),
                                                                                                "No."=FIELD("No.")));
                                                   CaptionML=ENU=Comment;
                                                   Editable=false }
    { 22  ;   ;Total Deposit Lines ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Gen. Journal Line".Amount WHERE ("Journal Template Name"=FIELD("Journal Template Name"),
                                                                                                      "Journal Batch Name"=FIELD("Journal Batch Name")));
                                                   CaptionML=ENU=Total Deposit Lines;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDocDim;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"No."                                   ;Clustered=true }
    {    ;"Bank Account No."                       }
    {    ;"Journal Template Name","Journal Batch Name" }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GenJnlLine@1020006 : Record "Gen. Journal Line";
      BankAccount@1020000 : Record "Bank Account";
      CurrExchRate@1020001 : Record "Currency Exchange Rate";
      GLSetup@1020002 : Record "General Ledger Setup";
      DepositHeader2@1020011 : Record "Deposit Header";
      GenJnlBatch@1020013 : Record "Gen. Journal Batch";
      NoSeriesMgt@1020003 : Codeunit NoSeriesManagement;
      DimMgt@1020004 : Codeunit DimensionManagement;
      Text000@1020005 : TextConst 'ENU=Deposit %1 %2';
      GenJnlManagement@1020010 : Codeunit GenJnlManagement;
      Text002@1020012 : TextConst 'ENU=Only one %1 is allowed for each %2. You can use Deposit, Change Batch if you want to create a new Deposit.';
      Text003@1480000 : TextConst 'ENU=You cannot rename a %1.';

    LOCAL PROCEDURE UpdateCurrencyFactor@1();
    VAR
      CurrencyDate@1020000 : Date;
    BEGIN
      if "Currency Code" <> '' then begin
        if "Posting Date" <> 0D then
          CurrencyDate := "Posting Date"
        else
          CurrencyDate := WORKDATE;
        "Currency Factor" := CurrExchRate.ExchangeRate(CurrencyDate,"Currency Code");
      end else
        "Currency Factor" := 0;
    END;

    LOCAL PROCEDURE CreateDim@2(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      SourceCodeSetup@1010 : Record "Source Code Setup";
      TableID@1011 : ARRAY [10] OF Integer;
      No@1012 : ARRAY [10] OF Code[20];
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      "Dimension Set ID" :=
        DimMgt.GetDefaultDimID(
          TableID,No,SourceCodeSetup.Deposits,
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0);
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@3(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    [External]
    PROCEDURE AssistEdit@1020000(OldDepositHeader@1000 : Record "Deposit Header") : Boolean;
    VAR
      DepositHeader@1020000 : Record "Deposit Header";
    BEGIN
      with DepositHeader do begin
        DepositHeader := Rec;
        GLSetup.GET;
        GLSetup.TESTFIELD("Deposit Nos.");
        if NoSeriesMgt.SelectSeries(GLSetup."Deposit Nos.",OldDepositHeader."No. Series","No. Series") then begin
          NoSeriesMgt.SetSeries("No.");
          Rec := DepositHeader;
          exit(true);
        end;
      end;
      exit(false);
    END;

    [External]
    PROCEDURE ShowDocDim@1020001();
    BEGIN
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet2(
          "Dimension Set ID",STRSUBSTNO('%1 %2',"Bank Account No.","No."),
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    BEGIN
    END.
  }
}

