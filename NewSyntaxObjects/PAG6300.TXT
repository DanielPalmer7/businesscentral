OBJECT Page 6300 Azure AD App Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=161544.437T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=SETUP AZURE ACTIVE DIRECTORY;
    PageType=NavigatePage;
    OnInit=VAR
             AzureADAppSetup@1000 : Record "Azure AD App Setup";
           BEGIN
             // Checks user permissions and closes the wizard with an error message if necessary.
             if not AzureADAppSetup.WRITEPERMISSION then
               ERROR(PermissionsErr);
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 // Always start on the introduction step
                 SetStep(CurrentStep::Intro);
               END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      Name=Actions;
                      ActionContainerType=ActionItems }
      { 22      ;1   ;Action    ;
                      Name=ActionReset;
                      CaptionML=ENU=Reset Reply URL;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=Step1Visible;
                      InFooterBar=true;
                      OnAction=BEGIN
                                 CurrPage.AzureAdSetup.PAGE.SetReplyURLWithDefault;
                               END;
                                }
      { 11      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=ENU=Back;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=true;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 GoToNextStep(false);
                               END;
                                }
      { 12      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=ENU=Next;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=true;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 GoToNextStep(true);
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=ENU=Finish;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=true;
                      Image=Approve;
                      OnAction=BEGIN
                                 CurrPage.AzureAdSetup.PAGE.Save;

                                 // notify Assisted Setup that this setup has been completed
                                 AssistedSetup.SetStatus(PAGE::"Azure AD App Setup Wizard",AssistedSetup.Status::Completed);
                                 CurrPage.UPDATE(false);
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Azure AD Application Setup;
                CaptionML=ENU=Azure AD Application Setup;
                ContainerType=ContentArea }

    { 14  ;1   ;Group     ;
                Name=Control14;
                Visible=TopBannerVisible AND NOT DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 5   ;2   ;Field     ;
                Name=<MediaRepositoryStandard>;
                CaptionML=ENU="";
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE }

    { 4   ;1   ;Group     ;
                Name=Control4;
                Visible=TopBannerVisible AND DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 15  ;2   ;Field     ;
                Name=<MediaRepositoryDone>;
                CaptionML=ENU="";
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE }

    { 8   ;1   ;Group     ;
                Name=Intro;
                CaptionML=ENU=Intro;
                Visible=IntroVisible;
                GroupType=Group }

    { 16  ;2   ;Group     ;
                Name=Para1.1;
                CaptionML=ENU=Welcome to Azure Active Directory (Azure AD) Setup;
                GroupType=Group }

    { 9   ;3   ;Field     ;
                Name=Para1.1.1;
                CaptionML=ENU=When you register an application in the Azure Portal, it enables on premise applications to communicate with Power BI, Microsoft Flow, Office 365 Exchange and other Azure services directly.  This registration is only required once for each Business Central instance.;
                ApplicationArea=#Basic,#Suite }

    { 24  ;3   ;Field     ;
                Name=Control24;
                ApplicationArea=#Basic,#Suite }

    { 17  ;3   ;Field     ;
                Name=Para1.1.2;
                CaptionML=ENU=This wizard will guide you through the steps required to register Business Central in the Azure Portal.;
                ApplicationArea=#Basic,#Suite }

    { 26  ;3   ;Field     ;
                Name=Control26;
                ApplicationArea=#Basic,#Suite }

    { 18  ;3   ;Field     ;
                Name=Para1.1.3;
                CaptionML=ENU=At the end of the registration process, the Azure Portal will provide an Application ID and Key that will be required to complete the setup.;
                ApplicationArea=#Basic,#Suite }

    { 19  ;2   ;Group     ;
                Name=Para1.2;
                CaptionML=ENU=Let's go!;
                GroupType=Group;
                InstructionalTextML=ENU=Choose Next to step through the process of registering Business Central in the Azure Portal and obtaining the necessary information to complete this setup. }

    { 2   ;1   ;Group     ;
                Name=Step1;
                CaptionML=ENU=Step 1;
                Visible=Step1Visible;
                GroupType=Group }

    { 20  ;2   ;Group     ;
                Name=Para2.1;
                CaptionML=ENU=Registering Business Central;
                GroupType=Group }

    { 21  ;3   ;Field     ;
                Name=Para2.1.1;
                CaptionML=ENU=To obtain an Application ID and Key, or to regenerate a Key for an existing Application ID, select the Auto Register link below (recommended) or enter the Application ID and Key you manually created in the Azure Portal.  You can also find more information on how to manually create an Application ID and Key in the 'How to:  Register Business Central in the Azure Management Portal' section of the documentation.;
                ApplicationArea=#Basic,#Suite }

    { 3   ;3   ;Part      ;
                Name=AzureAdSetup;
                CaptionML=[@@@={Locked};
                           ENU=" "];
                ApplicationArea=#Basic,#Suite;
                PagePartID="Azure AD App Setup Part";
                PartType=Page;
                ShowFilter=false }

    { 28  ;3   ;Field     ;
                Name=OAuthIntegration;
                ApplicationArea=#Basic,#Suite;
                HideValue=TRUE;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.OAuthIntegration;PublicKeyToken=31bf3856ad364e35];
                ShowCaption=false }

    { 6   ;1   ;Group     ;
                Name=Done;
                CaptionML=ENU=Done;
                Visible=DoneVisible;
                GroupType=Group }

    { 25  ;2   ;Group     ;
                Name=Para3.1;
                CaptionML=ENU=That's it!;
                GroupType=Group }

    { 7   ;3   ;Group     ;
                Name=Para3.1.1;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=To begin using the Azure Active Directory services, choose Finish. }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1010 : Record "Media Repository";
      MediaRepositoryDone@1011 : Record "Media Repository";
      MediaResourcesStandard@1014 : Record "Media Resources";
      MediaResourcesDone@1013 : Record "Media Resources";
      AssistedSetup@1008 : Record "Assisted Setup";
      ClientTypeManagement@1077 : Codeunit ClientTypeManagement;
      CurrentStep@1000 : 'Intro,AzureAD,Done';
      IntroVisible@1001 : Boolean;
      Step1Visible@1002 : Boolean;
      DoneVisible@1004 : Boolean;
      NextEnabled@1005 : Boolean;
      BackEnabled@1006 : Boolean;
      FinishEnabled@1007 : Boolean;
      StepOutOfRangeErr@1003 : TextConst 'ENU=Wizard step out of range.';
      PermissionsErr@1009 : TextConst 'ENU=Please contact an administrator to set up your Azure Active Directory application.';
      TopBannerVisible@1012 : Boolean;
      NavRegistrationPortalTxt@1015 : TextConst '@@@={Locked};ENU="https://go.microsoft.com/fwlink/?linkid=862265&version=v1&replyUrl=%1&keyExpiration=%2"';
      AutoRegisterTxt@1016 : TextConst 'ENU=Auto-Register';
      AutoRegisterTooltipTxt@1017 : TextConst 'ENU=You will be redirected to App Registration Portal.';
      NavRegistrationNotSupportedMsg@1018 : TextConst 'ENU=You must use the Windows Client or Web Client to register Business Central in the Azure Portal.';
      NavRegistrationGenericErr@1019 : TextConst 'ENU=An error occurred while registering the app. Please try again or manually register the app using Azure portal.';

    LOCAL PROCEDURE SetStep@1(NewStep@1000 : Option);
    BEGIN
      if (NewStep < CurrentStep::Intro) or (NewStep > CurrentStep::Done) then
        ERROR(StepOutOfRangeErr);

      ClearStepControls;
      CurrentStep := NewStep;

      case NewStep of
        CurrentStep::Intro:
          begin
            IntroVisible := true;
            NextEnabled := true;
          end;
        CurrentStep::AzureAD:
          begin
            Step1Visible := true;
            BackEnabled := true;
            NextEnabled := true;
          end;
        CurrentStep::Done:
          begin
            DoneVisible := true;
            BackEnabled := true;
            FinishEnabled := true;
          end;
      end;

      CurrPage.UPDATE(true);
    END;

    LOCAL PROCEDURE ClearStepControls@2();
    BEGIN
      // hide all tabs
      IntroVisible := false;
      Step1Visible := false;
      DoneVisible := false;

      // disable all buttons
      BackEnabled := false;
      NextEnabled := false;
      FinishEnabled := false;
    END;

    LOCAL PROCEDURE CalculateNextStep@7(Forward@1000 : Boolean) NextStep : Integer;
    BEGIN
      // // Calculates the next step and hides steps based on whether the Power BI setup is enabled or not

      // General cases
      if Forward and (CurrentStep < CurrentStep::Done) then
        // move forward 1 step
        NextStep := CurrentStep + 1
      else
        if not Forward and (CurrentStep > CurrentStep::Intro) then
          // move backward 1 step
          NextStep := CurrentStep - 1
        else
          // stay on the current step
          NextStep := CurrentStep;
    END;

    LOCAL PROCEDURE GoToNextStep@10(Forward@1000 : Boolean);
    BEGIN
      if Forward then
        ValidateStep(CurrentStep);

      SetStep(CalculateNextStep(Forward));
    END;

    LOCAL PROCEDURE ValidateStep@11(Step@1000 : Option);
    BEGIN
      if Step = CurrentStep::AzureAD then
        CurrPage.AzureAdSetup.PAGE.ValidateFields;
    END;

    LOCAL PROCEDURE LoadTopBanners@4();
    BEGIN
      if MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) and
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      then
        if MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") and
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        then
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    EVENT OAuthIntegration@-28::AuthorizationCodeRetrieved@3(authorizationCode@1000 : Text);
    BEGIN
    END;

    EVENT OAuthIntegration@-28::AuthorizationErrorOccurred@4(error@1001 : Text;description@1000 : Text);
    BEGIN
    END;

    EVENT OAuthIntegration@-28::AppRegistrationInformationRetrieved@5(clientId@1001 : Text;clientSecret@1000 : Text);
    BEGIN
      CurrPage.AzureAdSetup.PAGE.SetAppDetails(clientId,clientSecret);
      CurrPage.UPDATE;
    END;

    EVENT OAuthIntegration@-28::AppRegistrationErrorOccurred@6(errorCode@1001 : Text;description@1000 : Text);
    BEGIN
      case errorCode of
        'NotSupported':
          MESSAGE(NavRegistrationNotSupportedMsg);
        else
          ERROR(NavRegistrationGenericErr);
      end;
    END;

    EVENT OAuthIntegration@-28::ControlAddInReady@7();
    VAR
      AzureADAppSetup@1002 : Record "Azure AD App Setup";
      TypeHelper@1001 : Codeunit "Type Helper";
      Url@1000 : Text;
    BEGIN
      Url := CurrPage.AzureAdSetup.PAGE.GetRedirectUrl;
      Url := STRSUBSTNO(NavRegistrationPortalTxt,TypeHelper.UrlEncode(Url),FORMAT(CREATEDATETIME(CALCDATE('<1Y>',TODAY),TIME),0,9));

      if AzureADAppSetup.FINDFIRST then
        Url := Url + '&clientId=' + FORMAT(AzureADAppSetup."App ID");

      CurrPage.OAuthIntegration.RegisterApp(Url,AutoRegisterTxt,AutoRegisterTooltipTxt);
    END;

    BEGIN
    END.
  }
}

