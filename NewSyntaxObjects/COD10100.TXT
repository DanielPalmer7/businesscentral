OBJECT Codeunit 10100 Post Sales Tax Jnl
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160931.380T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    TableNo="Gen. Journal Line";
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      GenJnlLine@1030000 : Record "Gen. Journal Line";
      GenJnlTemplate@1030001 : Record "Gen. Journal Template";
      GenJnlPostBatch@1030002 : Codeunit "Gen. Jnl.-Post Batch";
      ManageSalesTaxJournal@1030003 : Codeunit "Manage Sales Tax Journal";
      TempJnlBatchName@1030004 : Code[10];
      Text000@1030005 : TextConst '@@@=Posting Date cannot be filtered when posting recurring journals.;ENU=cannot be filtered when posting recurring journals.';
      Text001@1030006 : TextConst 'ENU=Do you want to post the journal lines?';
      Text002@1030007 : TextConst 'ENU=There is nothing to post.';
      Text003@1030008 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1030009 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.';

    [External]
    PROCEDURE Code@1030000();
    BEGIN
      with GenJnlLine do begin
        GenJnlTemplate.GET("Journal Template Name");
        GenJnlTemplate.TESTFIELD("Force Posting Report",false);
        if GenJnlTemplate.Recurring and (GETFILTER("Posting Date") <> '') then
          FIELDERROR("Posting Date",Text000);

        if not CONFIRM(Text001,false) then
          exit;

        TempJnlBatchName := "Journal Batch Name";

        ManageSalesTaxJournal.PostToVAT(GenJnlLine);
        ManageSalesTaxJournal.CreateGenJnlLines(GenJnlLine);
        GenJnlPostBatch.RUN(GenJnlLine);

        if "Line No." = 0 then
          MESSAGE(Text002)
        else
          if TempJnlBatchName = "Journal Batch Name" then
            MESSAGE(Text003)
          else
            MESSAGE(
              Text004,
              "Journal Batch Name");

        if not FIND('=><') or (TempJnlBatchName <> "Journal Batch Name") then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

