OBJECT Codeunit 483 Change Global Dimensions
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160620.197T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    TableNo="Change Global Dim. Log Entry";
    Permissions=TableData "G/L Entry"=rm,
                TableData "Cust. Ledger Entry"=rm,
                TableData "Vendor Ledger Entry"=rm,
                TableData "Item Ledger Entry"=rm,
                TableData "Sales Shipment Header"=rm,
                TableData "Sales Shipment Line"=rm,
                TableData "Sales Invoice Header"=rm,
                TableData "Sales Invoice Line"=rm,
                TableData "Sales Cr.Memo Header"=rm,
                TableData "Sales Cr.Memo Line"=rm,
                TableData "Purch. Rcpt. Header"=rm,
                TableData "Purch. Rcpt. Line"=rm,
                TableData "Purch. Inv. Header"=rm,
                TableData "Purch. Inv. Line"=rm,
                TableData "Purch. Cr. Memo Hdr."=rm,
                TableData "Purch. Cr. Memo Line"=rm,
                TableData "Job Ledger Entry"=rm,
                TableData "Res. Ledger Entry"=rm,
                TableData "Bank Account Ledger Entry"=rm,
                TableData "Phys. Inventory Ledger Entry"=rm,
                TableData "Issued Reminder Header"=rm,
                TableData "Issued Fin. Charge Memo Header"=rm,
                TableData "Detailed Cust. Ledg. Entry"=rm,
                TableData "Detailed Vendor Ledg. Entry"=rm,
                TableData "Job WIP G/L Entry"=rm,
                TableData "Employee Ledger Entry"=rm,
                TableData "Detailed Employee Ledger Entry"=rm,
                TableData "Production Order"=rm,
                TableData "Prod. Order Line"=rm,
                TableData "Prod. Order Component"=rm,
                TableData "Prod. Order Routing Line"=rm,
                TableData "Prod. Order Capacity Need"=rm,
                TableData "Prod. Order Routing Tool"=rm,
                TableData "Prod. Order Routing Personnel"=rm,
                TableData "Prod. Order Rtng Qlty Meas."=rm,
                TableData "Prod. Order Comment Line"=rm,
                TableData "Prod. Order Rtng Comment Line"=rm,
                TableData "Prod. Order Comp. Cmt Line"=rm,
                TableData "FA Ledger Entry"=rm,
                TableData "Maintenance Ledger Entry"=rm,
                TableData "Ins. Coverage Ledger Entry"=rm,
                TableData "Value Entry"=rm,
                TableData "Capacity Ledger Entry"=rm,
                TableData "Service Header"=rm,
                TableData "Service Item Line"=rm,
                TableData "Service Line"=rm,
                TableData "Service Ledger Entry"=rm,
                TableData "Service Contract Header"=rm,
                TableData "Filed Service Contract Header"=rm,
                TableData "Return Shipment Header"=rm,
                TableData "Return Shipment Line"=rm,
                TableData "Return Receipt Header"=rm,
                TableData "Return Receipt Line"=rm,
                TableData "Posted Bank Rec. Line"=rm,
                TableData "Posted Deposit Header"=rm,
                TableData "Posted Deposit Line"=rm;
    OnRun=VAR
            ExpectedDelay@1000 : Duration;
          BEGIN
            ExpectedDelay := FindConcurrentTaskInProgress("Table ID");
            if ExpectedDelay > 0 then
              ScheduleJobForTable(Rec,CURRENTDATETIME + ExpectedDelay + 2000)
            else
              if RunTask then
                DeleteAllEntriesIfAllCompleted;
          END;

  }
  CODE
  {
    VAR
      GeneralLedgerSetup@1000 : Record "General Ledger Setup";
      BlockTableOpsSubscriber@1010 : Codeunit "Block Table Ops Subscriber";
      ChangeType@1002 : ARRAY [2] OF 'None,Blank,Replace,New';
      NewGlobalDimCode@1001 : ARRAY [2] OF Code[20];
      CurrentGlobalDimCode@1003 : ARRAY [2] OF Code[20];
      DimIsUsedInGLSetupErr@1006 : TextConst '@@@=%1 - a dimension code, like PROJECT;ENU=The dimension %1 is used in General Ledger Setup window as a shortcut dimension.';
      BindNotificationIDTok@1009 : TextConst '@@@={Locked};ENU=CACDC1FF-2352-4B9D-9575-8BA4D85E64A0';
      BindNotificationMsg@1011 : TextConst 'ENU=Sign out and sign back in for the changes to take effect.';
      CloseActiveSessionsMsg@1012 : TextConst 'ENU=Close all other active sessions.';
      CloseSessionNotificationTok@1013 : TextConst '@@@={Locked};ENU=A2C57B69-B056-4B3B-8D0F-C0D997145EE7';

    [External]
    PROCEDURE ActivateBlock@23();
    BEGIN
      if BlockTableOpsSubscriber.IsClear then
        if BlockTableOpsSubscriber.FindTablesToBlock then
          BINDSUBSCRIPTION(BlockTableOpsSubscriber);
    END;

    [External]
    PROCEDURE DeleteAllEntriesIfAllCompleted@20();
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
    BEGIN
      if not BlockTableOpsSubscriber.IsClear then begin
        ChangeGlobalDimLogEntry.SETFILTER(Status,'<>%1',ChangeGlobalDimLogEntry.Status::Completed);
        if ChangeGlobalDimLogEntry.ISEMPTY then
          Reset;
      end;
    END;

    LOCAL PROCEDURE GetDelayInScheduling@19() : Integer;
    BEGIN
      // duration in milliseconds between scheduled jobs
      exit(100);
    END;

    PROCEDURE Prepare@10();
    VAR
      IsListEmpty@1000 : Boolean;
    BEGIN
      if IsPrepareEnabled then
        if IsCurrentSessionActiveOnly then begin
          IsListEmpty := not InitTableList;
          if IsListEmpty then begin
            UpdateGLSetup;
            InitNewGlobalDimCodes;
          end;
        end else
          SendCloseSessionsNotification;
    END;

    PROCEDURE Reset@47();
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
    BEGIN
      ChangeGlobalDimLogEntry.DELETEALL;
      CLEARALL;
      BlockTableOpsSubscriber.ClearState;
      UNBINDSUBSCRIPTION(BlockTableOpsSubscriber);
      InitNewGlobalDimCodes;
    END;

    PROCEDURE Start@27();
    BEGIN
      if IsStartEnabled then
        if BlockTableOpsSubscriber.IsSubscribed then begin
          CompleteEmptyTables;
          UpdateGLSetup;
          ScheduleJobs(GetDelayInScheduling);
          InitNewGlobalDimCodes;
        end else
          SendBindNotification;
    END;

    LOCAL PROCEDURE CompleteEmptyTables@12();
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
    BEGIN
      ChangeGlobalDimLogEntry.SETRANGE("Total Records",0);
      ChangeGlobalDimLogEntry.MODIFYALL(Progress,10000);
      ChangeGlobalDimLogEntry.MODIFYALL(Status,ChangeGlobalDimLogEntry.Status::Completed);
    END;

    PROCEDURE FindTablesForScheduling@31(VAR ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry") : Boolean;
    BEGIN
      with ChangeGlobalDimLogEntry do begin
        SETRANGE("Parent Table ID",0);
        SETFILTER("Total Records",'>0');
        exit(FINDSET(true))
      end;
    END;

    LOCAL PROCEDURE GetMinCommitSize@41() : Integer;
    BEGIN
      // number of records that should be modified between COMMIT calls
      exit(10);
    END;

    LOCAL PROCEDURE CalcRecordsWithinCommit@40(TotalRecords@1000 : Integer) RecordsWithinCommit : Integer;
    BEGIN
      RecordsWithinCommit := ROUND(TotalRecords / 100,1,'>');
      if RecordsWithinCommit < GetMinCommitSize then
        RecordsWithinCommit := GetMinCommitSize;
    END;

    PROCEDURE ChangeDimsOnTable@39(VAR ChangeGlobalDimLogEntry@1001 : Record "Change Global Dim. Log Entry") Completed : Boolean;
    VAR
      DependentChangeGlobalDimLogEntry@1003 : Record "Change Global Dim. Log Entry";
      RecRef@1002 : RecordRef;
      DependentRecRef@1005 : RecordRef;
      CurrentRecNo@1004 : Integer;
      DependentRecNo@1006 : Integer;
      RecordsWithinCommit@1000 : Integer;
      StartedFromRecord@1007 : Integer;
      StartedFromDependentRecord@1010 : Integer;
    BEGIN
      RecRef.OPEN(ChangeGlobalDimLogEntry."Table ID");
      RecRef.LOCKTABLE(true);
      if not RecRef.ISEMPTY then begin
        CurrentRecNo := ChangeGlobalDimLogEntry."Completed Records";
        StartedFromRecord := CurrentRecNo;
        ChangeGlobalDimLogEntry."Total Records" := RecRef.COUNT;
        RecordsWithinCommit := CalcRecordsWithinCommit(ChangeGlobalDimLogEntry."Total Records");
        if RecRef.FINDSET(true) then begin
          if DependentChangeGlobalDimLogEntry.FindDependentTableNo(ChangeGlobalDimLogEntry,DependentRecRef) then begin
            DependentChangeGlobalDimLogEntry.SetSessionInProgress;
            DependentRecNo := DependentChangeGlobalDimLogEntry."Completed Records";
            StartedFromDependentRecord := DependentRecNo;
            DependentChangeGlobalDimLogEntry."Earliest Start Date/Time" := CURRENTDATETIME;
          end;
          if ChangeGlobalDimLogEntry."Completed Records" > 0 then
            RecRef.NEXT(ChangeGlobalDimLogEntry."Completed Records");
          ChangeGlobalDimLogEntry."Earliest Start Date/Time" := CURRENTDATETIME;
          repeat
            ChangeDimsOnRecord(ChangeGlobalDimLogEntry,RecRef);
            CurrentRecNo += 1;
            if DependentChangeGlobalDimLogEntry."Total Records" > 0 then
              ChangeDependentRecords(ChangeGlobalDimLogEntry,DependentChangeGlobalDimLogEntry,RecRef,DependentRecRef,DependentRecNo);

            if CurrentRecNo >= (ChangeGlobalDimLogEntry."Completed Records" + RecordsWithinCommit) then begin
              DependentChangeGlobalDimLogEntry.Update(DependentRecNo,StartedFromDependentRecord);
              Completed := ChangeGlobalDimLogEntry.UpdateWithCommit(CurrentRecNo,StartedFromRecord);
              if DependentRecNo > 0 then
                DependentRecRef.LOCKTABLE;
              RecRef.LOCKTABLE;
            end;
          until RecRef.NEXT = 0;
        end;
        if DependentRecNo > 0 then begin
          DependentRecRef.CLOSE;
          DependentChangeGlobalDimLogEntry.Update(DependentRecNo,StartedFromDependentRecord);
        end;
        Completed := ChangeGlobalDimLogEntry.UpdateWithCommit(CurrentRecNo,StartedFromRecord);
      end;
      RecRef.CLOSE;
    END;

    LOCAL PROCEDURE ChangeDimsOnRecord@50(ChangeGlobalDimLogEntry@1003 : Record "Change Global Dim. Log Entry";VAR RecRef@1000 : RecordRef) : Boolean;
    VAR
      GlobalDimFieldRef@1001 : ARRAY [2] OF FieldRef;
      OldDimValueCode@1002 : ARRAY [2] OF Code[20];
    BEGIN
      with ChangeGlobalDimLogEntry do begin
        if ("Change Type 1" = "Change Type 1"::None) and ("Change Type 2" = "Change Type 2"::None) then
          exit(false);

        GetFieldRefValues(RecRef,GlobalDimFieldRef,OldDimValueCode);
        ChangeDimOnRecord(RecRef,1,GlobalDimFieldRef[1],OldDimValueCode[2]);
        ChangeDimOnRecord(RecRef,2,GlobalDimFieldRef[2],OldDimValueCode[1]);
        exit(RecRef.MODIFY);
      end;
    END;

    LOCAL PROCEDURE ChangeDependentRecords@42(ParentChangeGlobalDimLogEntry@1005 : Record "Change Global Dim. Log Entry";ChangeGlobalDimLogEntry@1009 : Record "Change Global Dim. Log Entry";ParentRecRef@1000 : RecordRef;VAR RecRef@1002 : RecordRef;VAR CurrentRecNo@1008 : Integer);
    VAR
      ParentKeyValue@1007 : Variant;
      GlobalDimFieldRef@1001 : ARRAY [2] OF FieldRef;
      ParentKeyFieldRef@1006 : FieldRef;
      ParentDimValueCode@1003 : ARRAY [2] OF Code[20];
      DimValueCode@1004 : ARRAY [2] OF Code[20];
    BEGIN
      ParentChangeGlobalDimLogEntry.GetFieldRefValues(ParentRecRef,GlobalDimFieldRef,ParentDimValueCode);
      ChangeGlobalDimLogEntry.GetPrimaryKeyFieldRef(ParentRecRef,ParentKeyFieldRef);
      ParentKeyValue := ParentKeyFieldRef.VALUE;

      ParentKeyFieldRef := RecRef.FIELD(2);
      ParentKeyFieldRef.SETRANGE(ParentKeyValue);
      if RecRef.FINDSET(true) then begin
        repeat
          ChangeGlobalDimLogEntry.GetFieldRefValues(RecRef,GlobalDimFieldRef,DimValueCode);
          GlobalDimFieldRef[1].VALUE(ParentDimValueCode[1]);
          GlobalDimFieldRef[2].VALUE(ParentDimValueCode[2]);
          RecRef.MODIFY;
          CurrentRecNo += 1;
        until RecRef.NEXT = 0;
      end;
    END;

    PROCEDURE Rerun@38(ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry");
    BEGIN
      with ChangeGlobalDimLogEntry do
        if Status in [Status::" ",Status::Incomplete] then begin
          if "Parent Table ID" <> 0 then
            RescheduleParentTable("Parent Table ID")
          else
            ScheduleJobForTable(ChangeGlobalDimLogEntry,CURRENTDATETIME + 2000);
        end;
    END;

    LOCAL PROCEDURE RescheduleParentTable@37(ParentTableID@1001 : Integer);
    VAR
      ParentChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
    BEGIN
      ParentChangeGlobalDimLogEntry.GET(ParentTableID);
      ParentChangeGlobalDimLogEntry.VALIDATE("Completed Records",0);
      ScheduleJobForTable(ParentChangeGlobalDimLogEntry,CURRENTDATETIME + 100);
    END;

    LOCAL PROCEDURE ScheduleJobs@11(DeltaMsec@1002 : Integer);
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
      StartTime@1001 : DateTime;
    BEGIN
      if FindTablesForScheduling(ChangeGlobalDimLogEntry) then begin
        StartTime := CURRENTDATETIME + 1000;
        repeat
          StartTime += DeltaMsec;
          ScheduleJobForTable(ChangeGlobalDimLogEntry,StartTime);
        until ChangeGlobalDimLogEntry.NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE ScheduleJobForTable@24(ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";StartNotBefore@1003 : DateTime);
    VAR
      DoNotScheduleTask@1002 : Boolean;
      TaskID@1001 : GUID;
    BEGIN
      with ChangeGlobalDimLogEntry do begin
        OnBeforeScheduleTask("Table ID",DoNotScheduleTask,TaskID);
        if DoNotScheduleTask then
          "Task ID" := TaskID
        else
          "Task ID" :=
            TASKSCHEDULER.CREATETASK(
              CODEUNIT::"Change Global Dimensions",CODEUNIT::"Change Global Dim Err. Handler",
              true,COMPANYNAME,StartNotBefore,RECORDID);
        if ISNULLGUID("Task ID") then
          Status := Status::" "
        else
          Status := Status::Scheduled;
        "Earliest Start Date/Time" := StartNotBefore;
        MODIFY;
      end;
      if ChangeGlobalDimLogEntry."Is Parent Table" then
        ScheduleDependentTables(ChangeGlobalDimLogEntry);
    END;

    LOCAL PROCEDURE ScheduleDependentTables@30(ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry");
    VAR
      DependentChangeGlobalDimLogEntry@1001 : Record "Change Global Dim. Log Entry";
    BEGIN
      with DependentChangeGlobalDimLogEntry do begin
        SETRANGE("Parent Table ID",ChangeGlobalDimLogEntry."Table ID");
        if FINDSET then
          repeat
            "Task ID" := ChangeGlobalDimLogEntry."Task ID";
            VALIDATE("Completed Records",0);
            Status := ChangeGlobalDimLogEntry.Status;
            MODIFY;
          until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE IsCurrentSessionActiveOnly@21() Result : Boolean;
    VAR
      ActiveSession@1000 : Record "Active Session";
    BEGIN
      OnCountingActiveSessions(Result);
      if Result then
        exit(true);
      // Ignore session types: Web Service,Client Service,NAS,Management Client
      ActiveSession.SETFILTER(
        "Client Type",'<>%1&<>%2&<>%3&<>%4',
        ActiveSession."Client Type"::"Web Service",ActiveSession."Client Type"::"Client Service",
        ActiveSession."Client Type"::NAS,ActiveSession."Client Type"::"Management Client");
      ActiveSession.SETFILTER("Session ID",'<>%1',SESSIONID);
      ActiveSession.SETRANGE("Server Instance ID",SERVICEINSTANCEID);
      exit(ActiveSession.ISEMPTY);
    END;

    LOCAL PROCEDURE FindConcurrentTaskInProgress@53(TableID@1000 : Integer) RemainingDuration : Duration;
    VAR
      ChangeGlobalDimLogEntry@1001 : Record "Change Global Dim. Log Entry";
    BEGIN
      ChangeGlobalDimLogEntry.SETFILTER("Table ID",GetConcurrentTableFilter(TableID));
      ChangeGlobalDimLogEntry.SETRANGE(Status,ChangeGlobalDimLogEntry.Status::"In Progress");
      if ChangeGlobalDimLogEntry.FINDFIRST then
        RemainingDuration := ChangeGlobalDimLogEntry."Remaining Duration";
    END;

    LOCAL PROCEDURE GetConcurrentTableFilter@55(TableID@1000 : Integer) : Text;
    BEGIN
      case TableID of
        DATABASE::"Sales Header",
        DATABASE::"Sales Line":
          exit('36|37');
        DATABASE::"Purchase Header",
        DATABASE::"Purchase Line":
          exit('38|39');
        DATABASE::"Cust. Ledger Entry",
        DATABASE::"Sales Invoice Header",
        DATABASE::"Sales Invoice Line",
        DATABASE::"Sales Cr.Memo Header",
        DATABASE::"Sales Cr.Memo Line":
          exit('21|112|113|114|115');
        DATABASE::"Vendor Ledger Entry",
        DATABASE::"Purch. Inv. Header",
        DATABASE::"Purch. Inv. Line",
        DATABASE::"Purch. Cr. Memo Hdr.",
        DATABASE::"Purch. Cr. Memo Line":
          exit('25|122|123|124|125');
      end;
    END;

    PROCEDURE IsDimCodeEnabled@8() : Boolean;
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
    BEGIN
      exit(ChangeGlobalDimLogEntry.ISEMPTY);
    END;

    [External]
    PROCEDURE IsPrepareEnabled@5() : Boolean;
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
    BEGIN
      exit(
        ((ChangeType[1] <> ChangeType[1]::None) or (ChangeType[2] <> ChangeType[2]::None)) and
        ChangeGlobalDimLogEntry.ISEMPTY);
    END;

    PROCEDURE IsStartEnabled@28() : Boolean;
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
    BEGIN
      if ChangeGlobalDimLogEntry.ISEMPTY then
        exit(false);
      exit(not IsStarted);
    END;

    LOCAL PROCEDURE IsStarted@32() : Boolean;
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
    BEGIN
      ChangeGlobalDimLogEntry.SETFILTER(Status,'<>%1',ChangeGlobalDimLogEntry.Status::" ");
      exit(not ChangeGlobalDimLogEntry.ISEMPTY);
    END;

    [External]
    PROCEDURE Initialize@33();
    BEGIN
      InitNewGlobalDimCodes;
      if IsStartEnabled then
        RestoreNewDimCodes;
    END;

    [External]
    PROCEDURE InitNewGlobalDimCodes@3();
    BEGIN
      GeneralLedgerSetup.GET;
      CurrentGlobalDimCode[1] := GeneralLedgerSetup."Global Dimension 1 Code";
      CurrentGlobalDimCode[2] := GeneralLedgerSetup."Global Dimension 2 Code";
      NewGlobalDimCode[1] := CurrentGlobalDimCode[1];
      NewGlobalDimCode[2] := CurrentGlobalDimCode[2];
      CLEAR(ChangeType);
    END;

    [External]
    PROCEDURE GetCurrentGlobalDimCodes@16(VAR GlobalDimCode@1000 : ARRAY [2] OF Code[20]);
    BEGIN
      COPYARRAY(GlobalDimCode,CurrentGlobalDimCode,1);
    END;

    [External]
    PROCEDURE GetNewGlobalDimCodes@4(VAR GlobalDimCode@1000 : ARRAY [2] OF Code[20]) : Boolean;
    BEGIN
      COPYARRAY(GlobalDimCode,NewGlobalDimCode,1);
      exit(IsPrepareEnabled);
    END;

    [External]
    PROCEDURE SetNewGlobalDim1Code@1(NewCode@1000 : Code[20]);
    BEGIN
      ValidateDimCode(NewCode,NewGlobalDimCode[1]);
      if NewGlobalDimCode[1] = NewGlobalDimCode[2] then begin
        NewGlobalDimCode[2] := '';
        ChangeType[2] := ChangeType[2]::Blank
      end;
      CalcChangeType(1);
    END;

    [External]
    PROCEDURE SetNewGlobalDim2Code@2(NewCode@1000 : Code[20]);
    BEGIN
      ValidateDimCode(NewCode,NewGlobalDimCode[2]);
      if NewGlobalDimCode[2] = NewGlobalDimCode[1] then begin
        NewGlobalDimCode[1] := '';
        ChangeType[1] := ChangeType[1]::Blank
      end;
      CalcChangeType(2);
    END;

    LOCAL PROCEDURE CalcChangeType@13(DimNo@1002 : Integer);
    BEGIN
      if NewGlobalDimCode[DimNo] = CurrentGlobalDimCode[GetOtherDimNo(DimNo)] then
        ChangeType[DimNo] := ChangeType[DimNo]::Replace
      else
        if NewGlobalDimCode[DimNo] = CurrentGlobalDimCode[DimNo] then
          ChangeType[DimNo] := ChangeType[DimNo]::None
        else
          if NewGlobalDimCode[DimNo] = '' then
            ChangeType[DimNo] := ChangeType[DimNo]::Blank
          else
            ChangeType[DimNo] := ChangeType[DimNo]::New
    END;

    LOCAL PROCEDURE GetOtherDimNo@15(DimNo@1000 : Integer) : Integer;
    BEGIN
      if DimNo = 1 then
        exit(2);
      exit(1);
    END;

    PROCEDURE InitTableList@7() : Boolean;
    VAR
      ChangeGlobalDimLogEntry@1002 : Record "Change Global Dim. Log Entry";
      TempAllObjWithCaption@1000 : TEMPORARY Record AllObjWithCaption;
      TempParentTableInteger@1001 : TEMPORARY Record Integer;
      TotalRecords@1003 : Integer;
    BEGIN
      TotalRecords := 0;
      ChangeGlobalDimLogEntry.LOCKTABLE;
      ChangeGlobalDimLogEntry.DELETEALL;
      if FindTablesWithDims(TempAllObjWithCaption) then begin
        InsertLogEntryWithNewDimCodes;
        repeat
          ChangeGlobalDimLogEntry.INIT;
          ChangeGlobalDimLogEntry."Table ID" := TempAllObjWithCaption."Object ID";
          ChangeGlobalDimLogEntry."Table Name" := TempAllObjWithCaption."Object Name";
          ChangeGlobalDimLogEntry."Change Type 1" := ChangeType[1];
          ChangeGlobalDimLogEntry."Change Type 2" := ChangeType[2];
          FillTableData(ChangeGlobalDimLogEntry);
          TotalRecords += ChangeGlobalDimLogEntry."Total Records";
          TempParentTableInteger.Number := ChangeGlobalDimLogEntry."Parent Table ID";
          if TempParentTableInteger.Number <> 0 then
            TempParentTableInteger.INSERT;
          ChangeGlobalDimLogEntry.INSERT;
        until TempAllObjWithCaption.NEXT = 0;

        if TempParentTableInteger.FINDSET then
          repeat
            if ChangeGlobalDimLogEntry.GET(TempParentTableInteger.Number) then begin
              ChangeGlobalDimLogEntry."Is Parent Table" := true;
              ChangeGlobalDimLogEntry.MODIFY;
            end;
          until TempParentTableInteger.NEXT = 0;
      end;
      if TotalRecords = 0 then
        ChangeGlobalDimLogEntry.DELETEALL;
      exit(TotalRecords <> 0);
    END;

    LOCAL PROCEDURE TestDirectModifyPermission@51(VAR RecRef@1000 : RecordRef);
    BEGIN
      if RecRef.FINDFIRST then
        RecRef.MODIFY;
    END;

    LOCAL PROCEDURE FillTableData@49(VAR ChangeGlobalDimLogEntry@1001 : Record "Change Global Dim. Log Entry");
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.OPEN(ChangeGlobalDimLogEntry."Table ID");
      TestDirectModifyPermission(RecRef);
      ChangeGlobalDimLogEntry.FillData(RecRef);
      RecRef.CLOSE;
    END;

    LOCAL PROCEDURE FindTablesWithDims@26(VAR TempAllObjWithCaption@1001 : TEMPORARY Record AllObjWithCaption) : Boolean;
    VAR
      DimensionManagement@1000 : Codeunit DimensionManagement;
    BEGIN
      DimensionManagement.DefaultDimObjectNoWithGlobalDimsList(TempAllObjWithCaption);
      DimensionManagement.GlobalDimObjectNoList(TempAllObjWithCaption);
      DimensionManagement.JobTaskDimObjectNoList(TempAllObjWithCaption);
      OnAfterGetObjectNoList(TempAllObjWithCaption);
      exit(TempAllObjWithCaption.FINDSET);
    END;

    LOCAL PROCEDURE UpdateGLSetup@6();
    BEGIN
      GeneralLedgerSetup.GET;
      GeneralLedgerSetup.VALIDATE("Global Dimension 1 Code",NewGlobalDimCode[1]);
      GeneralLedgerSetup.VALIDATE("Global Dimension 2 Code",NewGlobalDimCode[2]);
      GeneralLedgerSetup.MODIFY(true);

      UpdateDimValues;
      COMMIT;
    END;

    LOCAL PROCEDURE InsertLogEntryWithNewDimCodes@48();
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
    BEGIN
      ChangeGlobalDimLogEntry.INIT;
      ChangeGlobalDimLogEntry."Table ID" := 0;
      ChangeGlobalDimLogEntry."Table Name" := STRSUBSTNO('%1;%2',NewGlobalDimCode[1],NewGlobalDimCode[2]);
      ChangeGlobalDimLogEntry.INSERT;
    END;

    LOCAL PROCEDURE RestoreNewDimCodes@43();
    VAR
      ChangeGlobalDimLogEntry@1000 : Record "Change Global Dim. Log Entry";
      NewDimCodes@1001 : Code[30];
      Pos@1002 : Integer;
    BEGIN
      ChangeGlobalDimLogEntry.GET(0);
      NewDimCodes := ChangeGlobalDimLogEntry."Table Name";
      Pos := STRPOS(NewDimCodes,';');
      SetNewGlobalDim1Code(COPYSTR(NewDimCodes,1,Pos - 1));
      SetNewGlobalDim2Code(COPYSTR(NewDimCodes,Pos + 1));
    END;

    LOCAL PROCEDURE UpdateDimValues@14();
    VAR
      DimensionValue@1000 : Record "Dimension Value";
    BEGIN
      with DimensionValue do begin
        SETCURRENTKEY(Code,"Global Dimension No.");
        SETRANGE("Global Dimension No.",1,2);
        MODIFYALL("Global Dimension No.",0);
        RESET;
        if NewGlobalDimCode[1] <> '' then begin
          SETRANGE("Dimension Code",NewGlobalDimCode[1]);
          MODIFYALL("Global Dimension No.",1);
        end;
        if NewGlobalDimCode[2] <> '' then begin
          SETRANGE("Dimension Code",NewGlobalDimCode[2]);
          MODIFYALL("Global Dimension No.",2);
        end;
      end;
    END;

    LOCAL PROCEDURE ValidateDimCode@9(NewCode@1000 : Code[20];VAR Code@1001 : Code[20]);
    VAR
      Dimension@1002 : Record Dimension;
    BEGIN
      if NewCode <> '' then begin
        Dimension.GET(NewCode);
        if IsUsedInShortcurDims(NewCode) then
          ERROR(DimIsUsedInGLSetupErr,NewCode);
      end;
      Code := NewCode;
    END;

    LOCAL PROCEDURE IsUsedInShortcurDims@17(DimensionCode@1000 : Code[20]) : Boolean;
    BEGIN
      exit(
        DimensionCode in
        [GeneralLedgerSetup."Shortcut Dimension 3 Code",
         GeneralLedgerSetup."Shortcut Dimension 4 Code",
         GeneralLedgerSetup."Shortcut Dimension 5 Code",
         GeneralLedgerSetup."Shortcut Dimension 6 Code",
         GeneralLedgerSetup."Shortcut Dimension 7 Code",
         GeneralLedgerSetup."Shortcut Dimension 8 Code"]);
    END;

    PROCEDURE GetBindNotificationID@34() Id : GUID;
    BEGIN
      EVALUATE(Id,BindNotificationIDTok);
    END;

    PROCEDURE GetCloseSessionsNotificationID@45() Id : GUID;
    BEGIN
      EVALUATE(Id,CloseSessionNotificationTok);
    END;

    LOCAL PROCEDURE PrepareNotification@25(VAR Notification@1000 : Notification;ID@1001 : GUID;Msg@1002 : Text);
    BEGIN
      Notification.ID(ID);
      Notification.RECALL;
      Notification.MESSAGE(Msg);
      Notification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
    END;

    LOCAL PROCEDURE SendBindNotification@35();
    VAR
      Notification@1000 : Notification;
    BEGIN
      PrepareNotification(Notification,GetBindNotificationID,BindNotificationMsg);
      Notification.SEND;
    END;

    LOCAL PROCEDURE SendCloseSessionsNotification@44();
    VAR
      DummySessionListPage@1001 : Page "Session List";
      Notification@1000 : Notification;
    BEGIN
      PrepareNotification(Notification,GetCloseSessionsNotificationID,CloseActiveSessionsMsg);
      Notification.ADDACTION(DummySessionListPage.CAPTION,CODEUNIT::"Change Global Dimensions",'ShowActiveSessions');
      Notification.SEND;
    END;

    PROCEDURE ShowActiveSessions@46(BlockNotification@1000 : Notification);
    BEGIN
      PAGE.RUN(PAGE::"Concurrent Session List");
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterGetObjectNoList@18(VAR TempAllObjWithCaption@1000 : TEMPORARY Record AllObjWithCaption);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCountingActiveSessions@36(VAR IsCurrSessionActiveOnly@1000 : Boolean);
    BEGIN
    END;

    [EventSubscriber(Codeunit,1,OnBeforeCompanyOpen)]
    LOCAL PROCEDURE OnBeforeCompanyOpenHandler@22();
    BEGIN
      ActivateBlock;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeScheduleTask@29(TableNo@1002 : Integer;VAR DoNotScheduleTask@1000 : Boolean;VAR TaskID@1001 : GUID);
    BEGIN
    END;

    BEGIN
    END.
  }
}

