OBJECT Page 1173 Document Attachment Details
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=161336.443T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    Editable=true;
    CaptionML=ENU=Attached Documents;
    SourceTable="Document Attachment";
    DelayedInsert=true;
    SourceTableView=SORTING(ID,"Table ID");
    PageType=Worksheet;
    OnInit=BEGIN
             FlowFieldsEditable := true;
           END;

    OnNewRecord=BEGIN
                  "File Name" := SelectFileTxt;
                END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      Name=Actions;
                      CaptionML=ENU=Actions;
                      ActionContainerType=ActionItems }
      { 8       ;1   ;Action    ;
                      Name=Preview;
                      CaptionML=ENU=Preview;
                      ToolTipML=ENU=Get a preview of the attachment.;
                      ApplicationArea=#All;
                      Promoted=true;
                      PromotedIsBig=true;
                      Image=Export;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 if "File Name" <> '' then
                                   Export(true);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Control1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                Name=Name;
                ToolTipML=ENU=Specifies the filename of the attachment.;
                ApplicationArea=#All;
                SourceExpr="File Name";
                Editable=FALSE;
                OnDrillDown=VAR
                              TempBlob@1002 : Record TempBlob;
                              FileManagement@1001 : Codeunit "File Management";
                              FileName@1000 : Text;
                            BEGIN
                              if "Document Reference ID".HASVALUE then
                                Export(true)
                              else begin
                                FileName := FileManagement.BLOBImportWithFilter(TempBlob,ImportTxt,FileName,STRSUBSTNO(FileDialogTxt,FilterTxt),FilterTxt);
                                if FileName <> '' then
                                  SaveAttachment(FromRecRef,FileName,TempBlob);
                                CurrPage.UPDATE(false);
                              end;
                            END;

                ImplicitType=Text250 }

    { 10  ;2   ;Field     ;
                Name="File Extension";
                ToolTipML=ENU=Specifies the file extension of the attachment.;
                ApplicationArea=#All;
                SourceExpr="File Extension";
                Editable=FALSE;
                ImplicitType=Text30 }

    { 5   ;2   ;Field     ;
                Name="File Type";
                ToolTipML=ENU=Specifies the type of document that the attachment is.;
                ApplicationArea=#All;
                SourceExpr="File Type";
                Editable=FALSE;
                ImplicitType=Option }

    { 3   ;2   ;Field     ;
                Name=User;
                ToolTipML=ENU=Specifies the user who attached the document.;
                ApplicationArea=#All;
                SourceExpr=User;
                Editable=FALSE;
                ImplicitType=Code50 }

    { 6   ;2   ;Field     ;
                Name="Attached Date";
                ToolTipML=ENU=Specifies the date when the document was attached.;
                ApplicationArea=#All;
                SourceExpr="Attached Date";
                ImplicitType=DateTime }

    { 9   ;2   ;Field     ;
                Name="Document Flow Purchase";
                ToolTipML=ENU=Specifies if the attachment must flow to transactions.;
                ApplicationArea=#All;
                SourceExpr="Document Flow Purchase";
                CaptionClass=GetCaptionClass(9);
                Visible=PurchaseDocumentFlow;
                Editable=FlowFieldsEditable;
                ImplicitType=Boolean }

    { 11  ;2   ;Field     ;
                Name="Document Flow Sales";
                ToolTipML=ENU=Specifies if the attachment must flow to transactions.;
                ApplicationArea=#All;
                SourceExpr="Document Flow Sales";
                CaptionClass=GetCaptionClass(11);
                Visible=SalesDocumentFlow;
                Editable=FlowFieldsEditable;
                ImplicitType=Boolean }

  }
  CODE
  {
    VAR
      FromRecRef@1009 : RecordRef;
      SalesDocumentFlow@1002 : Boolean;
      FileDialogTxt@1003 : TextConst '@@@="%1=file types, such as *.txt or *.docx";ENU=Attachments (%1)|%1';
      FilterTxt@1001 : TextConst '@@@={Locked};ENU="*.jpg;*.jpeg;*.bmp;*.png;*.gif;*.tiff;*.tif;*.pdf;*.docx;*.doc;*.xlsx;*.xls;*.pptx;*.ppt;*.msg;*.xml;*.*"';
      ImportTxt@1000 : TextConst 'ENU=Attach a document.';
      SelectFileTxt@1004 : TextConst 'ENU=Select File...';
      PurchaseDocumentFlow@1005 : Boolean;
      FlowToPurchTxt@1006 : TextConst 'ENU=Flow to Purch. Trx';
      FlowToSalesTxt@1007 : TextConst 'ENU=Flow to Sales Trx';
      FlowFieldsEditable@1008 : Boolean;

    LOCAL PROCEDURE GetCaptionClass@2(FieldNo@1000 : Integer) : Text;
    BEGIN
      if SalesDocumentFlow and PurchaseDocumentFlow then
        case FieldNo of
          9:
            exit(FlowToPurchTxt);
          11:
            exit(FlowToSalesTxt);
        end;
    END;

    PROCEDURE OpenForRecRef@3(RecRef@1000 : RecordRef);
    VAR
      FieldRef@1001 : FieldRef;
      RecNo@1002 : Code[20];
      DocType@1003 : 'Quote,Order,Invoice,"Credit Memo","Blanket Order","Return Order"';
      LineNo@1004 : Integer;
    BEGIN
      RESET;

      FromRecRef := RecRef;

      SETRANGE("Table ID",RecRef.NUMBER);

      if RecRef.NUMBER = DATABASE::Item then begin
        SalesDocumentFlow := true;
        PurchaseDocumentFlow := true;
      end;

      case RecRef.NUMBER of
        DATABASE::Customer,
        DATABASE::"Sales Header",
        DATABASE::"Sales Line",
        DATABASE::"Sales Invoice Header",
        DATABASE::"Sales Invoice Line",
        DATABASE::"Sales Cr.Memo Header",
        DATABASE::"Sales Cr.Memo Line":
          SalesDocumentFlow := true;
        DATABASE::Vendor,
        DATABASE::"Purchase Header",
        DATABASE::"Purchase Line",
        DATABASE::"Purch. Inv. Header",
        DATABASE::"Purch. Inv. Line",
        DATABASE::"Purch. Cr. Memo Hdr.",
        DATABASE::"Purch. Cr. Memo Line":
          PurchaseDocumentFlow := true;
      end;

      case RecRef.NUMBER of
        DATABASE::Customer,
        DATABASE::Vendor,
        DATABASE::Item,
        DATABASE::Employee,
        DATABASE::"Fixed Asset",
        DATABASE::Job,
        DATABASE::Resource:
          begin
            FieldRef := RecRef.FIELD(1);
            RecNo := FieldRef.VALUE;
            SETRANGE("No.",RecNo);
          end;
      end;

      case RecRef.NUMBER of
        DATABASE::"Sales Header",
        DATABASE::"Sales Line",
        DATABASE::"Purchase Header",
        DATABASE::"Purchase Line":
          begin
            FieldRef := RecRef.FIELD(1);
            DocType := FieldRef.VALUE;
            SETRANGE("Document Type",DocType);

            FieldRef := RecRef.FIELD(3);
            RecNo := FieldRef.VALUE;
            SETRANGE("No.",RecNo);

            FlowFieldsEditable := false;
          end;
      end;

      case RecRef.NUMBER of
        DATABASE::"Sales Line",
        DATABASE::"Purchase Line":
          begin
            FieldRef := RecRef.FIELD(4);
            LineNo := FieldRef.VALUE;
            SETRANGE("Line No.",LineNo);
          end;
      end;

      case RecRef.NUMBER of
        DATABASE::"Sales Invoice Header",
        DATABASE::"Sales Cr.Memo Header",
        DATABASE::"Purch. Inv. Header",
        DATABASE::"Purch. Cr. Memo Hdr.":
          begin
            FieldRef := RecRef.FIELD(3);
            RecNo := FieldRef.VALUE;
            SETRANGE("No.",RecNo);

            FlowFieldsEditable := false;
          end;
      end;

      case RecRef.NUMBER of
        DATABASE::"Sales Invoice Line",
        DATABASE::"Sales Cr.Memo Line",
        DATABASE::"Purch. Inv. Line",
        DATABASE::"Purch. Cr. Memo Line":
          begin
            FieldRef := RecRef.FIELD(3);
            RecNo := FieldRef.VALUE;
            SETRANGE("No.",RecNo);

            FieldRef := RecRef.FIELD(4);
            LineNo := FieldRef.VALUE;
            SETRANGE("Line No.",LineNo);

            FlowFieldsEditable := false;
          end;
      end;
    END;

    BEGIN
    END.
  }
}

