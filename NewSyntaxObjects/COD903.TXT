OBJECT Codeunit 903 Release Assembly Document
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160629.20T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    TableNo="Assembly Header";
    Permissions=TableData "Assembly Header"=m,
                TableData "Assembly Line"=r;
    OnRun=VAR
            AssemblyLine@1001 : Record "Assembly Line";
            InvtSetup@1000 : Record "Inventory Setup";
            WhseAssemblyRelease@1002 : Codeunit "Whse.-Assembly Release";
          BEGIN
            if Status = Status::Released then
              exit;

            OnBeforeReleaseAssemblyDoc(Rec);

            AssemblyLine.SETRANGE("Document Type","Document Type");
            AssemblyLine.SETRANGE("Document No.","No.");
            AssemblyLine.SETFILTER(Type,'<>%1',AssemblyLine.Type::" ");
            AssemblyLine.SETFILTER(Quantity,'<>0');
            if not AssemblyLine.FIND('-') then
              ERROR(Text001,"Document Type","No.");

            InvtSetup.GET;
            if InvtSetup."Location Mandatory" then begin
              AssemblyLine.SETRANGE(Type,AssemblyLine.Type::Item);
              if AssemblyLine.FINDSET then
                repeat
                  AssemblyLine.TESTFIELD("Location Code");
                until AssemblyLine.NEXT = 0;
            end;

            Status := Status::Released;
            MODIFY;

            if "Document Type" = "Document Type"::Order then
              WhseAssemblyRelease.Release(Rec);

            OnAfterReleaseAssemblyDoc(Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst '@@@="%1 = Document Type, %2 = No.";ENU=There is nothing to release for %1 %2.';

    [External]
    PROCEDURE Reopen@1(VAR AssemblyHeader@1000 : Record "Assembly Header");
    VAR
      WhseAssemblyRelease@1001 : Codeunit "Whse.-Assembly Release";
    BEGIN
      with AssemblyHeader do begin
        if Status = Status::Open then
          exit;

        OnBeforeReopenAssemblyDoc(AssemblyHeader);

        Status := Status::Open;
        MODIFY(true);

        if "Document Type" = "Document Type"::Order then
          WhseAssemblyRelease.Reopen(AssemblyHeader);

        OnAfterReopenAssemblyDoc(AssemblyHeader);
      end;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReleaseAssemblyDoc@2(VAR AssemblyHeader@1000 : Record "Assembly Header");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReopenAssemblyDoc@5(VAR AssemblyHeader@1000 : Record "Assembly Header");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReleaseAssemblyDoc@3(VAR AssemblyHeader@1000 : Record "Assembly Header");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReopenAssemblyDoc@4(VAR AssemblyHeader@1000 : Record "Assembly Header");
    BEGIN
    END;

    BEGIN
    END.
  }
}

