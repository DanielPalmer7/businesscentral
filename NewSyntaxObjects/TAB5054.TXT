OBJECT Table 5054 Contact Business Relation
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160133.453T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ContBusRel@1000 : Record "Contact Business Relation";
               Cont@1001 : Record Contact;
             BEGIN
               if "No." <> '' then begin
                 if ContBusRel.FindByContact("Link to Table","Contact No.") then
                   ERROR(
                     Text000,
                     Cont.TABLECAPTION,"Contact No.",TABLECAPTION,"Link to Table",ContBusRel."No.");

                 if ContBusRel.FindByRelation("Link to Table","No.") then
                   if GetContactBusinessRelation(ContBusRel) then
                     ERROR(
                       Text000,
                       "Link to Table","No.",TABLECAPTION,Cont.TABLECAPTION,ContBusRel."Contact No.");

                 ContBusRel.RESET;
                 ContBusRel.SETRANGE("Contact No.","Contact No.");
                 ContBusRel.SETRANGE("Business Relation Code","Business Relation Code");
                 ContBusRel.SETRANGE("No.",'');
                 ContBusRel.DELETEALL;
               end;
             END;

    CaptionML=ENU=Contact Business Relation;
    DrillDownPageID="Contact Business Relations";
  }
  FIELDS
  {
    { 1   ;   ;Contact No.         ;Code20        ;TableRelation=Contact WHERE (Type=CONST(Company));
                                                   OnValidate=BEGIN
                                                                if "Contact No." <> '' then
                                                                  VALIDATE("Business Relation Code");
                                                              END;

                                                   CaptionML=ENU=Contact No.;
                                                   NotBlank=true }
    { 2   ;   ;Business Relation Code;Code10      ;TableRelation="Business Relation";
                                                   OnValidate=VAR
                                                                RMSetup@1000 : Record "Marketing Setup";
                                                                Cust@1001 : Record Customer;
                                                                Vend@1002 : Record Vendor;
                                                                BankAcc@1003 : Record "Bank Account";
                                                              BEGIN
                                                                if ("No." = '') and
                                                                   ("Contact No." <> '') and
                                                                   ("Business Relation Code" <> '') and
                                                                   (CurrFieldNo <> 0)
                                                                then begin
                                                                  RMSetup.GET;
                                                                  if "Business Relation Code" = RMSetup."Bus. Rel. Code for Customers" then
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,Cust.TABLECAPTION);
                                                                  if "Business Relation Code" = RMSetup."Bus. Rel. Code for Vendors" then
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,Vend.TABLECAPTION);
                                                                  if "Business Relation Code" = RMSetup."Bus. Rel. Code for Bank Accs." then
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,BankAcc.TABLECAPTION);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Business Relation Code;
                                                   NotBlank=true }
    { 3   ;   ;Link to Table       ;Option        ;CaptionML=ENU=Link to Table;
                                                   OptionCaptionML=ENU=" ,Customer,Vendor,Bank Account";
                                                   OptionString=" ",Customer,Vendor,"Bank Account" }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF ("Link to Table"=CONST(Customer)) Customer
                                                                 ELSE IF ("Link to Table"=CONST(Vendor)) Vendor
                                                                 ELSE IF ("Link to Table"=CONST("Bank Account")) "Bank Account";
                                                   CaptionML=ENU=No. }
    { 5   ;   ;Business Relation Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Business Relation".Description WHERE (Code=FIELD("Business Relation Code")));
                                                   CaptionML=ENU=Business Relation Description;
                                                   Editable=false }
    { 6   ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE ("No."=FIELD("Contact No.")));
                                                   CaptionML=ENU=Contact Name;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Contact No.","Business Relation Code"  ;Clustered=true }
    {    ;"Link to Table","No."                    }
    {    ;"Link to Table","Contact No."            }
    {    ;"Business Relation Code"                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 already has a %3 with %4 %5.';
      Text001@1001 : TextConst 'ENU=%1 %2 is used when a %3 is linked with a %4.';
      Cont@1002 : Record Contact;

    LOCAL PROCEDURE GetContactBusinessRelation@2(ContactBusinessRelation@1000 : Record "Contact Business Relation") : Boolean;
    VAR
      Customer@1001 : Record Customer;
      Vendor@1002 : Record Vendor;
      BankAccount@1003 : Record "Bank Account";
    BEGIN
      case ContactBusinessRelation."Link to Table" of
        ContactBusinessRelation."Link to Table"::"Bank Account":
          exit(BankAccount.GET(ContactBusinessRelation."No."));
        ContactBusinessRelation."Link to Table"::Customer:
          exit(Customer.GET(ContactBusinessRelation."No."));
        ContactBusinessRelation."Link to Table"::Vendor:
          exit(Vendor.GET(ContactBusinessRelation."No."));
      end;
    END;

    PROCEDURE FindByContact@3(LinkType@1000 : Option;ContactNo@1001 : Code[20]) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Link to Table","Contact No.");
      SETRANGE("Link to Table",LinkType);
      SETRANGE("Contact No.",ContactNo);
      exit(FINDFIRST);
    END;

    PROCEDURE FindByRelation@4(LinkType@1000 : Option;LinkNo@1001 : Code[20]) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Link to Table","No.");
      SETRANGE("Link to Table",LinkType);
      SETRANGE("No.",LinkNo);
      exit(FINDFIRST);
    END;

    PROCEDURE GetContactNo@1(LinkType@1000 : Option;LinkNo@1001 : Code[20]) : Code[20];
    BEGIN
      if FindByRelation(LinkType,LinkNo) then
        exit("Contact No.");
      exit('');
    END;

    PROCEDURE CreateRelation@5(ContactNo@1002 : Code[20];LinkNo@1004 : Code[20];LinkToTable@1003 : Option);
    BEGIN
      INIT;
      "Contact No." := ContactNo;
      "Business Relation Code" := GetBusinessRelationCodeFromSetup(LinkToTable);
      "Link to Table" := LinkToTable;
      "No." := LinkNo;
      INSERT(true);
    END;

    PROCEDURE FindOrRestoreContactBusinessRelation@7(VAR Cont@1000 : Record Contact;RecVar@1006 : Variant;LinkToTable@1002 : Option);
    VAR
      ContCompany@1004 : Record Contact;
      CustContUpdate@1008 : Codeunit "CustCont-Update";
      VendContUpdate@1005 : Codeunit "VendCont-Update";
      RecRef@1007 : RecordRef;
      FieldRef@1010 : FieldRef;
    BEGIN
      RecRef.GETTABLE(RecVar);
      FieldRef := RecRef.FIELD(1);

      if not FindByRelation(LinkToTable,FORMAT(FieldRef.VALUE)) then
        if Cont.Type = Cont.Type::Person then
          if ContCompany.GET(Cont."Company No.") then begin
            ContCompany.CheckForExistingRelationships(LinkToTable);
            CreateRelation(ContCompany."No.",FORMAT(FieldRef.VALUE),LinkToTable);
          end else begin
            case RecRef.NUMBER of
              DATABASE::Customer:
                CustContUpdate.OnInsert(RecVar);
              DATABASE::Vendor:
                VendContUpdate.OnInsert(RecVar);
            end;
            FINDFIRST;
            Cont.VALIDATE("Company No.","Contact No.");
            Cont.MODIFY(true);
          end;
    END;

    LOCAL PROCEDURE GetBusinessRelationCodeFromSetup@6(LinkToTable@1002 : Option) : Code[10];
    VAR
      MarketingSetup@1000 : Record "Marketing Setup";
      ContactBusinessRelation@1001 : Record "Contact Business Relation";
    BEGIN
      MarketingSetup.GET;
      case LinkToTable of
        ContactBusinessRelation."Link to Table"::Customer:
          begin
            MarketingSetup.TESTFIELD("Bus. Rel. Code for Customers");
            exit(MarketingSetup."Bus. Rel. Code for Customers");
          end;
        ContactBusinessRelation."Link to Table"::Vendor:
          begin
            MarketingSetup.TESTFIELD("Bus. Rel. Code for Vendors");
            exit(MarketingSetup."Bus. Rel. Code for Vendors");
          end;
      end;
    END;

    BEGIN
    END.
  }
}

