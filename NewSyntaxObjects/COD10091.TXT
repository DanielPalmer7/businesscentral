OBJECT Codeunit 10091 Export Payments (RB)
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160927.310T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1020012 : TextConst 'ENU=Cannot start new Export File while %1 is in process.';
      Text001@1020011 : TextConst 'ENU=is not valid';
      Text002@1020010 : TextConst 'ENU=%1 in %2 %3 is invalid.';
      Text003@1020009 : TextConst 'ENU=File %1 already exists. Check the %2 in %3 %4.';
      Text004@1020008 : TextConst 'ENU=Cannot export details until an export file is started.';
      Text015@1020007 : TextConst 'ENU=Either %1 or %2 must refer to either a %3 or a %4 for an electronic payment.';
      Text016@1020006 : TextConst 'ENU=Vendor No. %1 has no bank account setup for electronic payments.';
      Text017@1020005 : TextConst 'ENU=Vendor No. %1 has more than one bank account setup for electronic payments.';
      Text018@1020004 : TextConst 'ENU=Customer No. %1 has no bank account setup for electronic payments.';
      Text019@1020003 : TextConst 'ENU=Customer No. %1 has more than one bank account setup for electronic payments.';
      Text005@1020002 : TextConst 'ENU=Cannot end export file until an export file is started.';
      Text006@1020001 : TextConst 'ENU=File %1 does not exist.';
      Text047@1020000 : TextConst 'ENU=Did the transmission work properly?';
      Text1020100@1020100 : TextConst 'ENU=%1 is blocked for %2 processing';
      PrivacyBlockedErr@1234567 : TextConst '@@@="%1 = accountant type";ENU=%1 is blocked for privacy.';
      Vendor@1020037 : Record Vendor;
      Customer@1020036 : Record Customer;
      BankAccount@1020035 : Record "Bank Account";
      CompanyInformation@1020034 : Record "Company Information";
      RBMgt@1480001 : Codeunit "File Management";
      TotalFileDebit@1020030 : Decimal;
      TotalFileCredit@1020029 : Decimal;
      RecordLength@1020028 : Integer;
      NoOfRec@1020027 : Integer;
      ExportFile@1020025 : File;
      Justification@1020024 : 'Left,Right';
      FileName@1020023 : Text;
      FileIsInProcess@1020021 : Boolean;
      FileDate@1020019 : Date;
      ModifierValues@1020017 : ARRAY [26] OF Code[1];
      TraceNo@1020015 : Integer;
      PaymentsThisAcct@1020013 : Integer;
      Text020@1020038 : TextConst 'ENU=Demand Debits are not supported. Check sign on %1 %2, %3 %4, %5 %6.';
      Text021@1480000 : TextConst 'ENU=You must now run the program that transmits the payments file to the bank. Transmit the file named %1 located at %2 to %3 (%4 %5 %6).  After the transmission is completed, you will be asked if it worked correctly.  Are you ready to transmit (answer No to cancel the transmission process)?';
      Transactions@1030000 : Integer;
      CurrencyType@1030001 : Code[3];
      NoOfCustInfoRec@1030002 : Integer;
      AcctNo@1030003 : Code[20];
      AcctName@1030004 : Text[30];
      AcctLanguage@1030005 : '"E  English","F  French"';
      RecipientAddress@1030006 : Text[80];
      RecipientCity@1030010 : Text[30];
      RecipientCountryCode@1030009 : Text[30];
      RecipientCounty@1030008 : Text[30];
      RecipientPostCode@1030007 : Code[20];
      RecipientBankAcctNo@1030015 : Text[30];
      RecipientTransitNo@1030014 : Text[20];
      RecipientBankNo@1030013 : Text[20];
      RecipientBankAcctCurrencyCode@1030012 : Code[10];
      RecipientBankAcctCountryCode@1030011 : Code[10];

    [External]
    PROCEDURE StartExportFile@6(BankAccountNo@1020001 : Code[20];ReferenceCode@1020000 : Code[10];GenJnlLine@1030010 : Record "Gen. Journal Line");
    VAR
      GLSetup@1030002 : Record "General Ledger Setup";
      FileHeaderRec@1020003 : Text[250];
      i@1020002 : Integer;
    BEGIN
      BuildIDModifier;
      if FileIsInProcess then
        ERROR(Text000,FileName);

      CompanyInformation.GET;
      CompanyInformation.TESTFIELD("Federal ID No.");

      with BankAccount do begin
        LOCKTABLE;
        GET(BankAccountNo);
        TESTFIELD("Export Format","Export Format"::CA);
        TESTFIELD("Transit No.");
        if not CheckDigit("Transit No.") then
          FIELDERROR("Transit No.",Text001);
        TESTFIELD("E-Pay Export File Path");
        if "E-Pay Export File Path"[STRLEN("E-Pay Export File Path")] <> '\' then
          ERROR(Text002,
            FIELDCAPTION("E-Pay Export File Path"),
            TABLECAPTION,
            "No.");
        TESTFIELD("Last E-Pay Export File Name");
        TESTFIELD("Bank Acc. Posting Group");
        TESTFIELD(Blocked,false);
        TESTFIELD("Client No.");
        TESTFIELD("Client Name");

        if GenJnlLine."Bank Payment Type" =
           GenJnlLine."Bank Payment Type"::"Electronic Payment-IAT"
        then begin
          GenJnlLine.TESTFIELD("Transaction Code");
          GenJnlLine.TESTFIELD("Company Entry Description");
        end;

        "Last E-Pay Export File Name" := INCSTR("Last E-Pay Export File Name");
        FileName := RBMgt.ServerTempFileName('');
        if "Last ACH File ID Modifier" = '' then
          "Last ACH File ID Modifier" := '1'
        else begin
          i := 1;
          while (i < ARRAYLEN(ModifierValues)) and
                ("Last ACH File ID Modifier" <> ModifierValues[i])
          do
            i := i + 1;
          if i = ARRAYLEN(ModifierValues) then
            i := 1
          else
            i := i + 1;
          "Last ACH File ID Modifier" := ModifierValues[i];
        end;
        "Last E-Pay File Creation No." := "Last E-Pay File Creation No." + 1;
        MODIFY;

        if EXISTS(FileName) then
          ERROR(Text003,
            FileName,
            FIELDCAPTION("Last E-Pay Export File Name"),
            TABLECAPTION,
            "No.");
        ExportFile.TEXTMODE(true);
        ExportFile.WRITEMODE(true);
        ExportFile.CREATE(FileName);

        FileIsInProcess := true;
        FileDate := TODAY;
        NoOfRec := 0;
        NoOfCustInfoRec := 0;
        TotalFileDebit := 0;
        TotalFileCredit := 0;
        RecordLength := 152;
        Transactions := 0;
        FileHeaderRec := '';
        ExportPrnString(FileHeaderRec);

        FileHeaderRec := '';
        AddNumToPrnString(FileHeaderRec,NoOfRec,1,6);                              // Record Count
        AddToPrnString(FileHeaderRec,'A',7,1,Justification::Left,' ');             // Record Type
        AddToPrnString(FileHeaderRec,'HDR',8,3,Justification::Left,' ');           // Transaction Code
        AddToPrnString(FileHeaderRec,"Client No.",11,10,Justification::Left,'0');  // Client Number
        AddToPrnString(FileHeaderRec,"Client Name",21,30,Justification::Left,' '); // Client Name
        AddNumToPrnString(FileHeaderRec,"Last E-Pay File Creation No.",51,4);      // File Creation Number
        AddNumToPrnString(FileHeaderRec,JulianDate(FileDate),55,7);                // File Creation Date
        if GenJnlLine."Currency Code" = '' then begin
          GLSetup.GET;
          CurrencyType := GLSetup."LCY Code";
        end else
          CurrencyType := GenJnlLine."Currency Code";
        AddToPrnString(FileHeaderRec,CurrencyType,62,3,Justification::Left,' ');
        AddToPrnString(FileHeaderRec,'1',65,1,Justification::Left,' ');            // Input Type
        AddToPrnString(FileHeaderRec,' ',66,15,Justification::Left,' ');
        AddToPrnString(FileHeaderRec,' ',81,6,Justification::Left,' ');
        AddToPrnString(FileHeaderRec,' ',87,8,Justification::Left,' ');
        AddToPrnString(FileHeaderRec,' ',95,9,Justification::Left,' ');
        AddToPrnString(FileHeaderRec,' ',104,46,Justification::Left,' ');
        AddToPrnString(FileHeaderRec,' ',150,2,Justification::Left,' ');
        AddToPrnString(FileHeaderRec,' ',152,1,Justification::Left,' ');
        ExportPrnString(FileHeaderRec);
      end;
    END;

    [External]
    PROCEDURE ExportElectronicPayment@17(GenJnlLine@1020002 : Record "Gen. Journal Line";PaymentAmount@1020001 : Decimal;SettleDate@1020000 : Date) : Code[30];
    VAR
      DemandCredit@1020004 : Boolean;
    BEGIN
      if not FileIsInProcess then
        ERROR(Text004);
      // NOTE:  If PaymentAmount is Positive, then we are Receiving money.
      // If PaymentAmount is Negative, then we are Sending money.
      if PaymentAmount = 0 then
        exit('');
      DemandCredit := (PaymentAmount < 0);
      PaymentAmount := ABS(PaymentAmount);
      if DemandCredit then
        TotalFileCredit := TotalFileCredit + PaymentAmount
      else
        TotalFileDebit := TotalFileDebit + PaymentAmount;
      with GenJnlLine do begin
        if not DemandCredit then // for now, this is not supported for Canada
          ERROR(Text020,
            FIELDCAPTION("Journal Template Name"),"Journal Template Name",
            FIELDCAPTION("Journal Batch Name"),"Journal Batch Name",
            FIELDCAPTION("Line No."),"Line No.");

        GetRecipientData(GenJnlLine);
        WriteRecord(GenJnlLine,PaymentAmount,SettleDate);
      end;

      exit(GenerateFullTraceNoCode(TraceNo));
    END;

    [External]
    PROCEDURE EndExportFile@10() : Boolean;
    VAR
      FileControlRec@1020000 : Text[250];
      ClientFile@1020001 : Text;
    BEGIN
      if not FileIsInProcess then
        ERROR(Text005);

      FileIsInProcess := false;

      FileControlRec := '';
      AddNumToPrnString(FileControlRec,NoOfRec,1,6);                                         // Record Count
      AddToPrnString(FileControlRec,'Z',7,1,Justification::Left,' ');                        // Record Type
      AddToPrnString(FileControlRec,'TRL',8,3,Justification::Left,' ');                      // Transaction Code
      AddToPrnString(FileControlRec,BankAccount."Client No.",11,10,Justification::Left,'0'); // Client Number
      AddNumToPrnString(FileControlRec,Transactions,21,6);                                   // Number of Credit Payment Transactions
      AddAmtToPrnString(FileControlRec,TotalFileCredit,27,14);                               // Total Value of Credit Payment Transactions
      AddToPrnString(FileControlRec,' ',41,6,Justification::Left,'0');
      AddToPrnString(FileControlRec,' ',47,14,Justification::Left,'0');
      AddNumToPrnString(FileControlRec,0,61,2);                                              // Zero Fill
      AddNumToPrnString(FileControlRec,NoOfCustInfoRec,63,6);                                // Number of Cust. Info Records
      AddToPrnString(FileControlRec,' ',69,12,Justification::Left,' ');
      AddToPrnString(FileControlRec,' ',81,6,Justification::Left,' ');
      AddToPrnString(FileControlRec,' ',87,63,Justification::Left,' ');
      AddToPrnString(FileControlRec,' ',150,2,Justification::Left,' ');
      AddToPrnString(FileControlRec,' ',152,1,Justification::Left,' ');
      ExportPrnString(FileControlRec);
      ExportFile.CLOSE;

      ClientFile := BankAccount."E-Pay Export File Path" + BankAccount."Last E-Pay Export File Name";
      RBMgt.DownloadToFile(FileName,ClientFile);
      ERASE(FileName);
      exit(true);
    END;

    LOCAL PROCEDURE GenerateFullTraceNoCode@14(TraceNo@1020000 : Integer) : Code[30];
    VAR
      TraceCode@1020001 : Text[250];
    BEGIN
      TraceCode := '';
      AddNumToPrnString(TraceCode,BankAccount."Last E-Pay File Creation No.",1,6);
      AddNumToPrnString(TraceCode,TraceNo,7,6);
      exit(TraceCode);
    END;

    LOCAL PROCEDURE AddNumToPrnString@12(VAR PrnString@1020003 : Text[250];Number@1020002 : Integer;StartPos@1020001 : Integer;Length@1020000 : Integer);
    VAR
      TmpString@1020005 : Text[250];
    BEGIN
      TmpString := DELCHR(FORMAT(Number),'=','.,-');
      AddToPrnString(PrnString,TmpString,StartPos,Length,Justification::Right,'0');
    END;

    LOCAL PROCEDURE AddAmtToPrnString@3(VAR PrnString@1020003 : Text[250];Amount@1020002 : Decimal;StartPos@1020001 : Integer;Length@1020000 : Integer);
    VAR
      TmpString@1020005 : Text[250];
      I@1020004 : Integer;
    BEGIN
      TmpString := FORMAT(Amount);
      I := STRPOS(TmpString,'.');
      case true of
        I = 0:
          TmpString := TmpString + '.00';
        I = STRLEN(TmpString) - 1:
          TmpString := TmpString + '0';
      end;
      TmpString := DELCHR(TmpString,'=','.,-');
      AddToPrnString(PrnString,TmpString,StartPos,Length,Justification::Right,'0');
    END;

    [External]
    PROCEDURE AddFedIDToPrnString@18(VAR PrnString@1020003 : Text[250];FedID@1020002 : Text[30];StartPos@1020001 : Integer;Length@1020000 : Integer);
    BEGIN
      AddToPrnString(PrnString,'1' + DELCHR(FedID,'=',' .,-'),StartPos,Length,Justification::Left,' ');
    END;

    LOCAL PROCEDURE AddToPrnString@4(VAR PrnString@1020005 : Text[250];SubString@1020004 : Text[80];StartPos@1020003 : Integer;Length@1020002 : Integer;Justification@1020001 : 'Left,Right';Filler@1020000 : Text[1]);
    VAR
      I@1020007 : Integer;
      SubStrLen@1020006 : Integer;
    BEGIN
      SubString := UPPERCASE(DELCHR(SubString,'<>',' '));
      SubStrLen := STRLEN(SubString);

      if SubStrLen > Length then begin
        SubString := COPYSTR(SubString,1,Length);
        SubStrLen := Length;
      end;

      if Justification = Justification::Right then
        for I := 1 to (Length - SubStrLen) do
          SubString := Filler + SubString
      else
        for I := SubStrLen + 1 to Length do
          SubString := SubString + Filler;

      if STRLEN(PrnString) >= StartPos then
        if StartPos > 1 then
          PrnString := COPYSTR(PrnString,1,StartPos - 1) + SubString + COPYSTR(PrnString,StartPos)
        else
          PrnString := SubString + PrnString
      else begin
        for I := STRLEN(PrnString) + 1 to StartPos - 1 do
          PrnString := PrnString + ' ';
        PrnString := PrnString + SubString;
      end;
    END;

    LOCAL PROCEDURE ExportPrnString@11(VAR PrnString@1020000 : Text[250]);
    BEGIN
      PrnString := PADSTR(PrnString,RecordLength,' ');
      ExportFile.WRITE(PrnString);
      NoOfRec := NoOfRec + 1;
      PrnString := '';
    END;

    LOCAL PROCEDURE GetRecipientData@20(GenJournalLine@1030000 : Record "Gen. Journal Line");
    VAR
      AcctType@1030001 : Text[1];
    BEGIN
      with GenJournalLine do begin
        if "Account Type" = "Account Type"::Vendor then begin
          AcctType := 'V';
          AcctNo := "Account No.";
        end else
          if "Account Type" = "Account Type"::Customer then begin
            AcctType := 'C';
            AcctNo := "Account No.";
          end else
            if "Bal. Account Type" = "Bal. Account Type"::Vendor then begin
              AcctType := 'V';
              AcctNo := "Bal. Account No.";
            end else
              if "Bal. Account Type" = "Bal. Account Type"::Customer then begin
                AcctType := 'C';
                AcctNo := "Bal. Account No.";
              end else
                ERROR(Text015,
                  FIELDCAPTION("Account Type"),FIELDCAPTION("Bal. Account Type"),Vendor.TABLECAPTION,Customer.TABLECAPTION);
        if AcctType = 'V' then
          GetRecipientDataFromVendor
        else
          if AcctType = 'C' then
            GetRecipientDataFromCustomer(GenJournalLine);
      end;
    END;

    LOCAL PROCEDURE GetRecipientDataFromVendor@21();
    VAR
      VendorBankAccount@1030002 : Record "Vendor Bank Account";
    BEGIN
      if AcctNo <> Vendor."No." then begin
        Vendor.GET(AcctNo);
        Vendor.TESTFIELD(Blocked,Vendor.Blocked::" ");
        Vendor.TESTFIELD("Privacy Blocked",false);
        PaymentsThisAcct := 0;
      end else
        PaymentsThisAcct := PaymentsThisAcct + 1;
      AcctName := COPYSTR(Vendor.Name,1,30);
      AcctLanguage := Vendor."Bank Communication";
      RecipientAddress := COPYSTR(Vendor.Address,1,35) + ' ' + COPYSTR(Vendor."Address 2",1,35);
      RecipientCity := Vendor.City;
      RecipientCountryCode := Vendor."Country/Region Code";
      RecipientCounty := Vendor.County;
      RecipientPostCode := Vendor."Post Code";
      VendorBankAccount.SETRANGE("Vendor No.",AcctNo);
      VendorBankAccount.SETRANGE("Use for Electronic Payments",true);
      if VendorBankAccount.COUNT < 1 then
        ERROR(Text016,AcctNo);
      if VendorBankAccount.COUNT > 1 then
        ERROR(Text017,AcctNo);
      VendorBankAccount.FINDFIRST;
      if not CheckDigit(VendorBankAccount."Transit No.") then
        VendorBankAccount.FIELDERROR("Transit No.",Text001);
      VendorBankAccount.TESTFIELD("Bank Account No.");
      RecipientBankNo := VendorBankAccount."Bank Branch No.";
      RecipientTransitNo := VendorBankAccount."Transit No.";
      RecipientBankAcctNo := VendorBankAccount."Bank Account No.";
      RecipientBankAcctCurrencyCode := VendorBankAccount."Currency Code";
      RecipientBankAcctCountryCode := VendorBankAccount."Country/Region Code";
    END;

    LOCAL PROCEDURE GetRecipientDataFromCustomer@22(GenJournalLine@1030000 : Record "Gen. Journal Line");
    VAR
      CustomerBankAccount@1030001 : Record "Customer Bank Account";
    BEGIN
      if AcctNo <> Customer."No." then begin
        Customer.GET(AcctNo);
        if Customer."Privacy Blocked" then
          ERROR(
            STRSUBSTNO(
              PrivacyBlockedErr,
              GenJournalLine."Account Type"));

        if Customer.Blocked = Customer.Blocked::All then
          ERROR(
            STRSUBSTNO(
              Text1020100,
              GenJournalLine."Account Type",Customer.Blocked));
        PaymentsThisAcct := 0;
      end else
        PaymentsThisAcct := PaymentsThisAcct + 1;
      AcctName := COPYSTR(Customer.Name,1,30);
      AcctLanguage := Customer."Bank Communication";
      RecipientAddress := COPYSTR(Customer.Address,1,35) + ' ' + COPYSTR(Customer."Address 2",1,35);
      RecipientCity := Customer.City;
      RecipientCountryCode := Customer."Country/Region Code";
      RecipientCounty := Customer.County;
      RecipientPostCode := Customer."Post Code";
      CustomerBankAccount.SETRANGE("Customer No.",AcctNo);
      CustomerBankAccount.SETRANGE("Use for Electronic Payments",true);
      if CustomerBankAccount.COUNT < 1 then
        ERROR(Text018,AcctNo);
      if CustomerBankAccount.COUNT > 1 then
        ERROR(Text019,AcctNo);
      CustomerBankAccount.FINDFIRST;
      if not CheckDigit(CustomerBankAccount."Transit No.") then
        CustomerBankAccount.FIELDERROR("Transit No.",Text001);
      CustomerBankAccount.TESTFIELD("Bank Account No.");
      RecipientBankNo := CustomerBankAccount."Bank Branch No.";
      RecipientTransitNo := CustomerBankAccount."Transit No.";
      RecipientBankAcctNo := CustomerBankAccount."Bank Account No.";
      RecipientBankAcctCurrencyCode := CustomerBankAccount."Currency Code";
      RecipientBankAcctCountryCode := CustomerBankAccount."Country/Region Code"
    END;

    LOCAL PROCEDURE JulianDate@23(NormalDate@1020000 : Date) : Integer;
    VAR
      Year@1020002 : Integer;
      Days@1020001 : Integer;
    BEGIN
      Year := DATE2DMY(NormalDate,3);
      Days := (NormalDate - DMY2DATE(1,1,Year)) + 1;
      exit(Year * 1000 + Days);
    END;

    [External]
    PROCEDURE CheckDigit@2(DigitString@1020000 : Code[20]) : Boolean;
    VAR
      Weight@1020006 : Code[8];
      Digit@1020005 : Integer;
      I@1020004 : Integer;
      Digit1@1020003 : Integer;
      Digit2@1020002 : Integer;
      CheckChar@1020001 : Code[1];
    BEGIN
      exit(true);  // For Canada

      Weight := '37137137';
      Digit := 0;

      if STRLEN(DigitString) <= STRLEN(Weight) then
        exit(false);

      for I := 1 to STRLEN(Weight) do begin
        EVALUATE(Digit1,COPYSTR(DigitString,I,1));
        EVALUATE(Digit2,COPYSTR(Weight,I,1));
        Digit := Digit + Digit1 * Digit2;
      end;

      Digit := 10 - Digit mod 10;
      if Digit = 10 then
        CheckChar := '0'
      else
        CheckChar := FORMAT(Digit);
      exit(DigitString[STRLEN(Weight) + 1] = CheckChar[1]);
    END;

    LOCAL PROCEDURE BuildIDModifier@5();
    BEGIN
      ModifierValues[1] := 'A';
      ModifierValues[2] := 'B';
      ModifierValues[3] := 'C';
      ModifierValues[4] := 'D';
      ModifierValues[5] := 'E';
      ModifierValues[6] := 'F';
      ModifierValues[7] := 'G';
      ModifierValues[8] := 'H';
      ModifierValues[9] := 'I';
      ModifierValues[10] := 'J';
      ModifierValues[11] := 'K';
      ModifierValues[12] := 'L';
      ModifierValues[13] := 'M';
      ModifierValues[14] := 'N';
      ModifierValues[15] := 'O';
      ModifierValues[16] := 'P';
      ModifierValues[17] := 'Q';
      ModifierValues[18] := 'R';
      ModifierValues[19] := 'S';
      ModifierValues[20] := 'T';
      ModifierValues[21] := 'U';
      ModifierValues[22] := 'V';
      ModifierValues[23] := 'W';
      ModifierValues[24] := 'X';
      ModifierValues[25] := 'Y';
      ModifierValues[26] := 'Z';
    END;

    [External]
    PROCEDURE TransmitExportedFile@16(BankAccountNo@1020001 : Code[20];FName@1020000 : Text);
    VAR
      ExportFullPathName@1020007 : Text;
      TransmitFullPathName@1020006 : Text;
    BEGIN
      with BankAccount do begin
        GET(BankAccountNo);
        TESTFIELD("E-Pay Export File Path");
        if "E-Pay Export File Path"[STRLEN("E-Pay Export File Path")] <> '\' then
          ERROR(Text002,
            FIELDCAPTION("E-Pay Export File Path"),
            TABLECAPTION,
            "No.");
        TESTFIELD("E-Pay Trans. Program Path");
        if "E-Pay Trans. Program Path"[STRLEN("E-Pay Trans. Program Path")] <> '\' then
          ERROR(Text002,
            FIELDCAPTION("E-Pay Trans. Program Path"),
            TABLECAPTION,
            "No.");
        ExportFullPathName := "E-Pay Export File Path" + FName;
        TransmitFullPathName := "E-Pay Trans. Program Path" + FName;

        if not RBMgt.ClientFileExists(ExportFullPathName) then
          ERROR(Text006,FName);
        RBMgt.CopyClientFile(ExportFullPathName,TransmitFullPathName,true);

        if CONFIRM(Text021,true,FName,"E-Pay Trans. Program Path",Name,TABLECAPTION,FIELDCAPTION("No."),"No.") then
          if CONFIRM(Text047) then
            RBMgt.DeleteClientFile(ExportFullPathName);
      end;
    END;

    LOCAL PROCEDURE WriteRecord@30(GenJournalLine@1030004 : Record "Gen. Journal Line";PaymentAmount@1030005 : Decimal;SettleDate@1030006 : Date);
    VAR
      DetailRec@1030003 : Text[250];
      IATAddressInfo1Rec@1030002 : Text[250];
      IATAddressInfo2Rec@1030001 : Text[250];
      IATRemittanceRec@1030000 : Text[250];
    BEGIN
      with GenJournalLine do begin
        Transactions := Transactions + 1;
        DetailRec := '';
        TraceNo := NoOfRec;
        AddNumToPrnString(DetailRec,NoOfRec,1,6);                                          // Record Count
        AddToPrnString(DetailRec,'C',7,1,Justification::Left,' ');
        AddToPrnString(DetailRec,"Transaction Code",8,3,Justification::Left,' ');          // Transaction Code
        AddToPrnString(DetailRec,BankAccount."Client No.",11,10,Justification::Left,'0');  // Client Number
        AddToPrnString(DetailRec,' ',21,1,Justification::Left,' ');
        AddToPrnString(DetailRec,AcctNo,22,19,Justification::Left,' ');                    // Customer Number
        AddNumToPrnString(DetailRec,PaymentsThisAcct,41,2);                                // Payment Number
        if RecipientBankAcctCountryCode = 'CA' then begin
          AddToPrnString(DetailRec,RecipientBankNo,43,4,Justification::Right,'0');        // Bank No.
          AddToPrnString(DetailRec,RecipientTransitNo,47,5,Justification::Right,'0');     // Transit No.
        end else
          if RecipientBankAcctCountryCode = 'US' then
            AddToPrnString(DetailRec,RecipientTransitNo,43,9,Justification::Right,'0');

        AddToPrnString(DetailRec,RecipientBankAcctNo,52,18,Justification::Left,' ');
        AddToPrnString(DetailRec,' ',70,1,Justification::Left,' ');
        AddAmtToPrnString(DetailRec,PaymentAmount,71,10);                                  // Payment Amount
        AddToPrnString(DetailRec,' ',81,6,Justification::Left,' ');
        AddNumToPrnString(DetailRec,JulianDate(SettleDate),87,7);                          // Payment Date
        AddToPrnString(DetailRec,AcctName,94,30,Justification::Left,' ');                  // Customer Name
        AddToPrnString(DetailRec,FORMAT(AcctLanguage),124,1,Justification::Left,' ');      // Language Code
        AddToPrnString(DetailRec,' ',125,1,Justification::Left,' ');
        AddToPrnString(DetailRec,BankAccount."Client Name",126,15,Justification::Left,' ');// Client Name
        if RecipientBankAcctCurrencyCode = '' then
          AddToPrnString(DetailRec,CurrencyType,141,3,Justification::Left,' ')
        else
          AddToPrnString(DetailRec,RecipientBankAcctCurrencyCode,141,3,Justification::Left,' ');
        AddToPrnString(DetailRec,' ',144,1,Justification::Left,' ');
        if RecipientCountryCode = 'CA' then
          AddToPrnString(DetailRec,'CAN',145,3,Justification::Left,' ')
        else
          if RecipientCountryCode = 'US' then
            AddToPrnString(DetailRec,'USA',145,3,Justification::Left,' ');
        AddToPrnString(DetailRec,' ',148,2,Justification::Left,' ');
        AddToPrnString(DetailRec,' ',150,2,Justification::Left,' ');
        AddToPrnString(DetailRec,'N',152,1,Justification::Left,' ');                       // No Optional Records Follow

        ExportPrnString(DetailRec);
        IATAddressInfo1Rec := '';
        AddNumToPrnString(IATAddressInfo1Rec,NoOfRec,1,6);
        AddToPrnString(IATAddressInfo1Rec,'C',7,1,Justification::Left,' ');
        AddToPrnString(IATAddressInfo1Rec,'AD1',8,3,Justification::Left,' ');
        AddToPrnString(IATAddressInfo1Rec,BankAccount."Client No.",11,10,Justification::Left,'0');
        AddToPrnString(IATAddressInfo1Rec,CompanyInformation.Name,21,30,Justification::Left,' ');
        AddToPrnString(IATAddressInfo1Rec,COPYSTR(CompanyInformation.Address,1,35) + ' ' +
          COPYSTR(CompanyInformation."Address 2",1,35),51,35,Justification::Left,' ');
        AddToPrnString(IATAddressInfo1Rec,CompanyInformation.City + '*' + CompanyInformation.County +
          '\',86,35,Justification::Left,' ');
        AddToPrnString(IATAddressInfo1Rec,CompanyInformation."Country/Region Code" + '*' + CompanyInformation."Post Code" +
          '\',121,32,Justification::Left,' ');

        ExportPrnString(IATAddressInfo1Rec);
        NoOfCustInfoRec += 1;

        IATAddressInfo2Rec := '';
        AddNumToPrnString(IATAddressInfo2Rec,NoOfRec,1,6);
        AddToPrnString(IATAddressInfo2Rec,'C',7,1,Justification::Left,' ');
        AddToPrnString(IATAddressInfo2Rec,'AD2',8,3,Justification::Left,' ');
        AddToPrnString(IATAddressInfo2Rec,BankAccount."Client No.",11,10,Justification::Left,'0');
        AddToPrnString(IATAddressInfo2Rec,RecipientAddress,21,35,Justification::Left,' ');
        AddToPrnString(IATAddressInfo2Rec,RecipientCity + '*' + RecipientCounty + '\',56,35,Justification::Left,' ');
        AddToPrnString(IATAddressInfo2Rec,RecipientCountryCode + '*' + RecipientPostCode + '\',91,35,Justification::Left,' ');
        AddToPrnString(IATAddressInfo2Rec,FORMAT("Transaction Type Code"),126,3,Justification::Left,' ');
        AddToPrnString(IATAddressInfo2Rec,"Company Entry Description",129,10,Justification::Left,' ');
        AddToPrnString(IATAddressInfo2Rec,' ',139,14,Justification::Left,' ');

        ExportPrnString(IATAddressInfo2Rec);
        NoOfCustInfoRec += +1;

        if ("Payment Related Information 1" <> '') or ("Payment Related Information 2" <> '') then begin
          IATRemittanceRec := '';
          AddNumToPrnString(IATRemittanceRec,NoOfRec,1,6);
          AddToPrnString(IATRemittanceRec,'C',7,1,Justification::Left,' ');
          AddToPrnString(IATRemittanceRec,'REM',8,3,Justification::Left,' ');
          AddToPrnString(IATRemittanceRec,BankAccount."Client No.",11,10,Justification::Left,'0');
          AddToPrnString(IATRemittanceRec,"Payment Related Information 1",21,80,Justification::Left,' ');
          AddToPrnString(IATRemittanceRec,"Payment Related Information 2",101,52,Justification::Left,' ');

          ExportPrnString(IATRemittanceRec);
          NoOfCustInfoRec += +1;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

