OBJECT Codeunit 50 SaaS Log In Management
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160530.373T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NoPermissionToEnterTrialErr@1003 : TextConst '@@@="%1 = Company Name";ENU=In order to open %1, your company must start a trial. You must be an administrator to enter the trial period.';
      RequireUserApprovalForTrialErr@1000 : TextConst '@@@="%1 = Company Name";ENU=Preview versions are no longer available. To continue using %1, use a web client to open the company and accept the terms and conditions.';

    LOCAL PROCEDURE ShowTermsAndConditions@1(Company@1000 : Record Company);
    VAR
      TenantLicenseState@1002 : Record "Tenant License State";
      PermissionManager@1003 : Codeunit "Permission Manager";
      RoleCenterNotificationMgt@1004 : Codeunit "Role Center Notification Mgt.";
      ThirtyDayTrialDialog@1001 : Page "Thirty Day Trial Dialog";
    BEGIN
      if PermissionManager.IsSandboxConfiguration then
        exit;

      if PermissionManager.IsPreview then
        exit;

      if not PermissionManager.SoftwareAsAService then
        exit;

      if Company."Evaluation Company" then
        exit;

      if TenantLicenseState.FINDLAST then begin
        if TenantLicenseState.State <> TenantLicenseState.State::Evaluation then
          exit;
      end;

      // Verify, that the user has company setup rights
      if not Company.WRITEPERMISSION then
        ERROR(NoPermissionToEnterTrialErr,Company.Name);

      if not GUIALLOWED then begin
        if CURRENTCLIENTTYPE in [CLIENTTYPE::OData,CLIENTTYPE::ODataV4,CLIENTTYPE::Api] then
          ERROR(RequireUserApprovalForTrialErr,Company.Name);
        exit;
      end;

      COMMIT;
      ThirtyDayTrialDialog.RUNMODAL;

      if not ThirtyDayTrialDialog.Confirmed then begin
        if RoleCenterNotificationMgt.IsEvaluationNotificationClicked then
          RoleCenterNotificationMgt.ShowEvaluationNotification;
        ERROR('');
      end;
    END;

    [EventSubscriber(Table,2000000073,OnBeforeValidateEvent,Company)]
    LOCAL PROCEDURE OnBeforeUserPersonalizationCompanyChange@2(VAR Rec@1000 : Record "User Personalization";VAR xRec@1001 : Record "User Personalization";CurrFieldNo@1003 : Integer);
    VAR
      Company@1002 : Record Company;
    BEGIN
      if Company.GET(Rec.Company) then
        ShowTermsAndConditions(Company);
    END;

    [EventSubscriber(Codeunit,1,OnBeforeCompanyOpen)]
    LOCAL PROCEDURE OnBeforeOpenCompany@12();
    VAR
      Company@1002 : Record Company;
    BEGIN
      if GUIALLOWED and (APPLICATIONIDENTIFIER <> 'INV') and Company.GET(COMPANYNAME) then
        ShowTermsAndConditions(Company);
    END;

    BEGIN
    END.
  }
}

