OBJECT Codeunit 2135 O365 Setup Email
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160716.37T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            SetupEmail(false);
          END;

  }
  CODE
  {
    VAR
      MailNotConfiguredErr@1000 : TextConst 'ENU=An email account must be configured to send emails.';
      ClientTypeManagement@1001 : Codeunit ClientTypeManagement;

    [External]
    PROCEDURE SilentSetup@5();
    VAR
      GraphMail@1001 : Codeunit "Graph Mail";
    BEGIN
      if GraphMail.IsEnabled or SMTPEmailIsSetUp then
        exit;

      if not GraphMail.HasConfiguration then
        ERROR(MailNotConfiguredErr);

      GraphMail.SetupGraph(false);

      if not GraphMail.IsEnabled then
        ERROR(MailNotConfiguredErr);
    END;

    [External]
    PROCEDURE SetupEmail@15(ForceSetup@1000 : Boolean);
    VAR
      GraphMail@1001 : Codeunit "Graph Mail";
    BEGIN
      if (not GraphMail.IsEnabled) and SMTPEmailIsSetUp then begin
        SetupSmtpEmail(ForceSetup);
        exit;
      end;

      if GraphMail.HasConfiguration then begin
        SetupGraphEmail(ForceSetup);
        exit;
      end;

      SetupSmtpEmail(ForceSetup);
    END;

    LOCAL PROCEDURE SetupGraphEmail@2(ForceSetup@1000 : Boolean);
    VAR
      GraphMail@1001 : Codeunit "Graph Mail";
      RunSetup@1002 : Boolean;
    BEGIN
      RunSetup := ForceSetup;
      if not RunSetup then begin
        if not GraphMail.IsEnabled then
          if not SMTPEmailIsSetUp then
            RunSetup := true;
      end;

      if RunSetup then
        if not GraphMail.SetupGraph(ForceSetup) then // Returns if graph mail was setup
          if not SMTPEmailIsSetUp then begin // If it wasn't maybe smtp was configured instead
            if ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Tablet then
              ERROR('');
            ERROR(MailNotConfiguredErr);
          end;
    END;

    LOCAL PROCEDURE SetupSmtpEmail@3(ForceSetup@1000 : Boolean);
    BEGIN
      if ForceSetup or (not SMTPEmailIsSetUp) then begin
        if not GUIALLOWED then
          ERROR(MailNotConfiguredErr);

        if ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Tablet then begin
          if PAGE.RUNMODAL(PAGE::"BC O365 Email Setup Wizard") <> ACTION::LookupOK then
            ERROR('');
        end else
          PAGE.RUNMODAL(PAGE::"O365 Email Setup Wizard");
        if not SMTPEmailIsSetUp then
          ERROR(MailNotConfiguredErr);
      end;
    END;

    PROCEDURE SMTPEmailIsSetUp@1() : Boolean;
    VAR
      SMTPMailSetup@1001 : Record "SMTP Mail Setup";
      MailManagement@1002 : Codeunit "Mail Management";
    BEGIN
      if not MailManagement.IsSMTPEnabled then
        exit(false);

      if SMTPMailSetup.FIND then
        exit((SMTPMailSetup."User ID" <> '') and (SMTPMailSetup.GetPassword <> ''));
    END;

    BEGIN
    END.
  }
}

