OBJECT Page 2140 O365 Sync with Microsoft Apps
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=161402.930T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sync with Microsoft Apps;
    OnInit=BEGIN
             UsernameVisible := ClientTypeManagement.GetCurrentClientType <> CLIENTTYPE::Phone;
           END;

    OnOpenPage=VAR
                 MarketingSetup@1000 : Record "Marketing Setup";
                 O365SalesInitialSetup@1001 : Record "O365 Sales Initial Setup";
               BEGIN
                 if MarketingSetup.GET then
                   EnableSync := MarketingSetup."Sync with Microsoft Graph";
                 SetUserName;
                 if O365SalesInitialSetup.GET then
                   SyncCoupons := O365SalesInitialSetup."Coupons Integration Enabled";
               END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name="Microsoft Sync";
                CaptionML=ENU=Microsoft Sync;
                ContainerType=ContentArea }

    { 7   ;1   ;Group     ;
                Name=Control7;
                GroupType=Group }

    { 2   ;2   ;Field     ;
                Name=EnableSync;
                CaptionML=ENU=Enable Synchronization;
                ToolTipML=ENU=Specifies whether Microsoft synchronization is enabled.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=EnableSync;
                OnValidate=VAR
                             O365SalesBackgroundSetup@1000 : Codeunit "O365 Sales Background Setup";
                           BEGIN
                             if not EnableSync then
                               if not CONFIRM(ConfirmDisablingSyncQst,false) then
                                 ERROR('');

                             O365SalesBackgroundSetup.InitializeGraphSync(EnableSync,false);

                             SetUserName;

                             // Coupons sync changes state along with graph sync
                             SyncCoupons := EnableSync;
                             StoreCouponsSync;
                           END;
                            }

    { 3   ;2   ;Field     ;
                Name=SyncCoupons;
                CaptionML=ENU=Show coupons;
                ToolTipML=ENU=Specifies if you want to see coupons when you create a new invoice or estimate.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=SyncCoupons;
                OnValidate=BEGIN
                             StoreCouponsSync;
                           END;
                            }

    { 6   ;2   ;Field     ;
                Name=SyncUser;
                CaptionML=ENU=User;
                ToolTipML=ENU=Specifies the user who owns the synchronization jobs.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=UserName;
                Visible=UsernameVisible;
                Editable=FALSE;
                OnAssistEdit=VAR
                               SelectedUser@1001 : Record User;
                               MarketingSetup@1000 : Record "Marketing Setup";
                             BEGIN
                               if not EnableSync then
                                 exit;

                               SelectedUser.SETFILTER("License Type",'<> %1',SelectedUser."License Type"::"External User");
                               if not (PAGE.RUNMODAL(PAGE::Users,SelectedUser) = ACTION::LookupOK) then
                                 exit;

                               MarketingSetup.GET;
                               if MarketingSetup.TrySetWebhookSubscriptionUser(SelectedUser."User Security ID") then begin
                                 MarketingSetup.MODIFY(true);
                                 UserName := SelectedUser."User Name";
                               end
                             END;
                              }

  }
  CODE
  {
    VAR
      ClientTypeManagement@1005 : Codeunit ClientTypeManagement;
      EnableSync@1000 : Boolean;
      UserName@1003 : Text;
      UsernameVisible@1001 : Boolean;
      SyncCoupons@1002 : Boolean;
      ConfirmDisablingSyncQst@1004 : TextConst 'ENU=If you disable synchronization with other Microsoft apps, some of the functionality in the Invoicing app will not be available. Do you want to disable sync with Microsoft apps?';

    LOCAL PROCEDURE SetUserName@15();
    VAR
      MarketingSetup@1002 : Record "Marketing Setup";
      User@1001 : Record User;
    BEGIN
      if EnableSync then begin
        if User.GET(MarketingSetup.GetWebhookSubscriptionUser) then
          UserName := User."User Name";
      end else
        UserName := '';
    END;

    LOCAL PROCEDURE StoreCouponsSync@4();
    VAR
      O365SalesInitialSetup@1000 : Record "O365 Sales Initial Setup";
    BEGIN
      O365SalesInitialSetup.GET;
      O365SalesInitialSetup."Coupons Integration Enabled" := SyncCoupons;
      O365SalesInitialSetup.MODIFY(true);
    END;

    BEGIN
    END.
  }
}

