OBJECT Codeunit 1317 Aged Inventory Chart Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160654.977T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      YCaptionTxt@4048 : TextConst 'ENU=Inventory Value';
      XCaptionTxt@4047 : TextConst 'ENU=Time on Inventory';
      PeriodStartDate@4049 : ARRAY [6] OF Date;
      XFromToYearsTxt@1001 : TextConst '@@@="%1=number of years,%2=number of years";ENU=%1..%2 years';
      XFromToDaysTxt@1000 : TextConst '@@@="%1=number of days,%2=number of days";ENU=%1..%2 days';
      XOverYearsTxt@1002 : TextConst 'ENU=Over %1 years';
      XOverDaysTxt@1003 : TextConst 'ENU=Over %1 days';

    [Internal]
    PROCEDURE UpdateChart@3(VAR BusChartBuf@1000 : Record "Business Chart Buffer");
    VAR
      ColumnIndex@1007 : Integer;
      PeriodStartDate@4049 : ARRAY [6] OF Date;
      InvtValue@4047 : ARRAY [5] OF Decimal;
    BEGIN
      with BusChartBuf do begin
        Initialize;
        AddMeasure(YCaptionTxt,1,"Data Type"::Decimal,"Chart Type"::StackedColumn);
        SetXAxis(XCaptionTxt,"Data Type"::String);
        CalcPeriodStartDates(PeriodStartDate,GetPeriodLengthInDays(BusChartBuf));
        AddChartColumns(BusChartBuf);
        CalcInventoryValuePerAge(InvtValue,PeriodStartDate);
        for ColumnIndex := 1 to 5 do
          SetValueByIndex(0,ColumnIndex - 1,InvtValue[6 - ColumnIndex]);
      end;
    END;

    [External]
    PROCEDURE DrillDown@5(VAR BusChartBuf@1000 : Record "Business Chart Buffer");
    VAR
      DrillDownXIndex@1001 : Integer;
    BEGIN
      CalcPeriodStartDates(PeriodStartDate,GetPeriodLengthInDays(BusChartBuf));
      DrillDownXIndex := BusChartBuf."Drill-Down X Index";
      case BusChartBuf."Drill-Down Measure Index" + 1 of
        1: // Item Ledger Entries
          DrillDownItemLedgerEntries(PeriodStartDate[5 - DrillDownXIndex],PeriodStartDate[6 - DrillDownXIndex]);
      end;
    END;

    PROCEDURE CalcInventoryValuePerAge@4050(VAR InvtValue@4048 : ARRAY [5] OF Decimal;PeriodStartDate@4047 : ARRAY [6] OF Date);
    VAR
      Item@4049 : Record Item;
      ItemLedgerEntry@4050 : Record "Item Ledger Entry";
      InvtQty@4051 : ARRAY [5] OF Decimal;
      UnitCost@4052 : Decimal;
      PeriodNo@4053 : Integer;
    BEGIN
      Item.SETRANGE(Type,Item.Type::Inventory,Item.Type::Inventory);
      if Item.FIND('-') then
        repeat
          with ItemLedgerEntry do begin
            SETRANGE(Open,true,true);
            SETRANGE("Item No.",Item."No.",Item."No.");
            if FIND('-') then
              repeat
                CalcRemainingQty(ItemLedgerEntry,PeriodStartDate,InvtQty,PeriodNo);
                UnitCost := CalcUnitCost(ItemLedgerEntry);
                InvtValue[PeriodNo] += UnitCost * ABS(InvtQty[PeriodNo]);
              until NEXT = 0;
          end;
        until Item.NEXT = 0;
    END;

    LOCAL PROCEDURE AddChartColumns@4051(VAR BusChartBuf@4047 : Record "Business Chart Buffer");
    VAR
      I@4048 : Integer;
      PeriodLengthOnXAxis@4049 : Integer;
      XAxisValueTxt@1000 : Text[30];
      LastXAxisValueTxt@1001 : Text[30];
    BEGIN
      with BusChartBuf do begin
        PeriodLengthOnXAxis := GetPeriodLengthInDays(BusChartBuf);
        if PeriodLengthOnXAxis = 365 then begin
          PeriodLengthOnXAxis := 1;
          XAxisValueTxt := XFromToYearsTxt;
          LastXAxisValueTxt := XOverYearsTxt;
        end else begin
          XAxisValueTxt := XFromToDaysTxt;
          LastXAxisValueTxt := XOverDaysTxt;
        end;
        for I := 0 to 3 do
          AddColumn(STRSUBSTNO(XAxisValueTxt,I * PeriodLengthOnXAxis,(I + 1) * PeriodLengthOnXAxis));  // X-Axis value
        AddColumn(STRSUBSTNO(LastXAxisValueTxt,FORMAT(4 * PeriodLengthOnXAxis)));  // X-Axis value
      end;
    END;

    LOCAL PROCEDURE CalcPeriodStartDates@4047(VAR PeriodStartDate@4047 : ARRAY [6] OF Date;PeriodLength@4049 : Integer);
    VAR
      I@4048 : Integer;
    BEGIN
      PeriodStartDate[6] := WORKDATE;
      PeriodStartDate[1] := 0D;
      for I := 2 to 5 do
        PeriodStartDate[I] := CALCDATE('<-' + FORMAT((6 - I) * PeriodLength) + 'D>',PeriodStartDate[6]);
    END;

    PROCEDURE CalcRemainingQty@4052(ItemLedgerEntry@4047 : Record "Item Ledger Entry";PeriodStartDate@4049 : ARRAY [6] OF Date;VAR InvtQty@4048 : ARRAY [5] OF Decimal;VAR PeriodNo@4051 : Integer);
    BEGIN
      with ItemLedgerEntry do begin
        for PeriodNo := 1 to 5 do
          InvtQty[PeriodNo] := 0;

        for PeriodNo := 1 to 5 do
          if ("Posting Date" > PeriodStartDate[PeriodNo]) and
             ("Posting Date" <= PeriodStartDate[PeriodNo + 1])
          then
            if "Remaining Quantity" <> 0 then begin
              InvtQty[PeriodNo] := "Remaining Quantity";
              exit;
            end;
      end;
    END;

    PROCEDURE CalcUnitCost@2(ItemLedgerEntry@4047 : Record "Item Ledger Entry") : Decimal;
    VAR
      ValueEntry@1055 : Record "Value Entry";
      UnitCost@4048 : Decimal;
    BEGIN
      with ValueEntry do begin
        SETRANGE("Item Ledger Entry No.",ItemLedgerEntry."Entry No.");
        UnitCost := 0;

        if FIND('-') then
          repeat
            if "Partial Revaluation" then
              SumUnitCost(UnitCost,"Cost Amount (Actual)" + "Cost Amount (Expected)","Valued Quantity")
            else
              SumUnitCost(UnitCost,"Cost Amount (Actual)" + "Cost Amount (Expected)",ItemLedgerEntry.Quantity);
          until NEXT = 0;
      end;
      exit(UnitCost);
    END;

    PROCEDURE GetPeriodLengthInDays@4048(BusChartBuf@4047 : Record "Business Chart Buffer") : Integer;
    BEGIN
      with BusChartBuf do
        case "Period Length" of
          "Period Length"::Day:
            exit(1);
          "Period Length"::Week:
            exit(7);
          "Period Length"::Month:
            exit(30);
          "Period Length"::Quarter:
            exit(90);
          "Period Length"::Year:
            exit(365);
        end;
    END;

    LOCAL PROCEDURE SumUnitCost@4053(VAR UnitCost@1002 : Decimal;CostAmount@1000 : Decimal;Quantity@1001 : Decimal);
    BEGIN
      UnitCost := UnitCost + CostAmount / ABS(Quantity);
    END;

    LOCAL PROCEDURE DrillDownItemLedgerEntries@5050(StartDate@5048 : Date;EndDate@4047 : Date);
    VAR
      ItemLedgerEntry@5049 : Record "Item Ledger Entry";
    BEGIN
      ItemLedgerEntry.SETRANGE(Open,true);
      // we don't want to include start date in the filter, unless it is 0D
      if StartDate = 0D then
        ItemLedgerEntry.SETRANGE("Posting Date",StartDate,EndDate)
      else
        ItemLedgerEntry.SETRANGE("Posting Date",CALCDATE('<1D>',StartDate),EndDate);
      ItemLedgerEntry.SETFILTER("Remaining Quantity",'<>0');
      PAGE.RUN(PAGE::"Item Ledger Entries",ItemLedgerEntry);
    END;

    PROCEDURE FromToYearsTxt@1() : Text[30];
    BEGIN
      exit(XFromToYearsTxt);
    END;

    PROCEDURE FromToDaysTxt@4() : Text[30];
    BEGIN
      exit(XFromToDaysTxt);
    END;

    PROCEDURE OverYearsTxt@6() : Text[30];
    BEGIN
      exit(XOverYearsTxt);
    END;

    PROCEDURE OverDaysTxt@7() : Text[30];
    BEGIN
      exit(XOverDaysTxt);
    END;

    BEGIN
    END.
  }
}

