OBJECT Page 10150 O365 Tax Settings Card
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=161723.403T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Tax Rate;
    DeleteAllowed=false;
    SourceTable="Tax Area";
    DelayedInsert=true;
    RefreshOnActivate=true;
    OnInit=BEGIN
             InitializeDefaultCountryCode;
           END;

    OnNewRecord=BEGIN
                  if IsCanada then begin
                    MESSAGE(AllTaxAreasAlreadyExistsMsg);
                    ERROR('');
                  end;
                END;

    OnQueryClosePage=BEGIN
                       exit(TryStoringTaxSettings);
                     END;

    OnAfterGetCurrRecord=BEGIN
                           SetDefaults;
                           if not IsCanada then begin
                             StateNameEditable := (TempSalesTaxSetupWizard.State = '');
                             CityNameEditable := (TempSalesTaxSetupWizard.City = '');
                           end;
                         END;

    ActionList=ACTIONS
    {
      { 1020003 ;    ;ActionContainer;
                      Name=ActionContainer1020003;
                      ActionContainerType=ActionItems }
    }
  }
  CONTROLS
  {
    { 1020000; ;Container ;
                Name=Control1020000;
                ContainerType=ContentArea }

    { 1020019;1;Group     ;
                Name=Control1020019;
                Visible=NOT IsCanada;
                GroupType=Group }

    { 1020001;2;Group     ;
                Name=Control1020001;
                GroupType=Group;
                InstructionalTextML=ENU=Enter your city tax information }

    { 1020011;3;Field     ;
                Name=City;
                CaptionML=ENU=City;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=TempSalesTaxSetupWizard.City;
                Enabled=CityNameEditable;
                OnValidate=BEGIN
                             UpdateDescriptionUS;
                           END;

                ShowCaption=false }

    { 1020010;3;Field     ;
                Name=CityRate;
                CaptionML=ENU=City Rate;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                BlankZero=true;
                SourceExpr=TempSalesTaxSetupWizard."City Rate";
                MinValue=0;
                OnValidate=BEGIN
                             if TempSalesTaxSetupWizard.City <> '' then begin
                               UpdateTotalTaxRate;
                               UpdateDescriptionUS;
                             end;
                           END;

                ShowCaption=false }

    { 1020006;2;Group     ;
                Name=Control1020006;
                GroupType=Group;
                InstructionalTextML=ENU=Enter your state tax information }

    { 1020005;3;Field     ;
                Name=State;
                CaptionML=ENU=State;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=TempSalesTaxSetupWizard.State;
                Enabled=StateNameEditable;
                OnValidate=BEGIN
                             UpdateDescriptionUS;
                           END;

                ShowCaption=false }

    { 1020004;3;Field     ;
                Name=StateRate;
                CaptionML=ENU=State Rate;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                BlankZero=true;
                SourceExpr=TempSalesTaxSetupWizard."State Rate";
                MinValue=0;
                OnValidate=BEGIN
                             if TempSalesTaxSetupWizard.State <> '' then begin
                               UpdateTotalTaxRate;
                               UpdateDescriptionUS;
                             end;
                           END;

                ShowCaption=false }

    { 1020017;1;Group     ;
                Name=Control1020017;
                Visible=IsCanada;
                GroupType=Group }

    { 1020012;2;Group     ;
                Name=Control1020012;
                Visible=GSTorHSTVisible;
                GroupType=Group }

    { 1020009;3;Field     ;
                Name=GSTorHST;
                CaptionML=ENU=GST/HST;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=GSTorHST;
                TableRelation="Tax Jurisdiction" WHERE ("Country/Region"=CONST(CA),
                                                        "Report-to Jurisdiction"=CONST('CA'));
                Editable=FALSE;
                OnValidate=BEGIN
                             GSTorHSTCode := COPYSTR(GSTorHST,1,MAXSTRLEN(GSTorHSTCode));
                             GSTorHST := COPYSTR(GetProvince(GSTorHSTCode),1,MAXSTRLEN(GSTorHST));
                             GSTorHSTrate := O365TaxSettingsManagement.GetTaxRate(GSTorHSTCode)
                           END;

                ImplicitType=Text50 }

    { 1020007;3;Field     ;
                Name=GSTorHSTrate;
                CaptionML=ENU=GST/HST Rate;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                DecimalPlaces=1:3;
                SourceExpr=GSTorHSTrate;
                Editable=(GSTorHST <> '');
                ImplicitType=Decimal }

    { 1020018;2;Group     ;
                Name=Control1020018;
                Visible=PSTVisible;
                GroupType=Group }

    { 1020014;3;Field     ;
                Name=PST;
                CaptionML=ENU=PST;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=PST;
                TableRelation="Tax Jurisdiction" WHERE ("Country/Region"=CONST(CA),
                                                        "Report-to Jurisdiction"=FILTER(<>'CA'));
                Editable=FALSE;
                OnValidate=BEGIN
                             PSTCode := COPYSTR(PST,1,MAXSTRLEN(PSTCode));
                             PST := COPYSTR(GetProvince(PSTCode),1,MAXSTRLEN(GSTorHST));
                             PSTrate := O365TaxSettingsManagement.GetTaxRate(PSTCode)
                           END;

                ImplicitType=Text50 }

    { 1020015;3;Field     ;
                Name=PSTrate;
                CaptionML=ENU=PST Rate;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                DecimalPlaces=1:3;
                SourceExpr=PSTrate;
                Editable=(PST <> '');
                ImplicitType=Decimal }

    { 1020021;1;Group     ;
                Name=Control1020021;
                GroupType=Group }

    { 1020020;2;Field     ;
                Name=Total;
                CaptionML=ENU=Total rate;
                ToolTipML=ENU=Specifies the total tax rate.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                DecimalPlaces=1:3;
                SourceExpr=TotalRate;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 1020008;1;Group     ;
                Name=Control1020008;
                GroupType=Group }

    { 1020016;2;Field     ;
                Name=Default;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=DefaultTxt;
                Enabled=NOT IsDefaultArea;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              O365TaxSettingsManagement.UpdateSalesTaxSetupWizard(TempSalesTaxSetupWizard);
                              DefaultTxt := ThisIsDefaultTxt;
                              IsDefaultArea := true;
                            END;

                ImplicitType=Text;
                ShowCaption=false }

  }
  CODE
  {
    VAR
      TempSalesTaxSetupWizard@1020005 : TEMPORARY Record "Sales Tax Setup Wizard";
      O365TaxSettingsManagement@1020001 : Codeunit "O365 Tax Settings Management";
      PSTCode@1020008 : Code[10];
      GSTorHSTCode@1020007 : Code[10];
      GSTorHST@1020002 : Text[50];
      PST@1020004 : Text[50];
      TaxRateDescription@1020017 : Text[50];
      DefaultTxt@1020011 : Text;
      GSTorHSTrate@1020003 : Decimal;
      PSTrate@1020006 : Decimal;
      TotalRate@1020012 : Decimal;
      TaxableCodeTxt@1030000 : TextConst '@@@={Locked};ENU=TAXABLE';
      CATxt@1030001 : TextConst '@@@={Locked};ENU=CA';
      PSTVisible@1020009 : Boolean;
      GSTorHSTVisible@1020010 : Boolean;
      ThisIsDefaultTxt@1020015 : TextConst 'ENU=This is the default tax rate';
      SetAsDefaultTxt@1020014 : TextConst 'ENU=Set as default tax rate';
      IsDefaultArea@1020016 : Boolean;
      IsCanada@1020000 : Boolean;
      CityNameEditable@1020013 : Boolean;
      StateNameEditable@1020019 : Boolean;
      AllTaxAreasAlreadyExistsMsg@1020020 : TextConst 'ENU=All tax rates already exists.';

    LOCAL PROCEDURE SetDefaults@1030001();
    BEGIN
      InitializeDefaultTaxAreaLabel;
      TempSalesTaxSetupWizard.Initialize;
      TempSalesTaxSetupWizard."Tax Area Code" := DELCHR(Code,'<>',' ');
      InitializeTaxAreaLines;
      if not IsCanada then
        UpdateDescriptionUS;
    END;

    LOCAL PROCEDURE InitializeDefaultTaxAreaLabel@1020005();
    BEGIN
      IsDefaultArea := O365TaxSettingsManagement.IsDefaultTaxAreaAPI(Code);
      if IsDefaultArea then
        DefaultTxt := ThisIsDefaultTxt
      else
        DefaultTxt := SetAsDefaultTxt;
    END;

    LOCAL PROCEDURE InitializeDefaultCountryCode@1020004();
    VAR
      CompanyInformation@1020000 : Record "Company Information";
    BEGIN
      if CompanyInformation.IsCanada then begin
        IsCanada := true;
        "Country/Region" := "Country/Region"::CA;
      end else
        "Country/Region" := "Country/Region"::US;
    END;

    LOCAL PROCEDURE InitializeTaxAreaLines@1020011();
    BEGIN
      if TempSalesTaxSetupWizard."Tax Area Code" <> '' then begin
        if IsCanada then
          InitializeTaxSetupFromTaxAreaLinesForCA
        else
          O365TaxSettingsManagement.InitializeTaxSetupFromTaxAreaLinesForUS(TempSalesTaxSetupWizard);
      end;
      UpdateTotalTaxRate;
    END;

    LOCAL PROCEDURE InitializeTaxSetupFromTaxAreaLinesForCA@1020001();
    VAR
      TaxAreaLine@1020000 : Record "Tax Area Line";
      TaxJurisdiction@1020001 : Record "Tax Jurisdiction";
    BEGIN
      ClearFieldsForCA;
      TaxAreaLine.SETRANGE("Tax Area",TempSalesTaxSetupWizard."Tax Area Code");
      if TaxAreaLine.FINDSET then
        repeat
          TaxJurisdiction.SETRANGE(Code,TaxAreaLine."Tax Jurisdiction Code");
          if TaxJurisdiction.FINDFIRST then
            if TaxJurisdiction."Report-to Jurisdiction" = CATxt then begin
              GSTorHSTCode := TaxJurisdiction.Code;
              GSTorHST := GetProvince(GSTorHSTCode);
              GSTorHSTrate := O365TaxSettingsManagement.GetTaxRate(GSTorHSTCode)
            end else begin
              PSTCode := TaxJurisdiction.Code;
              PST := GetProvince(PSTCode);
              PSTrate := O365TaxSettingsManagement.GetTaxRate(PSTCode)
            end;
        until TaxAreaLine.NEXT = 0;
      GSTorHSTVisible := GSTorHST <> '';
      PSTVisible := PST <> '';
    END;

    LOCAL PROCEDURE ClearFieldsForCA@1020014();
    BEGIN
      CLEAR(PSTCode);
      CLEAR(PST);
      CLEAR(PSTrate);
      CLEAR(GSTorHST);
      CLEAR(GSTorHSTCode);
      CLEAR(GSTorHSTrate);
    END;

    LOCAL PROCEDURE GetProvince@1020002(JurisdictionCode@1020001 : Code[10]) : Text[50];
    VAR
      TaxJurisdiction@1020003 : Record "Tax Jurisdiction";
    BEGIN
      if not TaxJurisdiction.GET(JurisdictionCode) then
        exit('');

      exit(TaxJurisdiction.GetDescriptionInCurrentLanguage);
    END;

    LOCAL PROCEDURE UpdateTotalTaxRate@1020049();
    BEGIN
      if IsCanada then
        TotalRate := GSTorHSTrate + PSTrate
      else
        TotalRate := TempSalesTaxSetupWizard."City Rate" + TempSalesTaxSetupWizard."State Rate";
    END;

    LOCAL PROCEDURE TryStoringTaxSettings@1020021() CanClosePage : Boolean;
    BEGIN
      CanClosePage := true;

      if IsCanada then
        StoreTaxSettingsForCA
      else
        CanClosePage := O365TaxSettingsManagement.StoreTaxSettingsForUS(TempSalesTaxSetupWizard,TaxRateDescription);

      if IsDefaultArea then
        O365TaxSettingsManagement.AssignDefaultTaxArea(TempSalesTaxSetupWizard."Tax Area Code");
    END;

    LOCAL PROCEDURE StoreTaxSettingsForCA@1020063();
    VAR
      TaxArea@1020002 : Record "Tax Area";
      TaxAreaLine@1020001 : Record "Tax Area Line";
    BEGIN
      if TempSalesTaxSetupWizard."Tax Area Code" = '' then
        exit;
      TempSalesTaxSetupWizard.SetTaxArea(TaxArea);
      TaxAreaLine.SETRANGE("Tax Area",TempSalesTaxSetupWizard."Tax Area Code");
      if not TaxAreaLine.ISEMPTY then
        TaxAreaLine.DELETEALL;
      if GSTorHSTCode <> '' then begin
        TempSalesTaxSetupWizard.SetTaxJurisdiction(GSTorHSTCode,GSTorHST,CATxt);
        TempSalesTaxSetupWizard.SetTaxAreaLine(TaxArea,GSTorHSTCode);
        TempSalesTaxSetupWizard.SetTaxDetail(GSTorHSTCode,TaxableCodeTxt,GSTorHSTrate);
      end;
      if PSTCode <> '' then begin
        TempSalesTaxSetupWizard.SetTaxJurisdiction(PSTCode,PST,PSTCode);
        TempSalesTaxSetupWizard.SetTaxAreaLine(TaxArea,PSTCode);
        TempSalesTaxSetupWizard.SetTaxDetail(PSTCode,TaxableCodeTxt,PSTrate);
      end;
    END;

    PROCEDURE UpdateDescriptionUS@1020015();
    BEGIN
      TaxRateDescription :=
        O365TaxSettingsManagement.GenerateTaxAreaDescription(TotalRate,TempSalesTaxSetupWizard.City,TempSalesTaxSetupWizard.State);
    END;

    BEGIN
    END.
  }
}

