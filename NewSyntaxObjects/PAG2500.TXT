OBJECT Page 2500 Extension Management
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=161410.947T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    Editable=false;
    CaptionML=ENU=Extension Management;
    InsertAllowed=false;
    DeleteAllowed=false;
    ModifyAllowed=false;
    SourceTable="NAV App";
    SourceTableView=SORTING(Name)
                    ORDER(Ascending)
                    WHERE(Name=FILTER(<>'_Exclude_*'),
                          "Package Type"=FILTER(=0|2));
    PageType=List;
    RefreshOnActivate=true;
    PromotedActionCategoriesML=ENU=New,Process,Report,Details,Manage;
    OnInit=VAR
             PermissionManager@1000 : Codeunit "Permission Manager";
             ExtensionMarketplaceMgmt@1001 : Codeunit ExtensionMarketplaceMgmt;
           BEGIN
             IsSaaS := PermissionManager.SoftwareAsAService;
             IsSaaSInstallAllowed := PermissionManager.IsSandboxConfiguration or GetSaaSInstallSetting;
             IsMarketplaceEnabled := ExtensionMarketplaceMgmt.IsMarketplaceEnabled;

             // Extension should be displayed as if they were on prem
             IsOnPremDisplay := (not IsMarketplaceEnabled or not IsSaaS);
           END;

    OnOpenPage=BEGIN
                 ActionsEnabled := false;

                 // Set installed filter if we are not displaying like on-prem
                 if not (IsOnPremDisplay or IsSaaSInstallAllowed) then begin
                   CurrPage.CAPTION(SaaSCaptionTxt);
                   SETFILTER("PerTenant Or Installed",'%1',true);
                 end else
                   SETFILTER("Tenant Visible",'%1',true);
               END;

    OnAfterGetRecord=VAR
                       IsInstalled@1000 : Boolean;
                       InstalledStatus@1001 : Text[250];
                     BEGIN
                       ActionsEnabled := true;
                       IsInstalled := NavExtensionInstallationMgmt.IsInstalled("Package ID");
                       InstalledStatus := NavExtensionInstallationMgmt.GetExtensionInstalledDisplayString("Package ID");

                       VersionDisplay :=
                         STRSUBSTNO(
                           VersionFormatTxt,
                           NavExtensionInstallationMgmt.GetVersionDisplayString("Version Major","Version Minor","Version Build","Version Revision"));
                       // Currently using the "Tenant ID" field to identify development extensions
                       IsTenantExtension := Scope = 1;

                       Style := false;
                       PublisherOrStatus := Publisher;

                       // Set Name styling if on prem display (shows green)
                       if IsOnPremDisplay or IsSaaSInstallAllowed then begin
                         PublisherOrStatus := InstalledStatus;
                         Style := IsInstalled;
                       end;

                       // Adding a '- NotInstalled' if in SaaS for PerTenant extensions
                       if IsSaaS and IsTenantExtension and not IsInstalled then
                         VersionDisplay := STRSUBSTNO(PerTenantAppendTxt,VersionDisplay,InstalledStatus);
                     END;

    ActionList=ACTIONS
    {
      { 11      ;    ;ActionContainer;
                      Name=ActionContainer11;
                      ActionContainerType=ActionItems }
      { 13      ;1   ;ActionGroup;
                      Name=ActionGroup13;
                      Visible=False;
                      Enabled=False }
      { 9       ;2   ;Action    ;
                      Name=Install;
                      CaptionML=ENU=Install;
                      ToolTipML=ENU=Install the extension for the current tenant.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Visible=IsOnPremDisplay OR IsSaaSInstallAllowed;
                      Enabled=ActionsEnabled AND IsOnPremDisplay;
                      Image=NewRow;
                      PromotedCategory=Category5;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 if NavExtensionInstallationMgmt.IsInstalled("Package ID") then begin
                                   MESSAGE(AlreadyInstalledMsg,Name);
                                   exit;
                                 end;

                                 RunOldExtensionInstallation;
                               END;
                                }
      { 14      ;2   ;Action    ;
                      Name=Uninstall;
                      CaptionML=ENU=Uninstall;
                      ToolTipML=ENU=Remove the extension from the current tenant.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Enabled=ActionsEnabled;
                      Image=RemoveLine;
                      PromotedCategory=Category5;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 if not NavExtensionInstallationMgmt.IsInstalled("Package ID") then begin
                                   MESSAGE(AlreadyUninstalledMsg,Name);
                                   exit;
                                 end;

                                 RunOldExtensionInstallation;
                               END;
                                }
      { 17      ;2   ;Action    ;
                      Name=Unpublish;
                      CaptionML=ENU=Unpublish;
                      ToolTipML=ENU=Unpublish the extension from the tenant.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Visible=IsTenantExtension;
                      Enabled=ActionsEnabled;
                      Image=RemoveLine;
                      PromotedCategory=Category5;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 if NavExtensionInstallationMgmt.IsInstalled("Package ID") then begin
                                   MESSAGE(CannotUnpublishIfInstalledMsg,Name);
                                   exit;
                                 end;

                                 NavExtensionInstallationMgmt.UnpublishNavTenantExtension("Package ID");
                               END;
                                }
      { 5       ;2   ;Action    ;
                      Name=Download Source;
                      CaptionML=ENU=Download Source;
                      ToolTipML=ENU=Download the source code for the extension.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Enabled=IsTenantExtension AND "Show My Code";
                      Image=ExportFile;
                      PromotedCategory=Category5;
                      Scope=Repeater;
                      OnAction=VAR
                                 TempBlob@1000 : Record TempBlob;
                                 FileManagement@1001 : Codeunit "File Management";
                                 NvOutStream@1002 : OutStream;
                                 Designer@1003 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=12.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.Designer.NavDesignerALFunctions";
                                 FileName@1004 : Text;
                                 VersionString@1005 : Text;
                                 CleanFileName@1006 : Text;
                               BEGIN
                                 TempBlob.Blob.CREATEOUTSTREAM(NvOutStream);
                                 VersionString :=
                                   NavExtensionInstallationMgmt.GetVersionDisplayString("Version Major","Version Minor","Version Build","Version Revision");

                                 Designer.GenerateDesignerPackageZipStreamByVersion(NvOutStream,ID,VersionString);
                                 FileName := STRSUBSTNO(ExtensionFileNameTxt,Name,Publisher,VersionString);
                                 CleanFileName := Designer.SanitizeDesignerFileName(FileName,'_');
                                 FileManagement.BLOBExport(TempBlob,CleanFileName,true);
                               END;
                                }
      { 16      ;2   ;Action    ;
                      Name=LearnMore;
                      CaptionML=ENU=Learn More;
                      ToolTipML=ENU=View information from the extension provider.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Enabled=ActionsEnabled;
                      Image=Info;
                      PromotedCategory=Category5;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 HYPERLINK(Help);
                               END;
                                }
      { 7       ;2   ;Action    ;
                      Name=Refresh;
                      CaptionML=ENU=Refresh;
                      ToolTipML=ENU=Refresh the list of extensions.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=RefreshLines;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 ActionsEnabled := false;
                                 CurrPage.UPDATE(false);
                               END;
                                }
      { 12      ;2   ;Action    ;
                      Name=Extension Marketplace;
                      CaptionML=ENU=Extension Marketplace;
                      ToolTipML=ENU=Browse the extension marketplace for new extensions to install.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Visible=NOT IsOnPremDisplay;
                      Enabled=IsSaaS;
                      PromotedIsBig=true;
                      Image=NewItem;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 if AppSource.IsAvailable then begin
                                   AppSource := AppSource.Create();
                                   AppSource.ShowAppSource();
                                 end;
                               END;
                                }
      { 19      ;2   ;Action    ;
                      Name=Upload Extension;
                      CaptionML=ENU=Upload Extension;
                      ToolTipML=ENU=Upload an extension to your application.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page "Upload And Deploy Extension";
                      Promoted=true;
                      Visible=IsSaaS;
                      PromotedIsBig=true;
                      Image=Import;
                      PromotedCategory=Category5 }
      { 20      ;2   ;Action    ;
                      Name=Deployment Status;
                      CaptionML=ENU=Deployment Status;
                      ToolTipML=ENU=Check status for upload process for extensions.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page "Extension Deployment Status";
                      Promoted=true;
                      Visible=IsSaaS;
                      PromotedIsBig=true;
                      Image=View;
                      PromotedCategory=Category5 }
      { 15      ;1   ;ActionGroup;
                      Name=Manage;
                      CaptionML=ENU=Manage }
      { 10      ;2   ;Action    ;
                      Name=View;
                      ShortCutKey=Return;
                      CaptionML=ENU=View;
                      ToolTipML=ENU=View extension details.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Visible=NOT IsSaaS;
                      Enabled=ActionsEnabled;
                      Image=View;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 RunOldExtensionInstallation;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Control1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                Editable=FALSE;
                GroupType=Repeater }

    { 6   ;2   ;Field     ;
                Name=Logo;
                CaptionML=ENU=Logo;
                ToolTipML=ENU=Specifies the logo of the extension, such as the logo of the service provider.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Logo;
                ImplicitType=Media }

    { 4   ;2   ;Field     ;
                Name=AdditionalInfo;
                CaptionML=ENU=AdditionalInfo;
                ToolTipML=ENU=Specifies the person or company that created the extension.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PublisherOrStatus;
                Style=Favorable;
                StyleExpr=Style;
                ImplicitType=Text }

    { 3   ;2   ;Field     ;
                Name=Name;
                ToolTipML=ENU=Specifies the name of the extension.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Name;
                ImplicitType=Text250 }

    { 18  ;2   ;Field     ;
                Name=Control18;
                ToolTipML=ENU=Specifies a spacer for 'Brick' view mode.;
                ApplicationArea=#Basic,#Suite;
                Visible=IsOnPremDisplay;
                Enabled=IsSaaS;
                HideValue=TRUE;
                Style=Favorable;
                StyleExpr=TRUE }

    { 8   ;2   ;Field     ;
                Name=Version;
                CaptionML=ENU=Version;
                ToolTipML=ENU=Specifies the version of the extension.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=VersionDisplay;
                ImplicitType=Text }

  }
  CODE
  {
    VAR
      VersionFormatTxt@1000 : TextConst '@@@="v=version abbr, %1=Version string";ENU=v. %1';
      NavExtensionInstallationMgmt@1002 : Codeunit NavExtensionInstallationMgmt;
      AppSource@1011 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=12.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.AppSource" RUNONCLIENT;
      PublisherOrStatus@1004 : Text;
      VersionDisplay@1009 : Text;
      ActionsEnabled@1006 : Boolean;
      IsSaaS@1001 : Boolean;
      SaaSCaptionTxt@1003 : TextConst '@@@=The caption to display when on SaaS;ENU=Installed Extensions';
      Style@1005 : Boolean;
      ExtensionFileNameTxt@1013 : TextConst '@@@="{Locked};%1=Name, %2=Publisher, %3=Version";ENU=%1_%2_%3.zip';
      AlreadyInstalledMsg@1007 : TextConst '@@@="%1 = name of extension";ENU=The extension %1 is already installed.';
      AlreadyUninstalledMsg@1008 : TextConst '@@@="%1 = name of extension.";ENU=The extension %1 is not installed.';
      IsSaaSInstallAllowed@1010 : Boolean;
      IsTenantExtension@1012 : Boolean;
      CannotUnpublishIfInstalledMsg@1014 : TextConst '@@@="%1 = name of extension";ENU=The extension %1 cannot be unpublished because it is installed.';
      IsMarketplaceEnabled@1015 : Boolean;
      IsOnPremDisplay@1016 : Boolean;
      PerTenantAppendTxt@1017 : TextConst '@@@="{Locked};%1=formatted version string, %2=not installed constant";ENU=%1 - %2';

    LOCAL PROCEDURE RunOldExtensionInstallation@16();
    VAR
      ExtensionDetails@1000 : Page "Extension Details";
    BEGIN
      ExtensionDetails.SETRECORD(Rec);
      ExtensionDetails.RUN;
      if ExtensionDetails.EDITABLE = false then
        CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE GetSaaSInstallSetting@1() : Boolean;
    VAR
      ServerConfigSettingHandler@1000 : Codeunit "Server Config. Setting Handler";
      InstallAllowed@1001 : Boolean;
    BEGIN
      InstallAllowed := ServerConfigSettingHandler.GetEnableSaaSExtensionInstallSetting;
      exit(InstallAllowed);
    END;

    BEGIN
    END.
  }
}

