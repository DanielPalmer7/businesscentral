OBJECT Page 10452 Service Order Stats. Dyn
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=161724.80T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Service Order Stats Dyn.;
    InsertAllowed=false;
    DeleteAllowed=false;
    LinksAllowed=false;
    SourceTable="Service Header";
    PageType=Card;
    OnOpenPage=BEGIN
                 SalesSetup.GET;
                 AllowInvDisc := not (SalesSetup."Calc. Inv. Discount" and CustInvDiscRecExists("Invoice Disc. Code"));
                 AllowVATDifference :=
                   SalesSetup."Allow VAT Difference" and
                   not ("Document Type" in ["Document Type"::Quote]);
                 VATLinesformIsEditable := AllowVATDifference or AllowInvDisc or ("Tax Area Code" <> '');
                 CurrPage.EDITABLE := VATLinesformIsEditable;
                 TaxArea.GET("Tax Area Code");
               END;

    OnAfterGetRecord=VAR
                       ServLine@1000 : Record "Service Line";
                       TempServLine@1001 : TEMPORARY Record "Service Line";
                       TempSalesTaxAmtLine@1020000 : TEMPORARY Record "Sales Tax Amount Line";
                       PrevPrintOrder@1020001 : Integer;
                       PrevTaxPercent@1020002 : Decimal;
                     BEGIN
                       CurrPage.CAPTION(STRSUBSTNO(Text000,"Document Type"));

                       if PrevNo = "No." then
                         exit;
                       PrevNo := "No.";
                       FILTERGROUP(2);
                       SETRANGE("No.",PrevNo);
                       FILTERGROUP(0);

                       CLEAR(ServLine);
                       CLEAR(TotalServLine);
                       CLEAR(TotalServLineLCY);
                       CLEAR(ServAmtsMgt);
                       CLEAR(BreakdownLabel);
                       CLEAR(BreakdownAmt);

                       for i := 1 to 7 do begin
                         TempServLine.DELETEALL;
                         CLEAR(TempServLine);
                         ServAmtsMgt.GetServiceLines(Rec,TempServLine,i - 1);
                         SalesTaxCalculate.StartSalesTaxCalculation;
                         TempServLine.SETFILTER(Type,'>0');
                         TempServLine.SETFILTER(Quantity,'<>0');
                         if TempServLine.FIND('-') then
                           repeat
                             SalesTaxCalculate.AddServiceLine(TempServLine);
                           until TempServLine.NEXT = 0;
                         case i of
                           1:
                             begin
                               TempSalesTaxLine1.DELETEALL;
                               SalesTaxCalculate.EndSalesTaxCalculation("Posting Date");
                               SalesTaxCalculate.GetSalesTaxAmountLineTable(TempSalesTaxLine1);
                             end;
                           2:
                             begin
                               TempSalesTaxLine2.DELETEALL;
                               SalesTaxCalculate.EndSalesTaxCalculation("Posting Date");
                               SalesTaxCalculate.GetSalesTaxAmountLineTable(TempSalesTaxLine2);
                             end;
                           3:
                             begin
                               TempSalesTaxLine3.DELETEALL;
                               SalesTaxCalculate.EndSalesTaxCalculation("Posting Date");
                               SalesTaxCalculate.GetSalesTaxAmountLineTable(TempSalesTaxLine3);
                             end;
                         end;

                         ServAmtsMgt.SumServiceLinesTemp(
                           Rec,TempServLine,i - 1,TotalServLine[i],TotalServLineLCY[i],
                           VATAmount[i],VATAmountText[i],ProfitLCY[i],ProfitPct[i],TotalAdjCostLCY[i]);
                         SalesTaxCalculate.DistTaxOverServLines(TempServLine);
                         if i = 3 then
                           TotalAdjCostLCY[i] := TotalServLineLCY[i]."Unit Cost (LCY)";

                         AdjProfitLCY[i] := TotalServLineLCY[i].Amount - TotalAdjCostLCY[i];
                         if TotalServLineLCY[i].Amount <> 0 then
                           AdjProfitPct[i] := ROUND(AdjProfitLCY[i] / TotalServLineLCY[i].Amount * 100,0.1);
                         TotalAmount1[i] := TotalServLine[i].Amount;
                         TotalAmount2[i] := TotalAmount1[i];
                         VATAmount[i] := 0;

                         SalesTaxCalculate.GetSummarizedSalesTaxTable(TempSalesTaxAmtLine);
                         BrkIdx := 0;
                         PrevPrintOrder := 0;
                         PrevTaxPercent := 0;
                         if TaxArea."Country/Region" = TaxArea."Country/Region"::CA then
                           BreakdownTitle := Text1020010
                         else
                           BreakdownTitle := Text1020011;
                         with TempSalesTaxAmtLine do begin
                           RESET;
                           SETCURRENTKEY("Print Order","Tax Area Code for Key","Tax Jurisdiction Code");
                           if FIND('-') then
                             repeat
                               if ("Print Order" = 0) or
                                  ("Print Order" <> PrevPrintOrder) or
                                  ("Tax %" <> PrevTaxPercent)
                               then begin
                                 BrkIdx := BrkIdx + 1;
                                 if BrkIdx > ARRAYLEN(BreakdownAmt) then begin
                                   BrkIdx := BrkIdx - 1;
                                   BreakdownLabel[i,BrkIdx] := Text1020012;
                                 end else
                                   BreakdownLabel[i,BrkIdx] := STRSUBSTNO("Print Description","Tax %");
                               end;
                               BreakdownAmt[i,BrkIdx] := BreakdownAmt[i,BrkIdx] + "Tax Amount";
                               VATAmount[i] := VATAmount[i] + "Tax Amount";
                             until NEXT = 0;
                           TotalAmount2[i] := TotalAmount2[i] + VATAmount[i];
                         end;
                       end;
                       TempServLine.DELETEALL;
                       CLEAR(TempServLine);

                       if Cust.GET("Bill-to Customer No.") then
                         Cust.CALCFIELDS("Balance (LCY)")
                       else
                         CLEAR(Cust);
                       case true of
                         Cust."Credit Limit (LCY)" = 0:
                           CreditLimitLCYExpendedPct := 0;
                         Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" < 0:
                           CreditLimitLCYExpendedPct := 0;
                         Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" > 1:
                           CreditLimitLCYExpendedPct := 10000;
                         else
                           CreditLimitLCYExpendedPct := ROUND(Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" * 10000,1);
                       end;

                       TempSalesTaxLine1.MODIFYALL(Modified,false);
                       TempSalesTaxLine2.MODIFYALL(Modified,false);
                       TempSalesTaxLine3.MODIFYALL(Modified,false);
                       // TempSalesTaxLine4.MODIFYALL(Modified,FALSE);

                       PrevTab := -1;
                     END;

    OnQueryClosePage=BEGIN
                       GetVATSpecification(PrevTab);
                       if TempSalesTaxLine1.GetAnyLineModified or TempSalesTaxLine2.GetAnyLineModified then
                         UpdateTaxonServLines;
                       exit(true);
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Control1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General }

    { 30  ;2   ;Field     ;
                Name="TotalServLine[1].""Inv. Discount Amount""";
                CaptionML=ENU=Inv. Discount Amount;
                ToolTipML=ENU=Specifies the invoice discount amount for the service order.;
                SourceExpr=TotalServLine[1]."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE;
                OnValidate=BEGIN
                             UpdateInvDiscAmount(1);
                           END;
                            }

    { 20  ;2   ;Field     ;
                Name="TotalAmount1[1]";
                SourceExpr=TotalAmount1[1];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,false);
                Editable=FALSE;
                OnValidate=BEGIN
                             UpdateTotalAmount(1);
                           END;
                            }

    { 16  ;2   ;Field     ;
                Name="VATAmount[1]";
                CaptionML=ENU=Tax Amount;
                ToolTipML=ENU=Specifies the tax amount.;
                SourceExpr=VATAmount[1];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                Name="TotalAmount2[1]";
                SourceExpr=TotalAmount2[1];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,true);
                Editable=FALSE;
                OnValidate=BEGIN
                             TotalAmount21OnAfterValidate;
                           END;
                            }

    { 7   ;2   ;Field     ;
                Name="TotalServLineLCY[1].Amount";
                CaptionML=ENU=Sales ($);
                ToolTipML=ENU=Specifies the sales amount, in dollars.;
                SourceExpr=TotalServLineLCY[1].Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 32  ;2   ;Field     ;
                Name="ProfitLCY[1]";
                CaptionML=ENU=Profit ($);
                ToolTipML=ENU="Specifies the profit, expressed as an amount.  ";
                SourceExpr=ProfitLCY[1];
                AutoFormatType=1;
                Editable=FALSE }

    { 46  ;2   ;Field     ;
                Name="TotalServLineLCY[1].""Unit Cost (LCY)""";
                CaptionML=ENU=Original Cost ($);
                ToolTipML=ENU=Specifies the original cost of the items on the service order.;
                SourceExpr=TotalServLineLCY[1]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 1020003;2;Field     ;
                Name=BreakdownTitle;
                CaptionClass=FORMAT(BreakdownTitle);
                Editable=FALSE }

    { 24  ;2   ;Field     ;
                Name="ProfitPct[1]";
                CaptionML=ENU=Original Profit %;
                ToolTipML=ENU=Specifies the profit, expressed as a percentage, which was associated with the service order when it was originally posted.;
                DecimalPlaces=1:1;
                SourceExpr=ProfitPct[1];
                Editable=FALSE }

    { 52  ;2   ;Field     ;
                Name="AdjProfitPct[1]";
                CaptionML=ENU=Adjusted Profit %;
                ToolTipML=ENU=Specifies the adjusted profit of the contents of the entire service order, in local currency.;
                DecimalPlaces=1:1;
                SourceExpr=AdjProfitPct[1];
                Editable=FALSE }

    { 22  ;2   ;Field     ;
                Name="TotalServLine[1].Quantity";
                CaptionML=ENU=Quantity;
                ToolTipML=ENU=Specifies the quantity of the item/resource on the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[1].Quantity;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                Name="TotalServLine[1].""Units per Parcel""";
                CaptionML=ENU=Parcels;
                ToolTipML=ENU=Specifies the number of parcels on the document.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[1]."Units per Parcel";
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                Name="TotalServLine[1].""Net Weight""";
                CaptionML=ENU=Net Weight;
                ToolTipML=ENU=Specifies the total net weight of items in the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[1]."Net Weight";
                Editable=FALSE }

    { 11  ;2   ;Field     ;
                Name="TotalServLine[1].""Gross Weight""";
                CaptionML=ENU=Gross Weight;
                ToolTipML=ENU=Specifies the gross weight of items in the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[1]."Gross Weight";
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                Name="TotalServLine[1].""Unit Volume""";
                CaptionML=ENU=Volume;
                ToolTipML=ENU=Specifies the volume of the items in the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[1]."Unit Volume";
                Editable=FALSE }

    { 120 ;2   ;Field     ;
                Name="TotalAdjCostLCY[1] - TotalServLineLCY[1].""Unit Cost (LCY)""";
                CaptionML=ENU=Cost Adjmt. Amount ($);
                ToolTipML=ENU=Specifies the amount of the cost adjustment on the service order, in dollars.;
                SourceExpr=TotalAdjCostLCY[1] - TotalServLineLCY[1]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE;
                OnLookup=BEGIN
                           LookupAdjmtValueEntries(0);
                         END;
                          }

    { 1020008;2;Field     ;
                Name="BreakdownAmt[1,1]";
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[1,1];
                CaptionClass=BreakdownLabel[1,1];
                Editable=FALSE }

    { 1020009;2;Field     ;
                Name=BreakdownAmt2;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[1,2];
                CaptionClass=GetCaptionClass(BreakdownLabel[1,2],false);
                Editable=FALSE }

    { 1020010;2;Field     ;
                Name=BreakdownAmt3;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[1,3];
                CaptionClass=FORMAT(BreakdownLabel[1,3]);
                Editable=FALSE }

    { 1020011;2;Field     ;
                Name=BreakdownAmt4;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[1,4];
                CaptionClass=FORMAT(BreakdownLabel[1,4]);
                Editable=FALSE }

    { 119 ;2   ;Field     ;
                Name="TempSalesTaxLine1.COUNT";
                DrillDown=true;
                CaptionML=ENU=No. of VAT Lines;
                ToolTipML=ENU=Specifies the number of tax lines on the service order.;
                SourceExpr=TempSalesTaxLine1.COUNT;
                OnDrillDown=BEGIN
                              VATLinesDrillDown(TempSalesTaxLine1,false);
                              UpdateHeaderInfo(1,TempSalesTaxLine1);
                            END;
                             }

    { 1905885101;1;Group  ;
                Name=Invoicing;
                CaptionML=ENU=Invoicing }

    { 71  ;2   ;Field     ;
                Name="TotalServLine[2].""Line Amount""";
                SourceExpr=TotalServLine[2]."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,false);
                Editable=FALSE }

    { 51  ;2   ;Field     ;
                Name="TotalServLine[2].""Inv. Discount Amount""";
                CaptionML=ENU=Inv. Discount Amount;
                ToolTipML=ENU=Specifies the invoice discount amount for the service order.;
                SourceExpr=TotalServLine[2]."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE;
                OnValidate=BEGIN
                             UpdateInvDiscAmount(2);
                           END;
                            }

    { 50  ;2   ;Field     ;
                Name="TotalAmount1[2]";
                SourceExpr=TotalAmount1[2];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,false);
                Editable=FALSE;
                OnValidate=BEGIN
                             UpdateTotalAmount(2);
                           END;
                            }

    { 49  ;2   ;Field     ;
                Name="VATAmount[2]";
                CaptionML=ENU=Tax Amount;
                ToolTipML=ENU=Specifies the tax amount.;
                SourceExpr=VATAmount[2];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 48  ;2   ;Field     ;
                Name="TotalAmount2[2]";
                SourceExpr=TotalAmount2[2];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,true);
                Editable=FALSE }

    { 69  ;2   ;Field     ;
                Name="TotalServLineLCY[2].Amount";
                CaptionML=ENU=Sales ($);
                ToolTipML=ENU=Specifies the sales amount, in dollars.;
                SourceExpr=TotalServLineLCY[2].Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 67  ;2   ;Field     ;
                Name="ProfitLCY[2]";
                CaptionML=ENU=Original Profit ($);
                ToolTipML=ENU=Specifies the profit, expressed as an amount in local currency, which was associated with the service order, when it was originally posted.;
                SourceExpr=ProfitLCY[2];
                AutoFormatType=1;
                Editable=FALSE }

    { 75  ;2   ;Field     ;
                Name="AdjProfitLCY[2]";
                CaptionML=ENU=Adjusted Profit ($);
                ToolTipML=ENU=Specifies the adjusted profit of the service order.;
                SourceExpr=AdjProfitLCY[2];
                AutoFormatType=1;
                Editable=FALSE }

    { 1020013;2;Field     ;
                Name=BreakdownTitle2;
                CaptionClass=FORMAT(BreakdownTitle);
                Editable=FALSE }

    { 66  ;2   ;Field     ;
                Name="ProfitPct[2]";
                CaptionML=ENU=Original Profit %;
                ToolTipML=ENU=Specifies the profit, expressed as a percentage, which was associated with the service order when it was originally posted.;
                DecimalPlaces=1:1;
                SourceExpr=ProfitPct[2];
                Editable=FALSE }

    { 79  ;2   ;Field     ;
                Name="AdjProfitPct[2]";
                CaptionML=ENU=Adjusted Profit %;
                ToolTipML=ENU=Specifies the adjusted profit of the contents of the entire service order, in local currency.;
                DecimalPlaces=1:1;
                SourceExpr=AdjProfitPct[2];
                Editable=FALSE }

    { 65  ;2   ;Field     ;
                Name="TotalServLine[2].Quantity";
                CaptionML=ENU=Quantity;
                ToolTipML=ENU=Specifies the quantity of the item/resource on the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[2].Quantity;
                Editable=FALSE }

    { 62  ;2   ;Field     ;
                Name="TotalServLine[2].""Units per Parcel""";
                CaptionML=ENU=Parcels;
                ToolTipML=ENU=Specifies the number of parcels on the document.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[2]."Units per Parcel";
                Editable=FALSE }

    { 64  ;2   ;Field     ;
                Name="TotalServLine[2].""Net Weight""";
                CaptionML=ENU=Net Weight;
                ToolTipML=ENU=Specifies the total net weight of items in the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[2]."Net Weight";
                Editable=FALSE }

    { 63  ;2   ;Field     ;
                Name="TotalServLine[2].""Gross Weight""";
                CaptionML=ENU=Gross Weight;
                ToolTipML=ENU=Specifies the gross weight of items in the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[2]."Gross Weight";
                Editable=FALSE }

    { 61  ;2   ;Field     ;
                Name="TotalServLine[2].""Unit Volume""";
                CaptionML=ENU=Volume;
                ToolTipML=ENU=Specifies the volume of the items in the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[2]."Unit Volume";
                Editable=FALSE }

    { 68  ;2   ;Field     ;
                Name="TotalServLineLCY[2].""Unit Cost (LCY)""";
                CaptionML=ENU=Original Cost ($);
                ToolTipML=ENU=Specifies the original cost of the items on the service order.;
                SourceExpr=TotalServLineLCY[2]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 17  ;2   ;Field     ;
                Name="TotalAdjCostLCY[2]";
                CaptionML=ENU=Adjusted Cost ($);
                ToolTipML=ENU=Specifies the adjusted cost of the service order.;
                SourceExpr=TotalAdjCostLCY[2];
                AutoFormatType=1;
                Editable=FALSE }

    { 122 ;2   ;Field     ;
                Name="TotalAdjCostLCY[2] - TotalServLineLCY[2].""Unit Cost (LCY)""";
                CaptionML=ENU=Cost Adjmt. Amount ($);
                ToolTipML=ENU=Specifies the amount of the cost adjustment on the service order, in dollars.;
                SourceExpr=TotalAdjCostLCY[2] - TotalServLineLCY[2]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE;
                OnLookup=BEGIN
                           LookupAdjmtValueEntries(1);
                         END;
                          }

    { 1020012;2;Field     ;
                Name=BreakdownAmt5;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[2,1];
                CaptionClass=FORMAT(BreakdownLabel[2,1]);
                Editable=FALSE }

    { 1020016;2;Field     ;
                Name=BreakdownAmt6;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[2,2];
                CaptionClass=FORMAT(BreakdownLabel[2,2]);
                Editable=FALSE }

    { 1020018;2;Field     ;
                Name=BreakdownAmt7;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[2,3];
                CaptionClass=FORMAT(BreakdownLabel[2,3]);
                Editable=FALSE }

    { 1020020;2;Field     ;
                Name=BreakdownAmt8;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[2,4];
                CaptionClass=FORMAT(BreakdownLabel[2,4]);
                Editable=FALSE }

    { 1480020;2;Field     ;
                Name="TempSalesTaxLine2.COUNT";
                DrillDown=true;
                CaptionML=ENU=No. of VAT Lines;
                ToolTipML=ENU=Specifies the number of tax lines on the service order.;
                SourceExpr=TempSalesTaxLine2.COUNT;
                OnDrillDown=BEGIN
                              VATLinesDrillDown(TempSalesTaxLine1,true);
                              UpdateHeaderInfo(1,TempSalesTaxLine1);
                            END;
                             }

    { 1906801201;1;Group  ;
                Name=Shipping;
                CaptionML=ENU=Shipping }

    { 105 ;2   ;Field     ;
                Name="TotalServLine[3].""Line Amount""";
                SourceExpr=TotalServLine[3]."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,false);
                Editable=FALSE }

    { 83  ;2   ;Field     ;
                Name="TotalServLine[3].""Inv. Discount Amount""";
                CaptionML=ENU=Inv. Discount Amount;
                ToolTipML=ENU=Specifies the invoice discount amount for the service order.;
                SourceExpr=TotalServLine[3]."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 82  ;2   ;Field     ;
                Name="TotalAmount1[3]";
                SourceExpr=TotalAmount1[3];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,false);
                Editable=FALSE }

    { 81  ;2   ;Field     ;
                Name="VATAmount[3]";
                CaptionML=ENU=Tax Amount;
                ToolTipML=ENU=Specifies the tax amount.;
                SourceExpr=VATAmount[3];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 80  ;2   ;Field     ;
                Name="TotalAmount2[3]";
                SourceExpr=TotalAmount2[3];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,true);
                Editable=FALSE }

    { 103 ;2   ;Field     ;
                Name="TotalServLineLCY[3].Amount";
                CaptionML=ENU=Sales ($);
                ToolTipML=ENU=Specifies the sales amount, in dollars.;
                SourceExpr=TotalServLineLCY[3].Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 102 ;2   ;Field     ;
                Name="TotalServLineLCY[3].""Unit Cost (LCY)""";
                CaptionML=ENU=Cost ($);
                ToolTipML=ENU=Specifies the cost of the service order, in dollars.;
                SourceExpr=TotalServLineLCY[3]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 101 ;2   ;Field     ;
                Name="ProfitLCY[3]";
                CaptionML=ENU=Profit ($);
                ToolTipML=ENU="Specifies the profit, expressed as an amount.  ";
                SourceExpr=ProfitLCY[3];
                AutoFormatType=1;
                Editable=FALSE }

    { 100 ;2   ;Field     ;
                Name="ProfitPct[3]";
                CaptionML=ENU=Profit %;
                ToolTipML=ENU="Specifies the profit, expressed as a percentage.  ";
                DecimalPlaces=1:1;
                SourceExpr=ProfitPct[3];
                Editable=FALSE }

    { 1020021;2;Field     ;
                Name=BreakdownTitle3;
                CaptionClass=FORMAT(BreakdownTitle);
                Editable=FALSE }

    { 99  ;2   ;Field     ;
                Name="TotalServLine[3].Quantity";
                CaptionML=ENU=Quantity;
                ToolTipML=ENU=Specifies the quantity of the item/resource on the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[3].Quantity;
                Editable=FALSE }

    { 94  ;2   ;Field     ;
                Name="TotalServLine[3].""Units per Parcel""";
                CaptionML=ENU=Parcels;
                ToolTipML=ENU=Specifies the number of parcels on the document.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[3]."Units per Parcel";
                Editable=FALSE }

    { 98  ;2   ;Field     ;
                Name="TotalServLine[3].""Net Weight""";
                CaptionML=ENU=Net Weight;
                ToolTipML=ENU=Specifies the total net weight of items in the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[3]."Net Weight";
                Editable=FALSE }

    { 97  ;2   ;Field     ;
                Name="TotalServLine[3].""Gross Weight""";
                CaptionML=ENU=Gross Weight;
                ToolTipML=ENU=Specifies the gross weight of items in the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[3]."Gross Weight";
                Editable=FALSE }

    { 93  ;2   ;Field     ;
                Name="TotalServLine[3].""Unit Volume""";
                CaptionML=ENU=Volume;
                ToolTipML=ENU=Specifies the volume of the items in the service order.;
                DecimalPlaces=0:5;
                SourceExpr=TotalServLine[3]."Unit Volume";
                Editable=FALSE }

    { 1020024;2;Field     ;
                Name=BreakdownAmt9;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[3,1];
                CaptionClass=FORMAT(BreakdownLabel[3,1]);
                Editable=FALSE }

    { 1020025;2;Field     ;
                Name=BreakdownAmt10;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[3,2];
                CaptionClass=FORMAT(BreakdownLabel[3,2]);
                Editable=FALSE }

    { 1020027;2;Field     ;
                Name=BreakdownAmt11;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[3,3];
                CaptionClass=FORMAT(BreakdownLabel[3,3]);
                Editable=FALSE }

    { 1020029;2;Field     ;
                Name=BreakdownAmt12;
                CaptionML=ENU=BreakdownAmt;
                BlankZero=true;
                SourceExpr=BreakdownAmt[3,4];
                CaptionClass=FORMAT(BreakdownLabel[3,4]);
                Editable=FALSE }

    { 1480022;2;Field     ;
                Name="TempSalesTaxLine3.COUNT";
                DrillDown=true;
                CaptionML=ENU=No. of VAT Lines;
                ToolTipML=ENU=Specifies the number of tax lines on the service order.;
                SourceExpr=TempSalesTaxLine3.COUNT;
                OnDrillDown=BEGIN
                              VATLinesDrillDown(TempSalesTaxLine1,false);
                              UpdateHeaderInfo(1,TempSalesTaxLine1);
                            END;
                             }

    { 1903289601;1;Group  ;
                Name=Customer;
                CaptionML=ENU=Customer }

    { 1480003;2;Field     ;
                Name="Cust.""Balance (LCY)""";
                CaptionML=ENU=Balance ($);
                ToolTipML=ENU="Specifies the customer's balance. ";
                SourceExpr=Cust."Balance (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 1480000;2;Field     ;
                Name="Cust.""Credit Limit (LCY)""";
                CaptionML=ENU=Credit Limit ($);
                ToolTipML=ENU=Specifies the customer's credit limit, in dollars.;
                SourceExpr=Cust."Credit Limit (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 1480002;2;Field     ;
                Name=CreditLimitLCYExpendedPct;
                ExtendedDatatype=Ratio;
                CaptionML=ENU=Expended % of Credit Limit ($);
                ToolTipML=ENU=Specifies the Expended Percentage of Credit Limit ($).;
                SourceExpr=CreditLimitLCYExpendedPct;
                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Service %1 Statistics';
      Text001@1001 : TextConst 'ENU=Total';
      Text002@1002 : TextConst 'ENU=Amount';
      Text003@1003 : TextConst 'ENU=%1 must not be 0.';
      Text004@1004 : TextConst 'ENU=%1 must not be greater than %2.';
      Text005@1005 : TextConst 'ENU=You cannot change the invoice discount because there is a %1 record for %2 %3.';
      TotalServLine@1006 : ARRAY [7] OF Record "Service Line";
      TotalServLineLCY@1007 : ARRAY [7] OF Record "Service Line";
      Cust@1008 : Record Customer;
      TempSalesTaxLine1@1020001 : TEMPORARY Record "Sales Tax Amount Line";
      TempSalesTaxLine2@1020002 : TEMPORARY Record "Sales Tax Amount Line";
      TempSalesTaxLine3@1020003 : TEMPORARY Record "Sales Tax Amount Line";
      SalesSetup@1012 : Record "Sales & Receivables Setup";
      ServAmtsMgt@1480029 : Codeunit "Serv-Amounts Mgt.";
      SalesTaxDifference@1020004 : Record "Sales Tax Amount Difference";
      TaxArea@1020005 : Record "Tax Area";
      SalesTaxCalculate@1020000 : Codeunit "Sales Tax Calculate";
      TotalAmount1@1014 : ARRAY [7] OF Decimal;
      TotalAmount2@1015 : ARRAY [7] OF Decimal;
      VATAmount@1016 : ARRAY [7] OF Decimal;
      VATAmountText@1017 : ARRAY [7] OF Text[30];
      ProfitLCY@1018 : ARRAY [7] OF Decimal;
      ProfitPct@1019 : ARRAY [7] OF Decimal;
      AdjProfitLCY@1031 : ARRAY [7] OF Decimal;
      AdjProfitPct@1030 : ARRAY [7] OF Decimal;
      TotalAdjCostLCY@1029 : ARRAY [7] OF Decimal;
      CreditLimitLCYExpendedPct@1020 : Decimal;
      i@1021 : Integer;
      PrevNo@1022 : Code[20];
      ActiveTab@1023 : 'General,Invoicing,Shipping';
      PrevTab@1024 : 'General,Invoicing,Shipping';
      VATLinesformIsEditable@1026 : Boolean;
      AllowInvDisc@1027 : Boolean;
      AllowVATDifference@1028 : Boolean;
      VATLinesForm@1044 : Page "Sales Tax Lines Subform";
      BreakdownTitle@1020006 : Text[35];
      BreakdownLabel@1020007 : ARRAY [3,4] OF Text[30];
      BreakdownAmt@1020008 : ARRAY [3,4] OF Decimal;
      BrkIdx@1020009 : Integer;
      Text1020010@1020010 : TextConst 'ENU=Tax Breakdown:';
      Text1020011@1020011 : TextConst 'ENU=Sales Tax Breakdown:';
      Text1020012@1020012 : TextConst 'ENU=Other Taxes';

    LOCAL PROCEDURE UpdateHeaderInfo@5(IndexNo@1000 : Integer;VAR SalesTaxAmountLine@1001 : TEMPORARY Record "Sales Tax Amount Line");
    VAR
      CurrExchRate@1002 : Record "Currency Exchange Rate";
      UseDate@1003 : Date;
    BEGIN
      TotalServLine[IndexNo]."Inv. Discount Amount" := SalesTaxAmountLine.GetTotalInvDiscAmount;
      TotalAmount1[IndexNo] :=
        TotalServLine[IndexNo]."Line Amount" - TotalServLine[IndexNo]."Inv. Discount Amount";
      VATAmount[IndexNo] := SalesTaxAmountLine.GetTotalTaxAmountFCY;
      if "Prices Including VAT" then
        TotalAmount2[IndexNo] := TotalServLine[IndexNo].Amount
      else
        TotalAmount2[IndexNo] := TotalAmount1[IndexNo] + VATAmount[IndexNo];

      if "Prices Including VAT" then
        TotalServLineLCY[IndexNo].Amount := TotalAmount2[IndexNo]
      else
        TotalServLineLCY[IndexNo].Amount := TotalAmount1[IndexNo];
      if "Currency Code" <> '' then
        if ("Document Type" = "Document Type"::Quote) and
           ("Posting Date" = 0D)
        then
          UseDate := WORKDATE
        else
          UseDate := "Posting Date";

      TotalServLineLCY[IndexNo].Amount :=
        CurrExchRate.ExchangeAmtFCYToLCY(
          UseDate,"Currency Code",TotalServLineLCY[IndexNo].Amount,"Currency Factor");

      ProfitLCY[IndexNo] := TotalServLineLCY[IndexNo].Amount - TotalServLineLCY[IndexNo]."Unit Cost (LCY)";
      if TotalServLineLCY[IndexNo].Amount = 0 then
        ProfitPct[IndexNo] := 0
      else
        ProfitPct[IndexNo] := ROUND(100 * ProfitLCY[IndexNo] / TotalServLineLCY[IndexNo].Amount,0.01);

      AdjProfitLCY[IndexNo] := TotalServLineLCY[IndexNo].Amount - TotalAdjCostLCY[IndexNo];
      if TotalServLineLCY[IndexNo].Amount = 0 then
        AdjProfitPct[IndexNo] := 0
      else
        AdjProfitPct[IndexNo] := ROUND(100 * AdjProfitLCY[IndexNo] / TotalServLineLCY[IndexNo].Amount,0.01);
    END;

    LOCAL PROCEDURE GetVATSpecification@21(QtyType@1000 : 'General,Invoicing,Shipping');
    BEGIN
      case QtyType of
        QtyType::General:
          begin
            VATLinesForm.GetTempTaxAmountLine(TempSalesTaxLine1);
            UpdateHeaderInfo(1,TempSalesTaxLine1);
          end;
        QtyType::Invoicing:
          begin
            VATLinesForm.GetTempTaxAmountLine(TempSalesTaxLine2);
            UpdateHeaderInfo(2,TempSalesTaxLine2);
          end;
        QtyType::Shipping:
          VATLinesForm.GetTempTaxAmountLine(TempSalesTaxLine3);
      end;
    END;

    LOCAL PROCEDURE UpdateTotalAmount@16(IndexNo@1000 : Integer);
    VAR
      SaveTotalAmount@1001 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      if "Prices Including VAT" then begin
        SaveTotalAmount := TotalAmount1[IndexNo];
        UpdateInvDiscAmount(IndexNo);
        TotalAmount1[IndexNo] := SaveTotalAmount;
      end;

      with TotalServLine[IndexNo] do
        "Inv. Discount Amount" := "Line Amount" - TotalAmount1[IndexNo];
      UpdateInvDiscAmount(IndexNo);
    END;

    LOCAL PROCEDURE UpdateInvDiscAmount@3(ModifiedIndexNo@1000 : Integer);
    VAR
      PartialInvoicing@1001 : Boolean;
      MaxIndexNo@1002 : Integer;
      IndexNo@1003 : ARRAY [2] OF Integer;
      i@1004 : Integer;
      InvDiscBaseAmount@1005 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      if not (ModifiedIndexNo in [1,2]) then
        exit;

      if ModifiedIndexNo = 1 then
        InvDiscBaseAmount := TempSalesTaxLine1.GetTotalInvDiscBaseAmount(false,"Currency Code")
      else
        InvDiscBaseAmount := TempSalesTaxLine2.GetTotalInvDiscBaseAmount(false,"Currency Code");

      if InvDiscBaseAmount = 0 then
        ERROR(Text003,TempSalesTaxLine2.FIELDCAPTION("Inv. Disc. Base Amount"));

      if TotalServLine[ModifiedIndexNo]."Inv. Discount Amount" / InvDiscBaseAmount > 1 then
        ERROR(
          Text004,
          TotalServLine[ModifiedIndexNo].FIELDCAPTION("Inv. Discount Amount"),
          TempSalesTaxLine2.FIELDCAPTION("Inv. Disc. Base Amount"));

      PartialInvoicing := (TotalServLine[1]."Line Amount" <> TotalServLine[2]."Line Amount");

      IndexNo[1] := ModifiedIndexNo;
      IndexNo[2] := 3 - ModifiedIndexNo;
      if (ModifiedIndexNo = 2) and PartialInvoicing then
        MaxIndexNo := 1
      else
        MaxIndexNo := 2;

      if not PartialInvoicing then
        if ModifiedIndexNo = 1 then
          TotalServLine[2]."Inv. Discount Amount" := TotalServLine[1]."Inv. Discount Amount"
        else
          TotalServLine[1]."Inv. Discount Amount" := TotalServLine[2]."Inv. Discount Amount";

      for i := 1 to MaxIndexNo do
        with TotalServLine[IndexNo[i]] do begin
          if (i = 1) or not PartialInvoicing then
            if IndexNo[i] = 1 then begin
              TempSalesTaxLine1.SetInvoiceDiscountAmount(
                "Inv. Discount Amount","Currency Code","Prices Including VAT","VAT Base Discount %");
            end else
              TempSalesTaxLine2.SetInvoiceDiscountAmount(
                "Inv. Discount Amount","Currency Code","Prices Including VAT","VAT Base Discount %");

          if (i = 2) and PartialInvoicing then
            if IndexNo[i] = 1 then begin
              InvDiscBaseAmount := TempSalesTaxLine2.GetTotalInvDiscBaseAmount(false,"Currency Code");
              if InvDiscBaseAmount = 0 then
                TempSalesTaxLine1.SetInvoiceDiscountPercent(
                  0,"Currency Code","Prices Including VAT",false,"VAT Base Discount %")
              else
                TempSalesTaxLine1.SetInvoiceDiscountPercent(
                  100 * TempSalesTaxLine2.GetTotalInvDiscAmount / InvDiscBaseAmount,
                  "Currency Code","Prices Including VAT",false,"VAT Base Discount %");
            end else begin
              InvDiscBaseAmount := TempSalesTaxLine1.GetTotalInvDiscBaseAmount(false,"Currency Code");
              if InvDiscBaseAmount = 0 then
                TempSalesTaxLine2.SetInvoiceDiscountPercent(
                  0,"Currency Code","Prices Including VAT",false,"VAT Base Discount %")
              else
                TempSalesTaxLine2.SetInvoiceDiscountPercent(
                  100 * TempSalesTaxLine1.GetTotalInvDiscAmount / InvDiscBaseAmount,
                  "Currency Code","Prices Including VAT",false,"VAT Base Discount %");
            end;
        end;

      UpdateHeaderInfo(1,TempSalesTaxLine1);
      UpdateHeaderInfo(2,TempSalesTaxLine2);

      if ModifiedIndexNo = 1 then
        VATLinesForm.SetTempTaxAmountLine(TempSalesTaxLine1)
      else
        VATLinesForm.SetTempTaxAmountLine(TempSalesTaxLine2);

      "Invoice Discount Calculation" := "Invoice Discount Calculation"::Amount;
      "Invoice Discount Value" := TotalServLine[1]."Inv. Discount Amount";
      MODIFY;

      UpdateTaxonServLines;
    END;

    LOCAL PROCEDURE GetCaptionClass@2(FieldCaption@1000 : Text[100];ReverseCaption@1001 : Boolean) : Text[80];
    BEGIN
      if "Prices Including VAT" xor ReverseCaption then
        exit('2,1,' + FieldCaption);
      exit('2,0,' + FieldCaption);
    END;

    LOCAL PROCEDURE UpdateTaxonServLines@1();
    VAR
      ServLine@1000 : Record "Service Line";
    BEGIN
      GetVATSpecification(ActiveTab);

      ServLine.RESET;
      ServLine.SETRANGE("Document Type","Document Type");
      ServLine.SETRANGE("No.","No.");
      ServLine.FINDFIRST;

      if TempSalesTaxLine1.GetAnyLineModified then begin
        SalesTaxCalculate.StartSalesTaxCalculation;
        SalesTaxCalculate.PutSalesTaxAmountLineTable(
          TempSalesTaxLine1,
          SalesTaxDifference."Document Product Area"::Service,
          "Document Type","No.");
        SalesTaxCalculate.DistTaxOverServLines(ServLine);
        SalesTaxCalculate.SaveTaxDifferences;
      end;
      if TempSalesTaxLine2.GetAnyLineModified then begin
        SalesTaxCalculate.StartSalesTaxCalculation;
        SalesTaxCalculate.PutSalesTaxAmountLineTable(
          TempSalesTaxLine2,
          SalesTaxDifference."Document Product Area"::Service,
          "Document Type","No.");
        SalesTaxCalculate.DistTaxOverServLines(ServLine);
        SalesTaxCalculate.SaveTaxDifferences;
      end;

      PrevNo := '';
    END;

    LOCAL PROCEDURE CustInvDiscRecExists@4(InvDiscCode@1000 : Code[20]) : Boolean;
    VAR
      CustInvDisc@1001 : Record "Cust. Invoice Disc.";
    BEGIN
      CustInvDisc.SETRANGE(Code,InvDiscCode);
      exit(CustInvDisc.FINDFIRST);
    END;

    LOCAL PROCEDURE CheckAllowInvDisc@8();
    VAR
      CustInvDisc@1000 : Record "Cust. Invoice Disc.";
    BEGIN
      if not AllowInvDisc then
        ERROR(
          Text005,
          CustInvDisc.TABLECAPTION,FIELDCAPTION("Invoice Disc. Code"),"Invoice Disc. Code");
    END;

    [External]
    PROCEDURE VATLinesDrillDown@1480007(VAR VATLinesToDrillDown@1000 : Record "Sales Tax Amount Line";ThisTabAllowsVATEditing@1001 : Boolean);
    BEGIN
      CLEAR(VATLinesForm);
      VATLinesForm.SetTempTaxAmountLine(VATLinesToDrillDown);
      VATLinesForm.InitGlobals(
        "Currency Code",AllowVATDifference,AllowVATDifference and ThisTabAllowsVATEditing,
        "Prices Including VAT",AllowInvDisc,"VAT Base Discount %");
      VATLinesForm.RUNMODAL;
      VATLinesForm.GetTempTaxAmountLine(VATLinesToDrillDown);
    END;

    [External]
    PROCEDURE GetDetailsTotal@6() : Decimal;
    BEGIN
      if TotalServLineLCY[2].Amount = 0 then
        exit(0);
      exit(ROUND(100 * (ProfitLCY[2] + ProfitLCY[4]) / TotalServLineLCY[2].Amount,0.01));
    END;

    [External]
    PROCEDURE GetAdjDetailsTotal@7() : Decimal;
    BEGIN
      if TotalServLineLCY[2].Amount = 0 then
        exit(0);
      exit(ROUND(100 * (AdjProfitLCY[2] + AdjProfitLCY[4]) / TotalServLineLCY[2].Amount,0.01));
    END;

    [External]
    PROCEDURE UpdateHeaderServLine@9();
    VAR
      TempServLine@1000 : TEMPORARY Record "Service Line";
    BEGIN
      CLEAR(ServAmtsMgt);

      for i := 1 to 7 do
        if i in [1,5,6,7] then begin
          TempServLine.DELETEALL;
          CLEAR(TempServLine);
          ServAmtsMgt.GetServiceLines(Rec,TempServLine,i - 1);

          ServAmtsMgt.SumServiceLinesTemp(
            Rec,TempServLine,i - 1,TotalServLine[i],TotalServLineLCY[i],
            VATAmount[i],VATAmountText[i],ProfitLCY[i],ProfitPct[i],TotalAdjCostLCY[i]);

          if TotalServLineLCY[i].Amount = 0 then
            ProfitPct[i] := 0
          else
            ProfitPct[i] := ROUND(100 * ProfitLCY[i] / TotalServLineLCY[i].Amount,0.1);

          AdjProfitLCY[i] := TotalServLineLCY[i].Amount - TotalAdjCostLCY[i];
          if TotalServLineLCY[i].Amount <> 0 then
            AdjProfitPct[i] := ROUND(100 * AdjProfitLCY[i] / TotalServLineLCY[i].Amount,0.1);

          if "Prices Including VAT" then begin
            TotalAmount2[i] := TotalServLine[i].Amount;
            TotalAmount1[i] := TotalAmount2[i] + VATAmount[i];
            TotalServLine[i]."Line Amount" := TotalAmount1[i] + TotalServLine[i]."Inv. Discount Amount";
          end else begin
            TotalAmount1[i] := TotalServLine[i].Amount;
            TotalAmount2[i] := TotalServLine[i]."Amount Including VAT";
          end;
        end;
    END;

    LOCAL PROCEDURE TotalAmount21OnAfterValidate@19074760();
    BEGIN
      with TotalServLine[1] do begin
        if "Prices Including VAT" then
          "Inv. Discount Amount" := "Line Amount" - "Amount Including VAT"
        else
          "Inv. Discount Amount" := "Line Amount" - Amount;
      end;
      UpdateInvDiscAmount(1);
    END;

    BEGIN
    END.
  }
}

