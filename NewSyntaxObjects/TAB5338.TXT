OBJECT Table 5338 Integration Synch. Job
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160142.950T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Integration Synch. Job;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;GUID          ;CaptionML=ENU=ID }
    { 2   ;   ;Start Date/Time     ;DateTime      ;CaptionML=ENU=Start Date/Time }
    { 3   ;   ;Finish Date/Time    ;DateTime      ;CaptionML=ENU=Finish Date/Time }
    { 4   ;   ;Message             ;Text250       ;CaptionML=ENU=Message }
    { 5   ;   ;Integration Table Mapping Name;Code20;
                                                   TableRelation="Integration Table Mapping".Name;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Integration Table Mapping Name }
    { 6   ;   ;Inserted            ;Integer       ;CaptionML=ENU=Inserted }
    { 7   ;   ;Modified            ;Integer       ;CaptionML=ENU=Modified }
    { 8   ;   ;Deleted             ;Integer       ;CaptionML=ENU=Deleted }
    { 9   ;   ;Unchanged           ;Integer       ;CaptionML=ENU=Unchanged }
    { 10  ;   ;Skipped             ;Integer       ;CaptionML=ENU=Skipped }
    { 11  ;   ;Failed              ;Integer       ;CaptionML=ENU=Failed }
    { 12  ;   ;Synch. Direction    ;Option        ;CaptionML=ENU=Synch. Direction;
                                                   OptionCaptionML=ENU=Bidirectional,ToIntegrationTable,FromIntegrationTable;
                                                   OptionString=Bidirectional,ToIntegrationTable,FromIntegrationTable }
    { 13  ;   ;Job Queue Log Entry No.;Integer    ;CaptionML=ENU=Job Queue Log Entry No. }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=true }
    {    ;"Start Date/Time",ID                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DeleteEntriesQst@1000 : TextConst '@@@="%1 = Integration Synch. Job caption";ENU=Are you sure that you want to delete the %1 entries?';

    [External]
    PROCEDURE DeleteEntries@2(DaysOld@1000 : Integer);
    VAR
      IntegrationSynchJobErrors@1001 : Record "Integration Synch. Job Errors";
    BEGIN
      if not CONFIRM(STRSUBSTNO(DeleteEntriesQst,TABLECAPTION)) then
        exit;
      IntegrationSynchJobErrors.DeleteEntries(DaysOld);
      SETFILTER("Finish Date/Time",'<=%1',CREATEDATETIME(TODAY - DaysOld,TIME));
      DELETEALL;
      SETRANGE("Finish Date/Time");
    END;

    [External]
    PROCEDURE GetErrorForRecordID@1(RecID@1000 : RecordID;VAR IntegrationSynchJobErrors@1001 : Record "Integration Synch. Job Errors") : Boolean;
    VAR
      CRMIntegrationManagement@1002 : Codeunit "CRM Integration Management";
    BEGIN
      if not ISNULLGUID(ID) then
        IntegrationSynchJobErrors.SETRANGE("Integration Synch. Job ID",ID);
      if CRMIntegrationManagement.IsCRMTable(RecID.TABLENO) xor ("Synch. Direction" = "Synch. Direction"::FromIntegrationTable) then
        IntegrationSynchJobErrors.SETRANGE("Destination Record ID",RecID)
      else
        IntegrationSynchJobErrors.SETRANGE("Source Record ID",RecID);
      exit(IntegrationSynchJobErrors.FINDLAST);
    END;

    [External]
    PROCEDURE AreAllRecordsFailed@3() : Boolean;
    BEGIN
      exit(
        (Deleted = 0) and (Inserted = 0) and (Modified = 0) and
        (Unchanged = 0) and (Skipped = 0) and (Failed <> 0));
    END;

    [External]
    PROCEDURE HaveJobsBeenIdle@4(JobQueueLogEntryNo@1000 : Integer) : Boolean;
    BEGIN
      RESET;
      SETRANGE("Job Queue Log Entry No.",JobQueueLogEntryNo);
      if not ISEMPTY then begin
        CALCSUMS(Inserted,Modified,Deleted,Failed);
        exit(Inserted + Modified + Deleted + Failed = 0);
      end;
    END;

    BEGIN
    END.
  }
}

