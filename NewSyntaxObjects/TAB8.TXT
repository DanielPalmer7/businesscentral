OBJECT Table 8 Language
{
  OBJECT-PROPERTIES
  {
    Date=20180425D;
    Time=160053.617T;
    Modified=true;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=ENU=Language;
    LookupPageID=Languages;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 2   ;   ;Name                ;Text50        ;CaptionML=ENU=Name }
    { 6   ;   ;Windows Language ID ;Integer       ;TableRelation="Windows Language";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Windows Language Name");
                                                              END;

                                                   CaptionML=ENU=Windows Language ID;
                                                   BlankZero=true }
    { 7   ;   ;Windows Language Name;Text80       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Windows Language".Name WHERE ("Language ID"=FIELD("Windows Language ID")));
                                                   CaptionML=ENU=Windows Language Name;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Name                                     }
  }
  CODE
  {

    [External]
    PROCEDURE GetUserLanguage@1() : Code[10];
    VAR
      UserLanguageId@1000 : Integer;
      Handled@1001 : Boolean;
    BEGIN
      OnGetUserLanguageId(UserLanguageId,Handled);

      if not Handled then
        UserLanguageId := GLOBALLANGUAGE;

      exit(GetLanguageCode(UserLanguageId));
    END;

    [External]
    PROCEDURE GetLanguageID@2(LanguageCode@1000 : Code[10]) : Integer;
    BEGIN
      CLEAR(Rec);
      if LanguageCode <> '' then
        if GET(LanguageCode) then
          exit("Windows Language ID");
      "Windows Language ID" := GLOBALLANGUAGE;
      exit("Windows Language ID");
    END;

    [External]
    PROCEDURE GetUserSelectedLanguageId@4() : Integer;
    VAR
      UserPersonalization@1000 : Record "User Personalization";
      BlankGuid@1001 : GUID;
      LanguageId@1002 : Integer;
    BEGIN
      UserPersonalization.SETRANGE("User ID",USERID);
      if not UserPersonalization.FINDFIRST then begin
        UserPersonalization.SETRANGE("User ID",BlankGuid);
        if not UserPersonalization.FINDFIRST then;
      end;

      LanguageId := UserPersonalization."Language ID";
      if LanguageId = 0 then
        LanguageId := GLOBALLANGUAGE;

      exit(LanguageId);
    END;

    LOCAL PROCEDURE GetLanguageCode@7(LanguageId@1000 : Integer) : Code[10];
    BEGIN
      CLEAR(Rec);
      SETRANGE("Windows Language ID",LanguageId);
      if FINDFIRST then;
      SETRANGE("Windows Language ID");
      exit(Code);
    END;

    [Integration]
    LOCAL PROCEDURE OnGetUserLanguageId@3(VAR UserLanguageId@1000 : Integer;VAR Handled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

